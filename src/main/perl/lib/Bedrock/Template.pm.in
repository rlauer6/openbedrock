use strict;
use warnings;

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

# provides a mimimal context object for Bedrock
package Faux::Context;

sub new {
  my $class = shift;
  my $outhandle = shift;

  my $self = { @_ };

  bless $self, ref($class) || $class;

  $self->{outhandle} = $outhandle;

  $self;
}

sub print {
  my $self = shift;

  if ( ref($self->{outhandle}) && $self->{outhandle}->can('print') ) {
    $self->{outhandle}->print(@_);
  }
}

sub close {
  my $self = shift;

  if ( ref($self->{outhandle}) && $self->{outhandle}->can('close') ) {
    $self->{outhandle}->close(@_);
  }
}

package Bedrock::Template;

use parent qw/Class::Accessor/;

__PACKAGE__->follow_best_practice();
__PACKAGE__->mk_accessors(qw/text param config/);

use lib '@perl5_pkglibdir@/Bedrock';

use Text::TagX;

use Bedrock::Array;
use Bedrock::BedrockConfig;

use IO::Scalar;
use Scalar::Util qw/reftype/;
use Data::Dumper;

=pod

=head1 PUBLIC

C<Bedrock::Template> - Bedrock templating with Perl

=head1 SYNOPSIS

 my $bedrock_template = Bedrock::Template->new( q{<var $foo>}, foo => "bar" )

 print $bedrock_template->parse;

=head1 DESCRIPTION

Provides a OO Perl interface to Bedrock templating.  Although you
might not get all the "goodies" of Bedrock on the web or Bedrock
shell, you get enough to make templating tempting and fun!

WeE<039>ve all wanted to be able to do this...

 my $message;

 open MESSAGE, "<email_template.roc";
   or die "could not open email template";
 {
  local $/;
  $message = <MESSAGE>;
 }

 close MESSAGE;

 my $customer = $sth->fetchrow_hashref;

 my $bedrock_template = Bedrock::Template->new($message, customer => $customer;

 $message = $bedrock_template->parse;

 my $msg = MIME::Lite->new(
                           From     => 'your_programmer@some_company.com',
                           To       =>  $customer->{email},
			   Subject  => "A notification of some sort...",
			   Type     => 'text/html',
			   Data     => $message
			  );

   $msg->send('smtp', 'localhost');
 }

=head1 DETAILS

The C<Bedrock::Template> class gives you access to Bedrock templating.
You can do most everything using this class that the using Bedrock on
the web or the Bedrock shell environments can do, including using
plugins.  To configure the Bedrock templating environment you can use
the default F<tagx.xml> file or provide a C<Bedrock::Config> object
when you instantiate the class.

=head2 Errors

Errors generated in parsing the template are presented as an exception
object (C<TagX::Error>).  The object has two methods you can use to
determine the error.

  my $text = eval {
    $template->parse;
  };
  
  if ( $@ ) {
    printf("error: %s at line: %d\n", $@->mesg, $@->line);
  }

=head1 METHODS

=head2 new

 new( text, param => value, ...) 

Instantiates a Bedrock templating object.

=over 5

=item text

The Bedrock text to parse.

=item key => value

A list of parameters to use for templating.  The can be scalars, object, or arrays.

=back

 new( options )

Pass a hash reference with the keys listed below.

=over 5

=item text

Bedrock template to parse.

=item param

Hash reference of objects used by your template.

=item CONFIG

This should be a Bedrock configuration object.  The system
configuration object is provided by default (F<tagx.xml>).

=back

=cut

sub new {
  my $class = shift;

  my $self = eval {
    if ( ref($_[0]) && reftype($_[0]) eq 'HASH') {
      $class->SUPER::new(shift);
    }
    else {
      my $text = shift;
      $class->SUPER::new({ text => \$text, param => {@_} });
    }
  };
  
  unless ($self->config) {
    $self->config(Bedrock::Config->new('@libdir@/bedrock/config/tagx.xml'));
  }

  return $self;
}


=pod

=head2 config

Sets or returns the configuration object used by the Bedrock parser.
Typically this is the deserialized version of Bedrock's configuration
file - F<tagx.xml>.  This is passed to Bedrock when your template is
parsed.  If no configuration object is found, the C<parse> method will
use Bedrock's default F<tagx.xml>.

=cut

sub config {
  my $self = shift;

  if ( @_ ) {
    $self->set_config(@_);
  }
  else {
    $self->get_config();
  }
}

=head2 param

 param( [parameters] )

Sets or returns a hash reference containing key/value pairs that
represent data or objects that will be used by the template.

 $self->param({foo => $bar, baz => [1..10], $session});

=cut

sub param {
  my $self = shift;
  
  if (@_) {
    $self->set_param(@_);
  }
  else {
    $self->get_param();
  }
}

=pod

=head2 text

 text( text )
 text( text-ref )

Sets or returns the Bedrock template string to parse.  C<text> can be
either a scalar or a reference to a scalar.  Returns a reference to
the string or undef if no string is currently set.

=cut


sub text {
  my $self = shift;

  if (@_) {
    my $text = shift;
    $self->set_text(ref($text) ? $text : \$text );
  }

  return $self->get_text();
}

=pod

=head2 parse

 parse( [text], param => value, ... )

Returns the results of passing the text and parameters to Bedrock's
parser. Parameters sent by this method are merged with those passed
in the constructor or those passed in the C<param()> method.

Example:

  my %params = (foo => bar, baz => buz);

  my $text =<<eom;
  foos are really <var $foo>
  and baz is really a <var $baz>
  eom

  $bedrock_template = Bedrock::Template->new( $text, %params );

  eval { my $output = $bedrock_template->parse; };

  print "ERROR: $@\n"
    if $@;

=over 5

=item text

Optional text to parse.  Use C<text()> to set text to parse, or you
can pass the text to parse.

=back

=cut

sub parse {
  my $self = shift;
  my $text = shift;
  
  my %args = @_;
  
  $self->text($text) if $text;

  # merge parameters with those passed
  my $param = $self->param() || {};
  map { $param->{$_} = $args{$_} } keys %args;

  my $output = "";
  
  my $ctx = Faux::Context->new(IO::Scalar->new(\$output), CONFIG => $self->config);

  print STDERR Dumper [ $text, $self->text() ];
  
  my $fh = IO::Scalar->new($self->text());
  
  my $tx = Text::TagX->new($fh, $ctx);

  $tx->param(%$param);
 
  my $error = eval {
    $tx->output;
  };

  die Dumper [ $tx->error ] 
    if ( $@ );

  return $output;
}

1;

=pod

=head1 AUTHOR

Rob Lauer - <rlauer6@comcast.net>

=cut
