#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2001, Charles Jones, LLC.
#    Copyright (C) 2023, TBC Development Group, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

package Bedrock::XML;

use strict;
use warnings;

use Bedrock::Constants qw(:chars :booleans);

use Bedrock::XML::Container::Scalar;
use Bedrock::XML::Container::Object;
use Bedrock::XML::Container::Array;

use Data::Dumper;
use English qw(-no_match_vars);
use FileHandle;
use IO::Scalar;
use Scalar::Util qw(reftype openhandle);
use Symbol;

########################################################################
sub start_handler {
########################################################################
  my ( $self, $p, $e, @args ) = @_;

  my $obj = $self->{obj};

  $e = lc $e;

  if ( $e eq 'object' ) {
    my $new_obj = Bedrock::XML::Container::Object->new( $p, @args );
    $obj = $obj ? $obj->new_object( $p, $e, $new_obj ) : $new_obj;
  }
  elsif ( $e eq 'array' ) {
    my $new_array = Bedrock::XML::Container::Array->new( $p, @args );
    $obj = $obj ? $obj->new_array( $p, $e, $new_array ) : $new_array;
  }
  elsif ( $e eq 'scalar' ) {
    if ( !$obj ) {
      $p->xpcroak('<scalar> must be contained in <object> or <array>');
    }

    my $new_scalar = Bedrock::XML::Container::Scalar->new( $p, @args );
    $obj = $obj->new_scalar( $p, $e, $new_scalar );
  }
  else {
    $p->xpcroak("unsupported tag <$e> encountered");
  }

  return $self->{obj} = $obj;
}

########################################################################
sub end_handler {
########################################################################
  my ( $self, @args ) = @_;

  my $obj = $self->{obj};

  my $parent = $obj->end_body(@args);

  if ( defined $parent ) {
    $self->{obj} = $parent;
  }

  return $self->{obj};
}

########################################################################
sub char_handler {
########################################################################
  my ( $self, @args ) = @_;

  return $self->{obj}->text(@args);
}

########################################################################
sub final_handler {
########################################################################
  my ($self) = @_;

  return $self->{obj}->finalize;
}

########################################################################
sub unquote_xml {
########################################################################
  my ($xml) = @_;

  $xml && do {
    $xml =~ s/&lt;/</gxsm;
    $xml =~ s/&gt;/>/gxsm;
    $xml =~ s/&apos;/\'/gxsm;
    $xml =~ s/&quot;/\"/gxsm;
    $xml =~ s/&amp;/\&/gxsm;
  };

  return $xml;
}

########################################################################
sub quote_xml {
########################################################################
  my ($xml) = @_;

  $xml && do {
    $xml =~ s/&/&amp;/gxsm;
    $xml =~ s/</&lt;/gxsm;
    $xml =~ s/>/&gt;/gxsm;
    $xml =~ s/\'/&apos;/gxsm;
    $xml =~ s/\"/&quot;/gxsm;
  };

  return $xml;
}

########################################################################
sub split_opts {
########################################################################
  my ($str) = @_;

  return
    if !length $str;

  local $_ = $str;

  my @options;

  while (length) {
    s/^\s+//xsm and next;

    if (s/^([\w-]+)\s*=\s*//xsm) {
      push @options, $1;
    }
    else {
      die "Invalid option name at: [$_]";
    }

    if (s/^(\x22([^\x22]*)\x22|\x27([^\x27]*)\x27)//xsm) {
      push @options, unquote_xml( $2 || $3 );
    }
    else {
      die "Invalid option value at: [$_]";
    }
  }

  return @options;
}

########################################################################
sub parse {
########################################################################
  my ($self) = @_;

  my $newscan = $TRUE;

  my $input = $self->{input};

  my $optstring = $EMPTY;
  my $text      = $EMPTY;

  my ($element);

  my $tag_start = '^(scalar|object|array)(?=\W)';
  my $tag_end   = '^(/scalar|/object|/array)>';
  my $opt_esc   = '^([^>]*)>';

  INPUT:
  while (<$input>) {
    MAIN: {

      #
      # Check whether we are into a new scan or collecting options
      # for a matched tag
      #
      if ( !$newscan ) {

        # Collecting options for a matched tag -- Look for tag end which is
        # an un-escaped '>'
        if (s/$opt_esc//oxsm) {

          # Found an un-escaped '>', which terminates the current tag and its options
          # $optstring contains the options collected so far
          $optstring .= $1;
          $self->start_handler( $self, $element, &split_opts($optstring) );
          $element = $optstring = $EMPTY;

          # Start a new scan after this
          $newscan = 1;

          # Handle possible left over stuff after the '>'
          redo MAIN;
        }
        else {

          # No terminating '>' in entire line -- start by reading input again
          $optstring .= $_;
          next INPUT;
        }
      }

      # New scan from here -- scan afresh for tags.
      # if no '<' found in line, skip line
      if ( !s/\A(.*?)<//oixsm ) {
        $text .= $_;
        next INPUT;
      }

      # We did find a '<' -- could be we have a possible supported tag next
      # The text before the '<' needs to be processed.
      $text .= $1;

      if ( length $text ) {
        $self->char_handler( $self, $text );
      }

      $text = $EMPTY;

      # Check if we have a supported tag
      if (s/$tag_start//oixsm) {
        $element = $1;

        # After creating object of suitable type depending on the tag, check
        # if the tag ends on the same line. Tag ending is an un-escaped '>'.
        if (s/$opt_esc//oixsm) {

          # The tag definition ended on same line -- all options specified on
          # one line
          $optstring = $1;
          $self->start_handler( $self, $element, &split_opts($optstring) );
          $element = $optstring = $EMPTY;

          # And rescan the string for more tags -- Note that $newscan is unchanged
          # from its value of 1, so we don't need to set it here
          redo MAIN;
        }
        else {

          # Tag definition did not end on one line -- Further input is consumed for
          # looking for the tag ending
          $newscan = 0;

          # Start collection of the options string
          $optstring = $_;
        }

      }
      elsif (s/$tag_end//oixsm) {

        # Check whether we have hit a tag-body-ending tag
        # and if so, end the current object
        $self->end_handler($self);

        # And rescan string for more tags
        redo MAIN;
      }
      elsif (s/^!--//) {

        # Consume comments
        do {
          last if (s/.*?-->//);
        } while (<$input>);

        redo MAIN;
      }
      else {

        # Unsupported tag hit -- croak hard
        die "What is this: <$_>\n";
      }
    }
  }

  return $self->final_handler($self);
}

########################################################################
sub new {
########################################################################
  my ( $class, $file ) = @_;

  my $fh = openhandle($file);

  if ( !$fh ) {
    open $fh, '<', $file  ## no critic (RequireBriefOpen)
      or die "Unable to open $file: $OS_ERROR\n";
  }

  my $xml = eval {
    local $RS = undef;
    return <$fh>;
  };

  die "unable to read XML file\n$EVAL_ERROR"
    if $EVAL_ERROR;

  close $fh;

  return
    if !$xml;

  return $class->newFromString($xml);
}

########################################################################
sub newFromString {  ## no critic (Capitalization)
########################################################################
  my ( $class, $string ) = @_;

  return
    if !$string;

  my $self = bless {}, $class;

  my $fd = gensym;

  $string =~ s/\A\s+//gxsm;

  tie *{$fd}, 'IO::Scalar', \$string;  ## no critic (ProhibitTies)
  $self->{input} = $fd;

  return $self->parse;
}

########################################################################
sub printxml {
########################################################################
  my ( $fd, $id, $ref, $nspaces ) = @_;

  $nspaces //= 0;

  my $spaces = $SPACE x $nspaces;

  if ( reftype($ref) eq 'HASH' ) {
    print {$fd} $spaces, qq(<object $id>\n);

    for my $key ( sort keys %{$ref} ) {
      if ( !$key ) {
        warn "object name cannot be null\n";
        next;
      }

      my $value = $ref->{$key};
      $key = quote_xml($key);

      if ( ref $value ) {
        printxml( $fd, qq(name="$key"), $value, $nspaces + 1 );
      }
      else {
        $value = quote_xml($value);
        print {$fd} $spaces, qq( <scalar name="$key">), $value, "</scalar>\n";
      }
    }

    return print {$fd} $spaces, "</object>\n";
  }
  elsif ( reftype($ref) eq 'ARRAY' ) {
    print {$fd} $spaces, qq(<array $id>\n);

    for my $value ( @{$ref} ) {
      if ( ref $value ) {
        printxml( $fd, $EMPTY, $value, $nspaces + 2 );
      }
      else {
        $value = quote_xml($value);
        print {$fd} $spaces, ' <scalar>', $value, "</scalar>\n";
      }
    }
    return print {$fd} $spaces, "</array>\n";
  }
  else {
    return print {$fd} $EMPTY;
  }
}

########################################################################
sub writeXML {  ## no critic (Capitalization)
########################################################################
  my ( $ref, $file ) = @_;

  if ( defined $file && ref $file ) {
    return printxml( $file, $EMPTY, $ref );
  }

  if ( defined $file && $file ) {
    open my $fh, '>', $file
      or die "Unable to open $file: $OS_ERROR\n";

    my $retval = printxml( $fh, $EMPTY, $ref );

    close $fh;

    return $retval;
  }

  return writeXMLString($ref);
}

########################################################################
sub writeXMLString {  ## no critic (Capitalization)
########################################################################
  my (@args) = @_;

  if ( !ref $args[0] && $args[0] =~ /Bedrock::XML/xsm ) {
    shift @args;
  }

  my $ref = shift @args;

  my $string;

  my $fd = gensym;
  tie *{$fd}, 'IO::Scalar', \$string;  ## no critic (ProhibitTies)

  printxml( $fd, $EMPTY, $ref );

  return $string;
}

########################################################################
sub xpcroak {
########################################################################
  my ( $self, @args ) = @_;

  die "Error encountered: @args";
}

1;

__END__

=pod

=head1 NAME

Bedrock::XML - encode/decode Bedrock XML

=head1 SYNOPSIS

 my $xml = Bedrock::XML::writeXMLString( { foo => bar } );

 Bedrock::XML::writeXML( { foo => bar }, 'foo.xml' );

 my $ref = new Bedrock::XML( '/usr/lib/bedrock/config/tagx.xml' );

 my $ref = Bedrock::XML->newFromString( "<object><scalar name="foo">bar</scalar>");

=head1 DESCRIPTION

Class that implements Bedrock's XML serialization and deserialization
of Perl objects.  Yes, looking in the rear view mirror this may be a
case of reinventing wheel.  In 1999 it was apparently necessary to
create a method to serialize data in an XMLish fashion.  Some
benchmarks have shown that this is no slower than other Perl XML
implementations thus there is no great motivation to change how
Bedrock serializes data.

A Bedrock XML object is the serialized (text) representation of a Perl
hash.  The hash may be an arbitrarily complex collection of the basic
Perl data types (I<scalar>, I<array>, I<hash>).  Bedrock XML presents hashes as
C<E<lt>objectE<gt>> elements, arrays as C<E<lt>arrayE<gt>> elements,
and scalars as C<E<lt>scalarE<gt>> elements.  

For example, given the Perl hash:

 { 
   foo => [ 1, 2, 3],
   bar => "hello world",
   buz => { biz => "goo" }
 } 

...the Bedrock XML representation would look like:

 <object>
  <array name="foo">
    <scalar>1</scalar>
    <scalar>2</scalar>
    <scalar>3</scalar>
  </array>
  <scalar name="bar">hello world</scalar>
  <object name="buz">
    <scalar name="biz">goo</scalar>
  </object>
 </object>

=head1 METHODS AND SUBROUTINES

=head2 new

 new( file | fd )

Reads a Bedrock XML file and returns a Perl hash reference.

=over 5

=item * file

File name of a Bedrock XML file.

=item * fd

Open file descriptor of a Bedrock XML file.

=back

=head2 newFromString

 newFromString( string )

Returns a hash reference to the parsed Bedrock XML string. Returns
undef if there is error parsing the string or the string is empty.

 my $xml <<='END_OF_XML';
 <object>
   <scalar name="foo">test</scalar>
 </object>
 END_OF_XML

 my $ref =  Bedrock::XML->newFromString( $xml );

=head2 writeXML

 writeXML( ref, [ fd | file] )

Returns or writes the serialized Bedrock XML version of an object.

 my $xml = Bedrock::XML::writeXML( $obj );

=over 5

=item * ref

Hash reference representing a Perl object to be serialized.

=item * fd

Open (writable) file descriptor that will be used to write the
serialized output.

=item * file

Name of a file to which the serialized output will be written.

=back

If no file descriptor or file name is given, the serialized string is
returned.

=head2 writeXMLString

 writeXMLString( ref )

Returns the serialized Bedrock XML version of an object.

=head1 AUTHOR

Jay Sridhar

=cut

1;
