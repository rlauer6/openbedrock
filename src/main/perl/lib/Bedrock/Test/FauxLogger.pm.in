package Bedrock::Test::FauxLogger;

# provide a mocked Handler and logger class for testing

use strict;
use warnings;

use Bedrock::Constants qw(:chars);
use IO::Scalar;
use Data::Dumper;

{
  no strict 'refs';  ## no critic

  foreach my $m (qw{ error debug warn fatal info trace }) {
    *{ __PACKAGE__ . "::$m" } = sub {
      my ( $self, @message ) = @_;

      return $self->_log( $m, @message );
    };
  }
}

########################################################################
sub new {
########################################################################
  my ( $class, $level ) = @_;
  $level //= 'info';

  my %log_levels = (
    error => 0,
    warn  => 1,
    info  => 2,
    debug => 3,
    trace => 4,
  );

  my $log_level = $log_levels{ lc $level } // $log_levels{info};

  my $log = $EMPTY;

  my $fh = IO::Scalar->new( \$log );

  my $self = bless {
    log         => \$log,
    fh          => $fh,
    _log_levels => \%log_levels,
    _log_level  => $log_level
  }, $class;

  return $self;
}

########################################################################
sub _log {
########################################################################
  my ( $self, $level, @message ) = @_;

  my $log_level = $self->{_log_levels}->{$level};

  return
    if $log_level > $self->{_log_level};

  my $depth = 0;

  my @stack;

  while ( my @frame = caller $depth++ ) {
    push @stack, \@frame;
  }

  my $caller_package = $stack[2]->[3];
  my $lineno         = $stack[1]->[2];

  my $log_message = sprintf '(%s):[%s] %s', $caller_package, $lineno, join $EMPTY, @message;

  print { $self->{fh} } $log_message, "\n";

  return;
}

########################################################################
sub as_string {
########################################################################
  my ($self) = @_;

  return ${ $self->{log} };
}

########################################################################
sub close {  ## no critic
########################################################################
  my ($self) = @_;

  my $fh = $self->{fh};
  return
    if !$fh;

  $fh->close;

  delete $self->{fh};

  return;
}

DESTROY {
  return shift->close;
}
1;
