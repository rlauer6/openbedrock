package Bedrock::Context;

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2001, Charles Jones, LLC.
#    Copyright (C) 2024, TBC Development Group, LLC.
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

use strict;
use warnings;

use Bedrock qw(:booleans :file);
use Bedrock::Array;
use Bedrock::Hash;
use Bedrock::Constants qw(:all);
use Bedrock::Dump;
use CGI;
use Carp;
use Crypt::CBC;
use Crypt::DES;
use Cwd qw(getcwd abs_path);
use Data::Dumper;
use English qw(-no_match_vars);
use File::Basename;
use File::Temp qw(tempdir);
use IO::File;
use List::Util qw( none pairs uniq );
use Scalar::Util qw( reftype );
use Text::URLEncode;
use Log::Log4perl::Level;

use Role::Tiny::With;
with 'Bedrock::Logger';

use Readonly;

Readonly my $UPLOAD_BUFFER_SIZE => 4 * 1024;
Readonly my $UPLOAD_FILE_PERMS  => oct 777;

our $VERSION = '@PACKAGE_VERSION@';  ## no critic (RequireInterpolationOfMetachars)

use parent qw(Exporter);

our @EXPORT_OK = qw( find_log4perl_conf init_log4perl_logger parse_cookie);

{
  no strict 'refs';  ## no critic

  foreach my $sub (qw(request response redis output_vars)) {
    *{ __PACKAGE__ . "::$sub" } = sub {
      my (@args) = @_;
      return _set( @args, "_$sub" );
    };
  }

  foreach my $sub (qw(config input session header output_handler)) {
    *{ __PACKAGE__ . "::$sub" } = sub {
      my (@args) = @_;
      return _set( @args, uc $sub );
    };
  }
}

########################################################################
sub new {
########################################################################
  my ( $class, @args ) = @_;

  my $options = ref $args[0] ? $args[0] : {@args};

  foreach ( keys %{$options} ) {
    $options->{ lc $_ } = $options->{$_};  # just lc everything please
  }

  my ( $config, $config_file ) = @{$options}{qw(config config_file)};

  my ($request_name) = grep { $options->{$_} } qw(apache_handler request_handler request);

  my $request = $options->{ $request_name // $EMPTY };

  die "usage: Bedrock::Context->new( config => config, request => request )\n"
    if !$request || !$config;

  my $self = bless {}, $class;

  # Populate the configuration object...note that if the caller is
  # Apache::Bedrock, then the CONFIG object is most likely the fully
  # merged Bedrock site configuration. There may not be a use case for
  # calling Bedrock::Context with anything but Bedrock's configuration
  # object.

  $config = eval {
    return $config
      if $config;

    return Bedrock::Config->new($config_file);
  };

  croak "ERROR: could not create a configuration object\n$EVAL_ERROR"
    if !$config || $EVAL_ERROR;

  $self->config($config);

  $self->request($request);

  $self->redis( $options->{redis} );

  if ( $options->{output_handler} ) {
    $self->output_handler( $options->{output_handler} );
    $self->response( $options->{output_handler} );
  }
  else {
    $self->output_handler($request);
    $self->response( $self->request );
  }

  init_log4perl_logger();

  return $self;
}

########################################################################
# convience logging methods that you might NOT want to use if you
# want to see the real file and line number of log messages!!
########################################################################

{
  ## no critic (RequireArgUnpacking)
  sub debug { return shift->logger->debug(@_); }
  sub info  { return shift->logger->info(@_); }
  sub warn  { return shift->logger->warn(@_); }  ## no critic (ProhibitBuiltinHomonyms)
  sub error { return shift->logger->error(@_); }
}

########################################################################
sub find_log4perl_conf {
########################################################################
  my (%options) = @_;

  my $config_paths = $options{path_list};

  my $file = $options{file} // 'log4perl.conf';

  # At one point we added `getcwd` to the list of paths to search.
  # This is a really bad idea since we don't know where the
  # current working directory is when this is being called. We may end
  # up doing a really deep search. Accordingly, we restrict the search
  # paths to the specified configuration directories. Alternatively,
  # the configuration variable LOG4PERL can be used to explicitly call
  # out the file.

  if ( !$config_paths ) {
    my @paths = split /:/xsm, $ENV{CONFIG_PATH} // $EMPTY;

    push @paths, grep {defined} $ENV{BEDROCK_CONFIG_PATH}, $DEFAULT_BEDROCK_CONFIG_PATH;

    $config_paths = [ uniq map { abs_path $_ } grep {$_} @paths ];
  }

  # filter out non-existent paths
  $config_paths = [ grep { $_ && -d $_ } @{$config_paths} ];

  return
    if !@{$config_paths};

  my ($path) = find_in_path(
    find_one  => $TRUE,
    path_list => $config_paths,
    file      => $file,
  );

  return $path;
}

########################################################################
# Usage      : init_log4perl_logger(log4perl_conf)
# Purpose    : initializes Log::Log4perl logging
# Parameters : log4perl_conf - reference to a text object representing
#              Log::Log4perl configuration or the name of a file that
#              contains the configuration
# Returns    : returns a boolean that indicates the success or failure of
#              intializing Log::Log4perl
# Errors     : none
########################################################################
sub init_log4perl_logger {
########################################################################
  my ($log4perl_conf) = @_;

  my $initialized = Log::Log4perl->initialized;

  return $initialized
    if $initialized;

  my $log4perl_conf_file;

  if ( !$log4perl_conf ) {
    $log4perl_conf_file = find_log4perl_conf;
  }

  return $initialized
    if !$log4perl_conf && !$log4perl_conf_file;

  if ( !ref $log4perl_conf && -e $log4perl_conf_file ) {
    my $log4perl_conf_text = slurp_file $log4perl_conf_file;

    $log4perl_conf = \$log4perl_conf_text;
  }

  $initialized = eval { return Log::Log4perl->init($log4perl_conf); };

  if ( !$initialized ) {
    print {*STDERR} sprintf
      "WARNING: could not initialize Log::Log4perl using %s\n%s",
      ${$log4perl_conf}, $EVAL_ERROR;
  }

  my $logger = Log::Log4perl->get_logger;

  $logger->debug(
    sprintf 'Log::Log4perl logging initialized using: %s',
    $log4perl_conf_file ? $log4perl_conf_file : ' passed configuration'
  );

  return $initialized;
}

# rewrite this to use hooks or mod_perl, if rewritten for mod_perl we
# need keep this in mind:

=pod

Re: Uploading Files bigger the 64M
Ryan Gies Tue, 25 Jan 2011 10:41:17 -0800

1) You may lower the limit, not raise it. If you are trying to raise
it, you will get "Conflicting information".

2) Your server config trumps your virtual-host config. If you have not
set APREQ2_ReadLimit outside of your VirtualHost, then it is set for
you (64M).

Personally, I do this:

A) Set APREQ2_ReadLimit in /etc/httpd/conf.d/apreq.conf to the largest
value I would allow for any virtual host.

B) Set APREQ2_ReadLimit to the largest upload value appropriate for
the client inside their VirtualHost container.

C) Set $req->read_limit inside a PerlResponseHandler depending on the
logical type of request:

# The Apache request library only allows lowering the read limit
# (POST_MAX). # The default is 64M (64 * 1024 * 1024) so if larger
# uploads are desired, one # must raise the limit by setting
# APREQ2_ReadLimit in the Apache configuration. # When using
# VirtualHosts, the larger limit *must* be specified in server
# configuration, and then optionally reduced in the vhost config.
# http://marc.info/?l=apreq-dev&m=115829354028472&w=2

  my $limit = $req->read_limit();
  my $max = $rr->max_post_size; # the max post size allowed for the current request
  if ($max > $limit) {
    $Log->warn(sprintf('Cannot raise read_limit from %d to %d', $limit, $max));
  }
   else {
    $req->read_limit($max) or die $!;
    $limit = $req->read_limit();
    $Log->error('Error setting read_limit') unless $limit == $max;
  }

=cut

#
########################################################################
sub upload_file {
########################################################################
  my ( $self, $form_var, @args ) = @_;

  my $fh = $self->cgi->param($form_var);

  my $platform = $self->cgi->param('platform');

  die 'upload_file upload error: ', $self->cgi->cgi_error()
    if !$fh;

  my $options = is_hash( $args[0] ) ? $args[0] : {@args};

  $options->{path} //= $self->config->{UPLOAD_PATH} // tempdir( CLEANUP => 1 );

  $self->debug( 'upload_file options: ' . Bedrock::Dump::compact($options) );

  my $upload_content_type = $self->cgi->uploadInfo($fh)->{'Content-Type'};

  $options->{'content-type'} //= $options->{'Content-type'};

  # check to see if this is a valid mime-type to be uploaded
  if ( $options->{'content-type'} ) {

    my $content_type = $options->{'content-type'};

    my @content_types;

    push @content_types, is_array($content_type) ? @{$content_type} : $content_type;

    $self->debug(
      sub {
        return sprintf 'upload_file acceptable content-types: %s', join $COMMA, @content_types;
      }
    );

    if ( none { $_ eq $upload_content_type } @content_types ) {
      my $err = sprintf 'upload_file content_type error: uploading (%s), must be one of (%s)',
        $upload_content_type,
        join $COMMA, @content_types;
      die $err;
    }
  }

  # if the file came from a WinDoze machine...set the parsing appropriately
  if ( $platform && $platform =~ /win/xsmi
    || $self->cgi->param($form_var) =~ /^[[:lower:]]:.*$/ixsm ) {

    fileparse_set_fstype('MSWin32');
  }

  my ( $name, $path, $ext )
    = fileparse( scalar $self->cgi->param($form_var), qr/[.][^.]*/xsm );

  my $upload_path = $options->{path};

  if ( !-d $upload_path ) {
    die "unable to create path: $upload_path\n"
      if !mkdir $upload_path, $UPLOAD_FILE_PERMS;
  }

  if ( !-w $upload_path ) {
    die "unable to write to path: $upload_path\n";
  }

  my $upload_filename = $options->{filename} || ( $name . $ext );

  my $uploaded_file = sprintf '%s/%s', $upload_path, $upload_filename;

  $self->info( sprintf "upload_file processing: file => [%s], content-type => [%s]\n",
    $uploaded_file, $upload_content_type );

  my $maxsize = $options->{maxsize};

  # allow 1k, 1m, etc.
  if ($maxsize) {
    if ( $maxsize =~ /^(\d+)k/xsmi ) {
      $maxsize = $1 * 1024;
    }

    if ( $maxsize =~ /^(\d+)m/xsmi ) {
      $maxsize = $1 * 1024 * 1024;
    }
  }

  my $cipher;

  if ( $options->{passkey} ) {
    $options->{cipher} = $options->{cipher} || 'DES';

    $cipher = Crypt::CBC->new(
      -cipher => $options->{cipher},
      -key    => $options->{passkey}
    );

    $cipher->start('encrypting');
    $uploaded_file .= '.enc';
  }

  open my $tmp_fh, '>', $uploaded_file
    or die sprintf 'could not open %s for writing', $uploaded_file;

  my $size;

  while ( my $bytesread = read $fh, my $buffer, $UPLOAD_BUFFER_SIZE ) {
    $size += $bytesread;

    die "File size limit reached.\n"
      if $maxsize && $size >= $maxsize;

    $self->debug( sprintf 'upload_file bytes read: %d', $bytesread );

    if ($cipher) {
      print {$tmp_fh} $cipher->crypt($buffer);
    }
    else {
      print {$tmp_fh} $buffer;
    }
  }

  if ($cipher) {
    print {$tmp_fh} $cipher->finish;
  }

  close $tmp_fh;

  return if !-s $uploaded_file;

  my %stats;

  my @file_info = stat $uploaded_file;

  @stats{qw(dev ino mode nlink uid gid rdev size atime mtime ctime blksize blocks)} = @file_info;

  %stats = (
    %stats,
    ( file           => $uploaded_file,
      $form_var      => "$name$ext",
      'content-type' => $upload_content_type,
      cipher         => $options->{cipher} // $EMPTY,
    )
  );

  return Bedrock::Hash->new(%stats);
}

########################################################################
sub cgi {
########################################################################
  my ( $self, @args ) = @_;

  if (@args) {
    $self->{cgi} = $args[0];
  }

  if ( !$self->{cgi} ) {
    $self->{cgi} = CGI->new;
  }

  return $self->{cgi};
}

########################################################################
# Usage      : getInputValue(names)
#            : getInputValue()
# Purpose    : returns array of input values or the names
# Parameters : names : list of input variable names
# Returns    : list of values or the list of variable names
# Errors     : none
########################################################################
sub getInputValue {  ## no critic (Capitalization)
########################################################################
  my ( $self, @args ) = @_;

  if ( !defined $self->input ) {

    # Collect the input

    # - a mod_perl 1.x -> 2.x gotcha... $r->args, $r->content in array
    # context no longer supported, does not return parsed results.
    # Replaced with Apache::Request::CGI args/contents although I have a
    # feeling there is a more better faster way to do this...
    # my @args = ($self->args, $self->content);

    my @args = $self->cgi->param;

    my $input = {};

    foreach my $name (@args) {
      my @values = $self->cgi->multi_param($name);

      if ( @values > 1 ) {
        $input->{$name} = Bedrock::Array->new(@values);
      }
      else {
        $input->{$name} = $values[0];
      }
    }

    $self->input($input);
  }

  if ( @args > 0 ) {
    # Have some parameters -- need the values
    return @{ $self->input }{@args};
  }
  else {
    # No parameters -- need the names of all parameters
    return keys %{ $self->input };
  }
}

#
# Close method is used when CGI wants to close browser connection and go off
# to do something else and not keep the user waiting
#
########################################################################
sub close {  ## no critic (ProhibitBuiltinHomonyms, ProhibitAmbiguousNames)
########################################################################
  my ($self) = @_;

  return $self->output_handler->close;
}

########################################################################
sub print {  ## no critic (ProhibitBuiltinHomonyms)
########################################################################
  my ( $self, @args ) = @_;

  return $self->output_handler->print(@args);
}

########################################################################
sub cgi_header_in {
########################################################################
  my ($self) = @_;

  my $r = $self->apache_handler;

  my $headers = $r->headers_in;
  $headers //= {};

  return %{$headers};
}

sub headers_out { goto &cgi_header_out; }

########################################################################
sub cgi_header_out {
########################################################################
  my ( $self, @headers ) = @_;

  my $r = $self->apache_handler;

  # mod_perl 1/2
  return ref($r) =~ /Apache2/xsm
    ? $r->headers_out->add(@headers)
    : $r->header_out(@headers);
}

########################################################################
sub send_http_header {
########################################################################
  my ( $self, @headers ) = @_;

  my $r = $self->apache_handler;

  return $r->send_http_header;
}

########################################################################
sub getOutputValue {  ## no critic (Capitalization)
########################################################################
  my ( $self, @args ) = @_;

  return if !defined $self->output_vars;

  if ( @args == 1 ) {
    return $self->output_vars->{ $args[0] };
  }
  elsif (@args) {
    if ( !wantarray ) {
      confess 'Scalar context is not valid here: should be array context only';
    }

    return @{ $self->output_vars }{@args};
  }
  else {
    if ( !wantarray ) {
      confess 'Scalar context is not valid here: should be array context only';
    }
    return keys %{ $self->output_vars };
  }
}

########################################################################
sub setOutputValue {  ## no critic (Capitalization)
########################################################################
  my ( $self, @args ) = @_;

  if ( !defined $self->output_vars ) {
    $self->output_vars = {};
  }

  for my $p ( pairs @args ) {
    my ( $key, $value ) = @{$p};
    $self->output_vars->{$key} = $value;
  }

  return $self;
}

########################################################################
sub getConfigValue {  ## no critic (Capitalization)
########################################################################
  my ( $self, @args ) = @_;

  if ( @args > 0 ) {
    return wantarray ? @{ $self->config }{@args} : $self->config->{ $args[0] };
  }
  else {
    return keys %{ $self->config };
  }
}

sub getCookieValue { goto &get_cookie_value; }

########################################################################
sub get_cookie_value {
########################################################################
  my ( $self, @args ) = @_;

  my $r = $self->apache_handler;

  my $raw_cookie = $r->headers_in->{Cookie} // $ENV{HTTP_COOKIE};
  $raw_cookie //= $EMPTY;

  return
    if !$raw_cookie;

  if ( !exists $self->{__cookies__} ) {
    $self->{__cookies__} = parse_cookie($raw_cookie);
  }

  my %cookie_jar = %{ $self->{__cookies__} };

  return sort keys %cookie_jar
    if !@args;

  return $cookie_jar{ $args[0] }
    if @args == 1;

  return @cookie_jar{@args};
}

########################################################################
sub parse_cookie {
########################################################################
  my ($raw_cookie) = @_;

  my %cookie_values;

  for my $cookie ( split /[;]\s?/xsm, $raw_cookie ) {
    $cookie =~ s/^\s*(.*?)\s*$/$1/xsm;

    my ( $key, $value ) = split /=/xsm, $cookie;

    $cookie_values{$key} = Text::URLEncode::Decode($value);
  }

  return \%cookie_values;
}

sub setCookieValue { goto &set_cookie_value; }

########################################################################
sub set_cookie_value {
########################################################################
  my ( $self, $cookie ) = @_;

  die q{something's wrong, no HEADER object defined in the context object!}
    if !$self->header;

  return $self->header->set( 'Set-Cookie', $cookie );
}

########################################################################
sub logger {
########################################################################
  my ($self) = @_;

  return $self->get_logger;
}

########################################################################
sub apache_handler { goto &request; }
########################################################################

# deprecated
########################################################################
sub set_header { goto &header; }
########################################################################

########################################################################
sub _set {
########################################################################
  my ( $self, @args ) = @_;

  my $key;
  my $value;

  if ( @args > 1 ) {
    ( $value, $key ) = @args;
    $self->{$key} = $value;
  }
  else {
    $key = $args[0];
  }

  return $self->{$key};
}

1;

__END__

=pod

=head1 PUBLIC

Bedrock::Context - provides contextual methods for Bedrock applications

=head1 SYNOPSIS

 use Bedrock::Context;

 my $ctx = Bedrock::Context->new( CONFIG => $config, REQUEST_HANDLER => $handler );

=head1 DESCRIPTION

The context object handles the task of gathering input and writing
output based on the context (web, cli) of the application.

=head1 METHODS AND SUBROUTINES

=head2 new

 new( args )

Constructor for context object.  Returns the created object.

Mandatory arguments are as follows:

=over 5

=item REQUEST_HANDLER

A reference to an object that provides a standard set of methods for
interacting with HTTP requests.

=item OUTPUT_HANDLER

Output handler supporting at least a `print()' method. Defaults to
REQUEST_HANDLER if not specified.

=item CONFIG

Hash containing configuration information. This is usually a
deserialized representation of F<tagx.xml> if this module is being
used as part of the Bedrock application. It does not necessarily have
to contain Bedrock configuraiton information. See L</NOTES> below.

=item CONFIG_FILE

Name of a configuration file containing a serialized representation
(usually Bedrock XML) of configruation values. Configuration files can
be represented as Bedrock XML (F<.xml>), JSON (F<.json>), or YAML
(F<.yaml>) files.

=back

=head2 print

 print( list );

Sends the array of output to the destination.

=head2 upload_file

 upload_file(form-var-name, options ... | {})

Upload a file to the web server. Returns a hash containing metadata about the file.

  @stats{qw(dev ino mode nlink uid gid rdev size atime mtime ctime blksize blocks)} = @file_info;

  %stats = (
    %stats,
    ( file           => $uploaded_file,
      $form_var      => "$name$ext",
      'content-type' => $upload_content_type,
      cipher         => $options->{cipher} // $EMPTY,
    )
  );

=over 5

=item dev, ino, mode, nlink, uid, gid, rdev, size, atime, mtime, ctime, blksize, blocks

Stats from C<stat>

=item file

Path to the uploaded file

=item $form_var

Name of the file

=item cipher

The cipher used (if any) to encrypt the file.

=back

C<options> is a hash reference or a hash of options described below.

=over 5

=item path

Path where the file should be stored. Looks for C<UPLOAD_PATH> in the
configuration object. If no UPLOAD_PATH is specified, a temporary
directory will be created and the file will be update there. The
return value will be the fully qualified path to the file.

=item 'Content-Type'

Scalar containing the allowable mime-type or an array of allowable mime-types.

=item filename

Name of the file.

=item maxsize

Maximum size to accept.  Value can be expressed as in M[B] or K[B].

=item passkey

Encryption passkey.

=item cipher

Encryption cipher (defaults to DES)

=back

=head1 SETTERS/GETTERS

These will set or get the value associated with the name of the
method. e.g. C<output_handler()> returns the value of the
OUTPUT_HANDLER key.

=head2 apache_handler

The Apache request handler.

=head2 cgi

 cgi()

Returns a CGI object.

=head2 cgi_header_in

 cgi_header_in()

Reads the headers.

=head2 cgi_header_out

 cgi_header_out( headers )

Sends the headers to the header cache for output.

=head2 config

The Bedrock configuration object.

=head2 getConfigValue

 getConfigValue( key || key-list )

Method returns a config parameter value. Used to hide the config
object from the public.

=head2 getCookieValue

See L</get_cookie_value>

=head2 get_cookie_value

 get_cookie_value( cookie-name, [ cookie-name, ...] )

=head2 getInputValue

 getInputValue( [name, ...] )

Method to get input parameter(s)/value(s). Invoked either with

- A list of parameter names. Returns the value of the parameters requested

- No args. Returns the names of all parameters in the input

=head2 getOutputValue

 getOutputValue( list )

Returns a list of output values or a list of keys if no list is given.

=head2 header

The header object (typically C<BLM::Startup::Header>).

=head2 input

Hash of CGI values.

=head2 output_handler

Output handler object.

=head2 output_vars

Hash of output values.

=head2 parse_cookie

 parse_cookie(cookie-value)

Returns a hash of key value pairs.

=head2 session

Session object if available.

=head2 send_http_headers

Outputs the headers.

=head2 setCookieValue

See L<set_cookie_value>

=head2 set_cookie_value

 set_cookie_value( cookie-name, value )

Set a cookie.

=head2 setOutputValue

 setOutputValue(key, value)

=head1 SEE ALSO

L<Bedrock::BedrockConfig>, L<Bedrock::Handler>

=head1 AUTHOR

J. Sridhar

Rob Lauer - <bigfoot@cpan.org>

=cut
