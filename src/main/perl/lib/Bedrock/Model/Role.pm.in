package Bedrock::Model::Role;

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

use strict;
use warnings;

use Role::Tiny;

use Bedrock::Model;
use BLM::DBHandler qw(create_table_name);

use Carp;
use Data::Dumper;
use English qw(-no_match_vars);

########################################################################
sub _create_model {
########################################################################
  my ( $self, @args ) = @_;

  return $self->_model(@args)->create_table();
}

########################################################################
sub _migrate_model {
########################################################################
  my ( $self, @args ) = @_;

  return $self->_model(@args)->migrate_model;
}

########################################################################
sub _verify_model {
########################################################################
  my ( $self, @args ) = @_;
  return $self->_model(@args)->verify_model;
}

########################################################################
sub _rename_field {
########################################################################
  my ( $self, $dbi, @args ) = @_;

  return $self->_model($dbi)->rename_field(@args);
}

########################################################################
sub _add_field {
########################################################################
  my ( $self, $dbi, @args ) = @_;

  my $model = $self->_model($dbi);
  $model->add_field(@args);

  my $migration = $model->verify_model;

  if ($migration) {
    $migration->set_dry_run(0);
    $model->set_migration($migration);
  }

  return $model;
}

########################################################################
sub _model_def {
########################################################################
  my ( $class, %args ) = @_;

  $class = ref $class ? ref $class : $class;

  my ( $model, $verbose ) = @args{qw(model verbose)};

  if ($verbose) {
    print {*STDERR} "checking if ${class}" . "::MODEL exist...\n";
  }

  # set model
  if ($model) {
    no strict 'refs';  ## no critic

    ${ $class . '::MODEL' } = $model;

    return $model;
  }

  # get model
  {
    no strict 'refs';  ## no critic

    $model = *{ $class . '::MODEL' };

    if ($verbose) {
      print {*STDERR} Dumper(
        [ class => $class,
          model => $model,
        ]
      );
    }

    return ${$model}
      if $model && ${$model};

    # if there is no $MODEL defined in the current namespace, see if
    # there is a model (.mdl) file we can require
    $model = eval { require lc table_name($class) . '.mdl'; };

    if ( $EVAL_ERROR && $verbose ) {
      print {*STDERR} "$EVAL_ERROR";
    }

    if ($model) {
      ${ $class . '::MODEL' } = $model;
    }
  }

  return $model;
}

########################################################################
sub _model {
########################################################################
  my ( $class, $dbi, %args ) = @_;

  $class = ref $class ? ref $class : $class;

  my $model = delete $args{model};
  $model //= $class->_model_def;

  die 'no model!'
    if !$model;

  my $table = delete $args{table};

  return Bedrock::Model->new(
    { table => $table // table_name($class),
      model => $model,
      dbi   => $dbi,
      %args
    }
  );
}

########################################################################
sub table_name {
########################################################################
  my ($class) = @_;

  return create_table_name($class);
}

1;
