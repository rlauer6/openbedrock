package Bedrock::Model::Handler;

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2026, TBC Development Group, LLC.
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

use strict;
use warnings;

# --------------------------------------------------------------------
# NOTE:
#
# The two modules below are not directly used in this module. They
# should however, be included so that other modules that subclass this
# module in order to create and manage tables from Bedrock::Model do
# not need to include them.
# --------------------------------------------------------------------

use BLM::DBHandler;
use Bedrock::Constants qw(:booleans);
use Bedrock::Hash;
use Bedrock::Model::Field;
use Bedrock::Model;

use List::Util qw(pairs);
use Scalar::Util qw(blessed);

use Role::Tiny::With;
with 'Bedrock::Model::Role';

use parent qw(BLM::IndexedTableHandler);

__PACKAGE__->create_accessors(qw(model));

our @EXPORT_OK = qw(easy_connect);

########################################################################
sub new {
########################################################################
  my ( $class, @args ) = @_;

  $class = ref($class) || $class;

  my $self = $class->SUPER::new(@args);

  $self->define_model;

  return $self;
}

########################################################################
sub define_model {
########################################################################
  my ( $self, $model ) = @_;

  $model //= $self->_model_def();

  die "no model definition\n"
    if !$model;

  $self->set_model($model);

  return $self;
}

########################################################################
sub easy_connect {
########################################################################
  return BLM::DBHandler::easy_connect(@_);
}

########################################################################
sub migration {
########################################################################
  my ( $self, $dbi ) = @_;
  $dbi //= $self->dbi;

  die "no dbi object available\n"
    if !$dbi;

  return $self->_verify_model($dbi);  # returns a migration object
}

########################################################################
sub create_table {
########################################################################
  my ( $self, @args ) = @_;

  return $self->create_model(@args)
    if @args;

  my $class = ref $self ? ref $self : $self;

  my ($table) = $class =~ /::([^:]+)$/xsm;

  return Bedrock::Model::_create_table(
    table    => lc $table,
    model    => $class->_model_def,
    describe => $TRUE,
  );
}

########################################################################
sub add_field { goto &_add_field; }
########################################################################

########################################################################
sub create_model {
########################################################################
  my ( $self, $dbi, %options ) = @_;

  my ( $table, $model ) = @options{qw(table model)};

  die "usage: create_model(dbi, table => table, model => model)\n"
    if !$dbi;

  $model = ref $model && blessed $model && $model->can('get_model') ? $model->get_model : $model;

  $self->_create_model( $dbi, $model ? ( model => $model ) : (), $table ? ( table => $table ) : (), );

  my $handler = $self->new( $dbi, { table => $table } );

  $handler->define_model($model);

  return $handler;
}

########################################################################
sub _set {
########################################################################
  my ( $self, @args ) = @_;

  my $model = $self->get_model;

  foreach my $p ( pairs @args ) {
    my ( $f, $v ) = @{$p};

    if ( exists $model->{$f}->{set} ) {
      $self->{$f} = $model->{$f}->{set}->( $f, $v );
    }
    else {
      $self->SUPER::_set( $f, $v );
    }
  }

  return;
}

########################################################################
sub _get {
########################################################################
  my ( $self, $f ) = @_;

  my $v = $self->{$f};

  my $model = $self->get_model;

  if ( exists $model->{$f}->{get} ) {
    return $model->{$f}->{get}->( $f, $v );
  }
  else {
    return $self->SUPER::_get($f);
  }
}

1;

## no critic (RequirePodSection)

__END__

=pod

=head1 NAME

Bedrock::Model::Handler - table handler for use with Bedrock::Model

=head1 SYNOPSIS

 package MyApp::Users;

 use parent qw(Bedrock::Model::Handler);

 our $MODEL = Bedrock::Hash->new(
    id => Bedrock::Model::Field->new(
           { 
            field => 'id',
            type  => 'int',
            extra => 'auto_increment',
            key  => 'pri'
           },
    username => Bedrock::Model::Field->new(
           { 
            field => 'foo',
            type  => 'varchar(32)',
            null  => 'no',
           },
    password => Bedrock::Model::Field->new(
           { 
            field => 'foo',
            type  => 'varchar(32)',
            null  => 'no'
           },
    email => new Bedrock::Model::Field(
           { 
            field => 'foo',
            type  => 'varchar(32)',
            null  => 'no'
           },
  );

 1;

 MyApp::Users->create_model($dbi);

 my $users = MyApp::Users->new($dbi);

 $users->set(username => 'admin',
             password => $password,
             email    => 'admin@example.org'
           );

 my $id = $users->save();

 my $new_record = $users->new($dbi, $id);

=head1 DESCRIPTION

Implements an ORM based on L<BLM::IndexedTableHandler> for MySQL
tables.  Extends L<BLM::IndexedTableHandler> to encapsulate the table
definition and maintain consistency between the class that I<handles>
table operations and the database (via migrations).

This class is designed to be the parent class for your table classes.

See L<BLM::IndexedTableHandler> for details regarding the capabilities
of the ORM.

See L<Bedrock::Model> for details on how to create tables and
migrations.

=head1 METHODS AND SUBROUTINES

C<Bedrock::Model::Handler> subclasses C<BLM::IndexedTableHandler>.

=head2 new

See L<BLM::IndexedTableHandler>

=head2 easy_connect

See L<BLM::DBHandler/easy_connect>

=head2 create_table

 create_table(dbi, table => table, model => model)

Creates a new database table based on the model provided. Returns a
new instance of L<Bedrock::Model::Handler>.

 use Bedrock::Model::Field qw(%Id %Email varchar_field);
 Bedrock::Model::Field::RETURN_FIELDS = 1;

 my $handler = Bedrock::Model::Handler->create_table(
   $dbi,
   table => 'foo',
   model => [ %Id, varchar_field('name', 64), %Email ],
 );
    
=over 5

=item table

Name of the table to create.

=item model

Either a C<Bedrock::Model> instance or a hash or array of
C<Bedrock::Model::Field> objects.

=back

=head1 AUTHOR

Rob Lauer - <bigfoot@cpan.org>

=head1 SEE ALSO

L<BLM::IndexedTableHandler>, L<Bedrock::Model>,
L<Bedrock::Model::Field>

=cut
