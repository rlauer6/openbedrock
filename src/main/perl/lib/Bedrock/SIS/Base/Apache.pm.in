#!/usr/local/bin/perl -w

# ident "@(#) $Header$"
# ident "@(#)        $Name$"

package SIS::Base::Apache;

use strict;

BEGIN
{
    use Exporter ();
    use vars	qw ($VERSION @ISA);
    $VERSION	= do { my @r = (q$Revision$ =~ /\d+/g); sprintf "%d."."%02d" x $#r, @r };
}

use SIS::Base;
use SIS::Config;

@ISA	= qw (SIS::Base);

#
# Overridden method to get input parameter(s)/value(s)
# Invoked either with 
#    1. a list of parameter names. Returns the value of the
#       parameters requested
#    2. No args. Returns the names of all parameters in the
#       input
sub getInputValue
{
    my $self = shift;
    if ( @_ > 0 ) {
	# Have some parameters -- need the values
	return @{$self->{'INPUT'}}{@_};
    } else {
	# No parameters -- need the names of all parameters
	return keys %{$self->{'INPUT'}};
    }
}

#
# Constructor for Apache modules
# Returns the created object
# Mandatory Arguments are as follows:
#    CONFIG_FILE	=> Name of the config file
#    APACHE_HANDLER	=> Apache handler object
#
sub new
{
    my $class = shift;
    my %args = @_;

    die "$class->new(): CONFIG_FILE parameter must be specified."
	unless $args{'CONFIG_FILE'};

    die "$class->new(): APACHE_HANDLER parameter must be specified."
	unless $args{'APACHE_HANDLER'};

    my $self = bless {}, $class;
    for my $name (qw (CONFIG_FILE APACHE_HANDLER)) {
	$self->{$name} = $args{$name};
    }
    $self->{'OUTPUT'} = $args{APACHE_HANDLER};

    # Initialize the configuration
    $self->{'CONFIG'} = new SIS::Config ( $self->{'CONFIG_FILE'} );

    # Collect the input
    my $r = $self->{'APACHE_HANDLER'};
    my @args = ($r->args, $r->content);
    my ($name, $value);
    my $input = {};
    while (($name, $value) = splice @args, 0, 2) {
	if ( exists $input->{$name} ) {
	    $input->{$name} = [$input->{$name}] unless &isa( $input->{$name}, 'ARRAY' );
	    push @{$input->{$name}}, $value;
	} else {
	    $input->{$name} = $value;
	}
    }
    $self->{'INPUT'} = $input;

    $self;
}

sub cgi_header_out
{
    my $self = shift;
    $self->{'APACHE_HANDLER'}->cgi_header_out( @_ );
}

sub send_http_header
{
    my $self = shift;
    $self->{'APACHE_HANDLER'}->send_http_header;
}

1;

#
# Name of Release: $Name$
# $Log$
# Revision 1.2  2000/07/06 18:32:58  sridhar
# Added methods cgi_header_out(), print() and send_http_header() which
# invoke the corresponding methods of the apache request object.
#
# Revision 1.1  2000/06/09 17:51:07  sridhar
# Base module for all Apache applications, providing same functionality
# as SIS::Base for CGIs
#
#
