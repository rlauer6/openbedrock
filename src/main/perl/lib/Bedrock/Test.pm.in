#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2024, TBC Development Group, LLC.
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

use strict;
use warnings;

########################################################################
package Faux::ApacheHandler;
########################################################################

use Data::Dumper;
use English qw(-no_match_vars);

our %LOG_LEVELS = (
  error => 0,
  warn  => 1,
  info  => 2,
  debug => 3,
  trace => 4,
);

{
  no strict 'refs';  ## no critic

  for my $level (qw(error info trace warn debug)) {
    *{ 'Faux::ApacheHandler::' . $level } = sub {
      my ( $self, @message ) = @_;

      return $self->_log( $level, @message );
    };
  }
}

our $HANDLER;

########################################################################
sub new {
########################################################################
  my ( $class, $level ) = @_;

  return $HANDLER
    if $HANDLER;

  my $self = bless {}, $class;
  $HANDLER = $self;

  $self->{_log_level} = $LOG_LEVELS{ $level // 'info' } // $LOG_LEVELS{info};

  my $logfile = sprintf 'bedrock-%s.log', $PID;

  my $fh;

  open $fh, '>', $logfile  ## no critic (RequireBriefOpen)
    or die "could not open $logfile\n";

  $self->{_fh} = $fh;

  $self->{_log_file} = $logfile;

  return $self;
}

########################################################################
sub _log {
########################################################################
  my ( $self, $level, @message ) = @_;

  my $log_level = $LOG_LEVELS{$level};

  return
    if $log_level > $self->{_log_level};

  my $depth = 0;
  my @stack;

  while ( my @frame = caller $depth++ ) {
    push @stack, \@frame;
  }

  my $caller_package = $stack[2]->[3];
  my $lineno         = $stack[1]->[2];

  my $log_message = sprintf '(%s):[%s] %s', $caller_package, $lineno, join q{}, @message;

  return print { $self->{_fh} } $log_message, "\n";
}

sub headers_in { return {} }

########################################################################
sub DESTROY {
########################################################################
  my ($self) = @_;

  if ( $self->{_fh} ) {
    close $self->{_fh};
  }

  return;
}

########################################################################
sub AUTOLOAD { }  ## no critic (ProhibitAutoLoading)
########################################################################

########################################################################
sub log {  ## no critic (ProhibitBuiltinHomonyms)
########################################################################
  my ($self) = @_;

  return $self;
}

########################################################################
package Faux::Context;
########################################################################

use Bedrock;
use Bedrock::Context qw(init_log4perl_logger);
use Data::Dumper;
use English qw(-no_match_vars);

# we need to provide Text::TagX with a context object if we want
# certain behaviors in Bedrock.
########################################################################
sub new {
########################################################################
  my ( $class, $outhandle, %options ) = @_;

  # everything else should be a hash of option
  $options{outhandle} = $outhandle;

  my $self = bless \%options, $class;

  my $log_level = $ENV{REQUEST_LOG_LEVEL} // $options{CONFIG}->{LogLevel};

  $self->request( Faux::ApacheHandler->new( $log_level // 'debug' ) );

  init_log4perl_logger();

  # 1. If a cache was passed in arguments, use it (Dependency Injection)
  if ( $self->{cache} ) {
    Carp::carp( "cache preset to %s\n", ref( $self->{cache} ) );
  }
  # 2. Otherwise, if the Environment requests one, build it (Auto-Discovery)
  elsif ( $ENV{BEDROCK_CACHE_ENGINE} ) {

    my $cache = eval { return Bedrock::Cache->new(); };

    if ( !$cache || $EVAL_ERROR ) {
      # Use carp so we see where the test failed, but don't kill the harness
      Carp::carp("WARNING: Test Harness could not init Bedrock::Cache: $EVAL_ERROR");
    }
    else {
      $self->{cache} = $cache;
    }
  }

  return $self;
}

########################################################################
sub cache { return $_[0]->{cache}; }
########################################################################

########################################################################
sub cgi_header_in    { return () }
sub send_http_header { }
sub getCookieValue   { }
sub cgi_header_out   { }
########################################################################

{
  ## no critic (RequireArgUnpacking, ProhibitNoStrict)
  no strict 'refs';

  my @var_list = qw{
    OUTPUT_HANDLER APACHE_HANDLER REQUEST_HANDLER SESSION CONFIG HEADER
    output_vars _request _response
  };

  foreach my $var (@var_list) {
    *{ 'Faux::Context::' . lc $var } = sub { _set( @_, $var ) };
  }
}

########################################################################
sub request { return _set( @_, 'request' ) };  ## no critic (RequireArgUnpacking)
########################################################################

########################################################################
sub _set {
########################################################################
  my ( $self, @args ) = @_;

  if ( @args > 1 ) {
    return $self->{ $args[1] } = $args[0];
  }
  else {
    return $self->{ $args[0] };
  }
}

########################################################################
sub print {  ## no critic (ProhibitBuiltinHomonyms)
########################################################################
  my ( $self, @args ) = @_;

  return $self->{outhandle}->print(@args);
}

########################################################################
sub close {  ## no critic (ProhibitBuiltinHomonyms, ProhibitAmbiguousNames)
########################################################################
  my ( $self, @args ) = @_;

  return $self->{outhandle}->close(@args);
}

########################################################################
package Bedrock::Test;
########################################################################

use Apache::Bedrock qw(load_startup_modules);
use Bedrock qw(slurp_file);  # keep this first
use Bedrock::BedrockConfig;
use Bedrock::Constants qw(:chars :booleans :defaults);
use Bedrock::Handler;
use Bedrock::Test::Utils qw(create_temp_file);
use Carp;
use Data::Dumper;
use English qw(-no_match_vars);
use File::Basename qw(basename);
use File::Temp qw( tempfile );
use IO::Scalar;
use JSON;
use List::Util qw( any none);
use Scalar::Util qw(reftype);
use Symbol;
use Test::More;
use YAML qw(LoadFile);

use parent qw(Exporter);

our @EXPORT_OK = qw(load run config bedrock);

# interprets and runs the test configurations
########################################################################
sub run {
########################################################################
  my ( $tests, %options ) = @_;

  my $name   = $options{name};
  my $config = $options{config} || config();

  if ( !ref $tests ) {
    $tests = load( $tests, %options );

    die "could not load [$tests]\n"
      if !ref $tests;
  }

  if ( !ref $tests || reftype($tests) ne 'ARRAY' ) {
    die "test object is not an array\n";
  }

  plan tests => exists $options{plan} ? $options{plan} : scalar @{$tests};

  foreach my $t ( @{$tests} ) {
    check_test($t);

    my $tempfile
      = $t->{tempfile} ? create_temp_file( %{ $t->{tempfile} } ) : $EMPTY;

    # override configuration values if config section present
    if ( $t->{config} ) {
      for ( keys %{ $t->{config} } ) {
        $config->{$_} = $t->{config}->{$_};
      }
    }

    local %ENV = %ENV;

    my $env = \%ENV;

    if ( $t->{env} ) {
      for ( keys %{ $t->{env} } ) {
        $env->{$_} = $t->{env}->{$_};
      }
    }

    if ( $t->{default_env} ) {
      for ( keys %{ $t->{default_env} } ) {
        $env->{$_} //= $t->{default_env}->{$_};
      }
    }

    my %r = bedrock(
      $t->{test},
      name     => $name,
      env      => $env,
      param    => $t->{param},
      config   => $config,
      tempfile => $tempfile,
    );

    if ( $tempfile && $t->{tempfile}->{cleanup} ) {
      unlink $tempfile;
    }

    # are we looking for an error?
    if ( $t->{error} ) {
      $r{output} = $r{error};

      # unless op is set, assume we match on regexp
      if ( !$t->{op} ) {

        # make it a regexp if it was a bare word
        if ( $t->{error} !~ /^\/.*?\//xsm ) {
          $t->{result} = sprintf '/%s/', $t->{error};
        }
        else {
          $t->{result} = $t->{error};
        }

        $t->{op} = 'like';
      }
      else {
        $t->{result} = $t->{error};
      }
    }

    # default for all results is equal to
    my $op = $t->{op} || 'is';

    if ( ref $t->{result} && reftype( $t->{result} ) eq 'ARRAY' ) {
      $op = 'any';
    }

    if ( $t->{trim} ) {
      my $result = $r{output};

      $result =~ s/\A\s*(.*?)\z/$1/xsm;

      $r{output} = $result;
    }

    if ( my $chomp = $t->{chomp} ) {

      my $result = $r{output};

      if ( $chomp =~ /\d/xsm ) {
        $chomp = {
          1 => 'front',
          2 => 'end',
          3 => 'all',
        }->{$chomp} // 'all';
      }

      if ( $chomp =~ /(?:front|all)$/xsm ) {
        $result =~ s/\n*\z//xsm;
      }

      if ( $chomp =~ /(?:end|all)$/xsm ) {
        $result =~ s/\A\n*//xsm;
      }

      $r{output} = $result;
    }

    if ( ref $op ) {
      $op = 'cmp_ok';
    }

    my %op_subs = (
      any => sub {
        my $got = $r{output};

        my @expected = ref( $t->{result} ) ? @{ $t->{result} } : $t->{result};

        return ok( ( any { $_ eq $got } @expected ), $t->{name} );
      },

      is => sub {
        return is( $r{output}, $t->{result}, $t->{name} );
      },

      isnt => sub {
        return isnt( $r{output}, $t->{result}, $t->{name} );
      },

      like => sub {
        return like( $r{output}, $t->{result}, $t->{name} );
      },

      unlike => sub {
        return unlike( $r{output}, $t->{result}, $t->{name} );
      },

      cmp_ok => sub {
        return cmp_ok( $r{output}, $t->{op}->{cmp_ok}, $t->{result}, $t->{name} );
      },
      isa_ok => sub {
        return isa_ok( $r{output}, $t->{result}, $t->{name} );
      },
    );

    die sprintf "ERROR (%s) Unknown test operator: %s for test %s\n", basename($PROGRAM_NAME), $op, $t->{name}
      if !$op_subs{$op} || reftype( $op_subs{$op} ) ne 'CODE';

    my $success = $op_subs{$op}->();

    if ( !$success ) {
      if ( $r{error} ) {
        diag( sprintf "[%s]:[%s]:[%s]\n", @r{qw/input output/}, $r{error} );
      }
      else {
        if ( defined $t->{comment} ) {
          note( sprintf '%s', $t->{comment} );
        }

        note( sprintf "[%s]:[%s]\n", @r{qw/input output/} );
      }
    }
  }

  return;
}

########################################################################
sub config {
########################################################################
  my ($config_path) = @_;

  my $log_level = $ENV{REQUEST_LOG_LEVEL} // 'debug';

  if ($config_path) {
    $config_path =~ s/;/:/xsmg;
    $ENV{CONFIG_PATH} = $config_path;
  }

  return Bedrock::Handler->new( Faux::ApacheHandler->new($log_level) )->config;
}

# loads the t/{file}.txt file
########################################################################
sub load {
########################################################################
  my ( $file, %options ) = @_;

  if ( $file =~ /[.]ya?ml$/xsm ) {
    $options{yaml} = $TRUE;
  }

  my $tests = eval {
    if ( exists $options{yaml} && $options{yaml} ) {
      return [ LoadFile($file) ];
    }
    else {

      local $RS = undef;

      open my $fh, '<', $file
        or croak "could not open $file";

      my $tests = <$fh>;

      close $fh
        or croak "could not close $file";

      return JSON->new->utf8->relax->decode($tests);
    }
  };

  die "unable to load test file [$file]: $EVAL_ERROR"
    if $EVAL_ERROR;

  return $tests;
}

########################################################################
sub bedrock {
########################################################################
  my ( $text, %options ) = @_;

  my %param = $options{param} ? %{ $options{param} } : ();

  my $config = $options{config};

  require Text::TagX;

  my $output = $EMPTY;

  my $ctx = Faux::Context->new( IO::Scalar->new( \$output ), CONFIG => $config, );

  my $tx = Text::TagX->new( IO::Scalar->new( \$text ), $ctx );

  $tx->options( ALLOW_SNIPPET_TAG => 'yes' );

  $tx->param( %param, config => $config );

  my @modules
    = grep { $_->{module} ne 'BLM::Startup::UserSession' } @{ $config->get_module_config() };

  $config->init_plugin_path;

  load_startup_modules( \@modules, $ctx, $tx );

  my $e = eval {
    my %faux_env = %ENV;

    if ( $options{env} && reftype( $options{env} ) eq 'HASH' ) {

      foreach my $var ( %{ $options{env} } ) {
        next if !$var;
        $faux_env{$var} = $options{env}->{$var};
      }
    }

    local %ENV = %faux_env;

    $tx->output;
  };

  return (
    input  => $text,
    error  => $EVAL_ERROR,
    output => $output
  );
}

########################################################################
sub check_test {
########################################################################
  my ($test) = @_;

  die "no result or error key found in test\n"
    if !exists $test->{result} && !exists $test->{error};

  die "no test key found in test\n"
    if !exists $test->{test} && !exists $test->{include};

  my $file = $test->{include};

  if ($file) {
    if ( !-e $file ) {
      $file = "$file.roc";
    }

    $test->{test} = eval { return slurp_file $file; };

    die "$EVAL_ERROR"
      if !$test->{test} || $EVAL_ERROR;
  }

  return $TRUE;
}

1;

__END__

=pod

=head1 NAME

Bedrock::Test - unit test framework for Bedrock

=head1 SYNOPSIS

 use Bedrock::Test qw(run config load);

=head1 DESCRIPTION

L<Bedrock::Test> implements a testing framework built using Bedrock
templating and L<Test::More>.  L<Bedrock::Test> can be use to create
unit tests for just about anything, it is not just a framework for
testing Bedrock applications.  Think of L<Bedrock::Test> as a
templating language's attempt to create a testing language, driven by
configuration files.

Tests are typically stored in a YAML file as an array of tests.  Here's an example:

 ---
 name: <plugin>
 comment: test to see if the plugin tag instantiates a Bedrock object of the proper class
 test: |-
   <plugin:MIME::Lite><if $MIMELite --ref "MIME::Lite">1<else>0</if>
 result: 1
 ---
 name: '<plugin --define-var="name">'
 comment: test to see if the plugin can be named
 test: |-
   <plugin:MIME::Lite --define-var="mime_lite">
   <if $mime_lite --ref "MIME::Lite">1<else>0</if>
 result: 1

Create a file in YAML format.  Define at least a key named C<test> and
one named C<result>.

 $ cat > foo-test-yml
 ---
 test: <null:foo 1><var $foo>
 result: 1

Create a test harness...

 $ cat > foo-test.t
 use strict;
 use warnings;
 
 use lib '@perl5_pkglibdir';

 use Bedrock::Test qw/run/;
 
 run 'foo-test.yaml';
 ^D

Run the test...

 $ prove foo-test.t

=head1 METHODS AND SUBROUTINES

L<Bedrock::Test> can export three subroutines described below.  In
most cases, you may only need to import the C<run> subroutine.

=head2 run

 run( tests, [options] )

Executes a set of tests within the L<Test::More> framework, comparing
the expected result with the output from the Bedrock snippet.

=over 5

=item tests

C<tests> can be the name of a test file or a reference to an array of
hashes that represent a test.  See explanation below.

=item options

=over 10

=item name

Name that will be used as part of the log file for this snippet.
You'll typically want to pass the name portion of the YAML file that
contains the snippet, although this name is up to you.  If you do not
pass a name, the a name will be fabricated by using the template:
C<bedrock-test_{PID}>.

=item config

Your own configuration file (typically your incarnation of
F<tagx.xml>).  Defaults to the Bedrock configuration file.  You can
load your own configuration file and any additional Bedrock XML files
that represent valid configuration objects by specifying the
C<BEDROCK_CONFIG_PATH> in the environment prior to running your tests.

 BEDROCK_CONFIG_PATH=/var/www/vhosts/config prove foo-test.pl

To override individual elements of the configuration file use the
C<config:> section in the test file.

=item logdir

Specifies the directory for the test log.  Defaults to F</tmp>.  The
test log file will be file named using the template:

 {logdir}/bedrock-test_{name}.log

=item plan

Total number of tests in your plan.  Defaults to the number of tests
in the array or file.

=back

=back

A test is represented by a hash that contains at least a C<test> and
C<result> key.  Example:

 {
   test => '<null:foo 1><var $foo>',
   result => 1
 }

=over 5

=item name

The name of the test. This will be reported on the TAP output.

=item tempfile

A hash of parameters for creating a temporary directory and possibly a
subdirectory and file.

=over 10

=item cleanup

Boolean that determines if the temporary file should be removed after
the test completes.

=item dir

Name of a subdirectory to create. If you include this value, the file
will be moved to this directory.

=item name

Name of a file to create in the temporary or subdirectory.

=item content

Contents of the file.

=back

Example:

 tempfile:
   name: foo.peb
   dir: t
   cleanup: 1
   content: |
     <var $ARGV.join(",")>

The file F<foo.peb> will be created and moved to the F<t> directory
relative to the current working directory.

=item test

The Bedrock snippet to execute.

=item result

The output that should be expected by executing the Bedrock snippet.
For the C<like> or C<unlike> operations, you can use a regular
expression as the result to compare.  Example:

 /hello world/i
 /^\s*hello world\s*$/si

=item chomp

Flag indicating that the result should have new lines removed.
C<chomp>

  1, front  => remove leading blank lines
  2, end    => remove trailing blank lines
  3, all    => remove blank lines from front and end

=item trim

Boolean that indicates if whitespace should be removed from front and
end of result.

default: false

=item op

The C<Test::More> operation to use for comparing the ouput of the
snippet to the result.  Valid values are:

=over 10

=item is

=item isa_ok

=item isnt

=item like

=item unlike

=back

=item error

If the C<error> key is set instead of the C<result> key, the test will
use the exception thrown by Bedrock as the result to compare against
the C<error> key.

Note that all errors are matched as regular expressions unless you
specifically set the C<op> value.

Example:

  error: Unable

  error: /^Unable/

=item comment

A comment regarding the test.  Not currently used, but may be helpful
in documenting your tests.

=item env

If you need to inject or override an enviroment variable you can
create a hash of key/value pairs that will replace or add to the
existing environment. For example, some tests in your file might
require environment variables that you've passed on the command line:

 DBI_HOST=localhost prove -v t/00-some-test.t

...but in the same test file you later need to use a different value
for the environment variable. Add an C<env> section to the test and
add the new values. These will be injected into the environment for
just that test.

 env:
   DBI_HOST: my-server
   DBI_PASS: flintstone
   DBI_USER: fred

=item default_env

Works similarly to C<env> but will not override a previously set
environment varialble.

=item config

Likewise you can override individual values in your configuration file
using the C<config> section.

Example:

 config:
   ABSOLUTE_PATHS: yes

=back

=head2 config

 config( path )

Returns Bedrock's configuration object.  Pass the path to the
configuration directory which contains your F<tagx.xml>, and possibly
other Bedrock XML configuration files.  If the directory contains a
F<config.d>, F<startup>, or F<plugin> directory then these will be
recursed into as well to create the final merged Bedrock configuration
object.

The default configuration path is F<@libdir@/bedrock/config>.

Note that your snippets I<can in fact> use your Application Plugins
(BLMs)! As long as they are properly configured and you
provide the path to your configuration files, the test harness will
create I<a Bedrock environment suitable for executing your BLMs>. 

=head2 load

 load( file, [options] )

Loads the tests from either a YAML or JSON formatted file. See
L</run_tests> documentation for a description of the what a test
object should like.

=over 5

=item file

Fully qualifed path to the YAML or JSON file to load.  If the file's
extension is C<.yml> or C<.yaml> it will be considered to be a YAML
file.

=item options

Options to pass to the C<load> subroutine.

=over 10

=item yaml

Boolean that indicates the file is in YAML format.

=back

=back

=head2 bedrock

 bedrock(snippet, options)

I<This method is run internally by C<run_tests> and you should
normally not need to execute this directly.>

Executes a Bedrock snippet. C<options> is hash of options described
below.

=over 5

=item param

Hash of parameters to pass to Bedrock for use when it executes your
snippet.

=item name

Name that will be used as part of the log file for this snippet.  You
typically want to pass the name portion of the YAML file that contains
the snippet, although this name is up to you.  If you do not pass a
name, the a name will be fabricated as using the template:
C<bedrock-test_{PID}>.

=item config

Your own configuration file (typically your incarnation of
F<tagx.xml>).  Defaults to the Bedrock configuration file.  You can
load your own configuration file and any additional Bedrock XML files
that represent configuration files by specifying the
C<BEDROCK_CONFIG_PATH> in the environment prior to running your tests.

 $ BEDROCK_CONFIG_PATH=/var/www/vhosts/config prove foo-test.pl

=item logdir

Speficies the directory for the tests logs.  Defaults to F</tmp>.

=back

Returns a hash containing:

=over 5

=item input

The Bedrock snippet to execute.

=item output

The output after the Bedrock snippet has been parsed.

=item error

Bedrock's error object.

=back

=head1 TESTING TRICKS

The test framework give you access to Bedrock's configuration object
as well as the shell enviroment via the C<$config> and C<$env>
objects.  Accordingly, you can use these to modify test behaviors
when they are executed.

 DBI_DSN=dbi:Oracle:NAME_TEST prove foo-test.t

=head1 SEE ALSO

L<Text::TagX>, L<Test::More>

=head1 AUTHOR

Rob Lauer - <rlauer6@comcast.net>

=cut
