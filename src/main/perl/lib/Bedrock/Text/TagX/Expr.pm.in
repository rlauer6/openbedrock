package TagX::Expr;

#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2001, Charles Jones, LLC.
#    Copyright (C) 2024, TBC Development Group, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

use strict;
use warnings;

use Role::Tiny::With;
with 'Bedrock::Logger';

use Bedrock::Constants qw(:chars :booleans);
use Bedrock::RegExp;
use Cwd;
use Data::Dumper;
use English qw(-no_match_vars);
use List::Util qw( none any pairs );
use Log::Log4perl::Level;
use Scalar::Util qw( looks_like_number reftype blessed);

use parent qw(Exporter);

our $VERSION = '@PACKAGE_VERSION@';  ## no critic (RequireInterpolationOfMetachars)
use Readonly;

Readonly::Array our @COMPARISON_OPS => qw(
  and
  eq
  exists
  file
  ge
  gt
  le
  lt
  ne
  or
  re
  ref
);

Readonly::Array our @UNARY_OPS => qw(
  array
  blessed
  cached
  defined
  exists
  hash
  not
  object
  scalar
);

Readonly::Array our @BINARY_OPS => ( map {"--$_"} @COMPARISON_OPS ),
  split //xsm, '+.-*/';

########################################################################
our %STRING_CMP = (
  '--gt' => sub {
    my ( $a, $b ) = map { $_ // $EMPTY } @_;
    return $a gt $b;
  },
  '--ge' => sub {
    my ( $a, $b ) = map { $_ // $EMPTY } @_;
    return $a ge $b;
  },
  '--lt' => sub {
    my ( $a, $b ) = map { $_ // $EMPTY } @_;
    return $a lt $b;
  },
  '--le' => sub {
    my ( $a, $b ) = map { $_ // $EMPTY } @_;
    return $a le $b;
  },
  '--eq' => sub {
    my ( $a, $b ) = map { $_ // $EMPTY } @_;
    return $a eq $b;
  },
  '--ne' => sub {
    my ( $a, $b ) = map { $_ // $EMPTY } @_;
    return $a ne $b;
  },
  '--and' => sub {
    my ( $a, $b ) = map { $_ // $EMPTY } @_;
    return $a && $b;
  },
  '--or' => sub {
    my ( $a, $b ) = map { $_ // $EMPTY } @_;
    return $a || $b;
  },
  '--file' => sub {
    my $file;

    if ( $_[1] =~ /^(\S*)/ ) {
      $file = $1;
    }
    else {
      die "no filename specified: usage: filename --file file-test-op\n";
    }

    for ($file) {
      /^\~([^~]*)$/ && do {
        $file = getcwd() . '/' . $1;
        last;
      };

      /^[^\/~]/ && do {
        $file = $ENV{DOCUMENT_ROOT} . '/' . $file;
        last;
      };
    }

    $_[0] =~ /^-?([rwxoRWXOezsfdlpSbctugkTBMAC])/ && do {
      return eval "-$1 \"" . $file . "\" ? 1 : 0";  ## no critic
    };

    die "invalid file test operation: '$_[0] $_[1]'";
  },

  '--ref' => sub { return ( ref( $_[0] ) =~ /$_[1]/ ) ? 1 : 0; },

  '--reftype' => sub { return ( ref( $_[0] ) && reftype( $_[0] ) eq $_[1] ) ? 1 : 0; },

  '--re' => sub {
    # $lhs may be undef
    my ( $lhs, $rhs, $self ) = @_;
    $lhs = $lhs // $EMPTY;

    my $re = regexp_compile($rhs);

    return $lhs !~ $re ? $FALSE : ( $TRUE, regexp_capture_group() );
  },
  '--exists' => sub {
    my ( $lhs, $rhs ) = @_;

    die "invalid argument for '--exists': HASH --exists KEY\n"
      if !ref($lhs) || reftype($lhs) ne 'HASH';

    return exists $lhs->{$rhs} ? $TRUE : $FALSE;
  },

);

our %NUMBER_CMP = (
  '--gt' => sub {
    my ( $a, $b ) = map { $_ || 0 } @_;
    return $a > $b;
  },
  '--ge' => sub {
    my ( $a, $b ) = map { $_ || 0 } @_;
    return $a >= $b;
  },
  '--lt' => sub {
    my ( $a, $b ) = map { $_ || 0 } @_;
    return $a < $b;
  },
  '--le' => sub {
    my ( $a, $b ) = map { $_ || 0 } @_;
    return $a <= $b;
  },
  '--eq' => sub {
    my ( $a, $b ) = map { $_ || 0 } @_;
    return $a == $b;
  },
  '--ne' => sub {
    my ( $a, $b ) = map { $_ || 0 } @_;
    return $a != $b;
  },
  '--and' => sub {
    my ( $a, $b ) = map { $_ || 0 } @_;
    return $a && $b;
  },
  '--or' => sub {
    my ( $a, $b ) = map { $_ || 0 } @_;
    return $a || $b;
  },
);

our %ARITHMETIC_OPERATIONS = (
  $PLUS_SIGN => sub {
    my ( $l, $r ) = @_;

    if ( looks_like_number($l) && looks_like_number($r) ) {
      $l += $r;
    }
    else {
      $l .= $r;
    }
    return $l;
  },
  $MINUS_SIGN => sub {
    my ( $l, $r ) = @_;

    if ( looks_like_number($l) && looks_like_number($r) ) {
      $l -= $r;
    }
    else {
      $l =~ s/$r$//xsmg;  # "barfoobar" - "bar" = "foo" ?
    }

    return $l;
  },
  $ASTERISK => sub {
    my ( $l, $r ) = @_;

    if ( looks_like_number($l) && looks_like_number($r) ) {
      $l *= $r;
    }
    elsif ( looks_like_number($r) ) {
      $l = $l x $r;
    }

    return $l;
  },
  $SLASH => sub {
    my ( $l, $r ) = @_;
    if ( looks_like_number($l) && looks_like_number($r) ) {
      die "illegal division by zero\n"
        if $r == 0;

      $l = $l / $r;
    }
    else {
      $l =~ s/$r//xsmg;
    }

    return $l;
  },
  $DOT => sub {
    my ( $l, $r ) = @_;

    $l .= $r;

    return $l;
  },
);

our %UNARY_FUNCS = (
  array => sub {
    my ( $op, $l, @args ) = @_;

    die "invalid argument for unary(`--$op') operator.\n"
      if !$l->can('eval');

    my $value = $l->eval(@args);

    return ref $value && reftype($value) eq 'ARRAY';
  },
  blessed => sub {
    my ( $op, $l, @args ) = @_;

    die "invalid argument for unary(`--$op') operator.\n"
      if !$l;

    my $value = $l->can('eval') ? $l->eval(@args) : $l;
    return ref $value && blessed($value);
  },
  cached => sub {
    my ( $op, $l, @args ) = @_;
    die "invalid argument for unary(`--$op') operator.\n"
      if !$l;

    my $value = $l->can('eval') ? $l->eval(@args) : $l;

    return sub {
      my ($tx) = @_;
      return $tx->is_cached($value);
    };
  },
  defined => sub {
    my ( $op, $l, @args ) = @_;

    die "invalid argument for unary(`--$op') operator.\n"
      if ref($l) ne 'TagX::Term::Var';

    my $defined = $l->defined(@args);

    return $defined;
  },
  exists => sub {
    my ( $op, $l, @args ) = @_;

    die "invalid argument for unary(`--$op') operator.\n"
      if ref($l) ne 'TagX::Term::Var';

    my $exists = $l->exists(@args);

    return $exists;
  },
  hash => sub {
    my ( $op, $l, @args ) = @_;

    die "invalid argument for unary(`--$op') operator.\n"
      if !$l->can('eval');

    my $value = $l->eval(@args);

    return ref($value) && reftype($value) eq 'HASH';
  },
  not => sub {
    my ( $op, $l, @args ) = @_;

    die "invalid argument for unary(`--$op') operator.\n"
      if !$l->can('eval');

    my $value = $l->eval(@args);

    return !( defined($value) && $value );
  },
  scalar => sub {
    my ( $op, $l, @args ) = @_;

    die "invalid argument for unary(`--$op') operator.\n"
      if !$l->can('eval');

    my $value = $l->eval(@args);

    return ref $value && reftype($value) eq 'SCALAR';
  },
);

########################################################################
sub new {
########################################################################
  my ( $class, @args ) = @_;

  my $self  = bless [], $class;
  my $nargs = scalar @args;

  # Check whether unary or binary operator
  my $op_or_lhs = shift @args;

  if ( ref $op_or_lhs ) {
    my $op = shift @args;

    die "Invalid operator in expression\n"
      if ref $op;

    if ( $op && $op =~ /^[+\-.*\/]$/xsm ) {
      die sprintf "Insufficient number of arguments(%s) to operator (%s)\n", $nargs - 1, $op
        if $nargs <= 2;

      @{$self} = ( $op_or_lhs, $op );

      while ( @args > 0 ) {
        $op_or_lhs = shift @args;

        die "Operator `$op_or_lhs' is illegal here.\n"
          if !ref $op_or_lhs;

        push @{$self}, $op_or_lhs;

        if ( @args > 1 ) {
          $op_or_lhs = shift @args;

          die "Operator `$op_or_lhs' is illegal here: must be `+' only.\n"
            if ref $op_or_lhs
            or ( $op_or_lhs ne $PLUS_SIGN && $op_or_lhs ne $DOT );

          push @{$self}, $op_or_lhs;
        }
      }
    }
    elsif ( $op && any { $op eq $_ } map {"--$_"} @COMPARISON_OPS ) {

      die sprintf "Invalid number of arguments(%s) for operator (%s)\n", $nargs - 1, $op
        if $nargs != 3;

      my $rhs = shift @args;

      die "Token `$rhs' is illegal here.\n"
        if !ref $rhs;

      @{$self} = ( $op_or_lhs, $op, $rhs );
    }
    elsif ( $nargs == 1 ) {
      @{$self} = ($op_or_lhs);
    }
    else {
      die sprintf "Token [%s] is illegal here.\n", $op // '<undef>';
    }
  }
  elsif ( any { lc $op_or_lhs eq $_ } map {"--$_"} @UNARY_OPS ) {

    die sprintf "Unary operator %s requires a single argument: found %d instead\n", $op_or_lhs, $nargs - 1
      if $nargs != 2;

    my $rhs = shift @args;

    die "Token `$rhs' is not a valid argument to unary operator `$op_or_lhs'.\n"
      if !ref $rhs;

    @{$self} = ( $op_or_lhs, $rhs );
  }
  else {
    die "Token `$op_or_lhs' is not a valid unary operator.\n";
  }

  return $self;
}

########################################################################
sub verbose {
########################################################################
  my ( $self, @args ) = @_;

  my $logger = $self->get_logger;

  if (@args) {
    $logger->level( $args[0] > 1 ? $TRACE : $DEBUG );
  }

  return any { $logger->level eq $_ } ( $DEBUG, $TRACE );
}

########################################################################
sub eval {  ## no critic (ProhibitBuiltinHomonyms, RequireArgUnpacking)
########################################################################
  my $self = shift;  # shift off self, we'll unpack later

  my $logger = $self->get_logger( ref $self );

  my ( $l, $op, $r );

  my @args = @{$self};

  $logger->debug(" begin eval with @{[scalar(@args)]} items: (@args)");

  $l = shift @args;

  if ( defined $l && ref $l ) {
    $logger->debug("LHS [$l] is an object");

    if ( @args > 0 ) {

      # binary operators
      $op = lc shift @args;

      die "Invalid operator `$op'.\n"
        if !any { $op eq $_ } @BINARY_OPS;

      $logger->debug("op is [$op]");

      # if ( $op =~ /^[\+\-\.\*\/]$/ ) {
      if ( any { $op eq $_ } qw{+ - . * /} ) {

        my $str = $l->eval(@_);

        if ( !defined $str ) {
          $logger->error("$$l evaluated to undef!\n");
        }

        $logger->debug("$l eval'ed to [$str]");

        unshift @args, $op;

        foreach my $p ( pairs @args ) {
          ( $op, $l ) = @{$p};

          my $str2 = $l->eval(@_);

          $str = $ARITHMETIC_OPERATIONS{$op}->( $str, $str2 );
        }

        return $str;
      }
      else {
        $r = shift @args;

        my $lval = $l->eval(@_);

        my $rval = $r->eval(@_);

        $logger->debug( sprintf 'RHS: [%s]', $rval // '<undef>' );

        my $result;

        eval {
          if ( looks_like_number($lval) && looks_like_number($rval) ) {

            $logger->debug( 'result: ' . $NUMBER_CMP{$op}( $lval, $rval ) );

            $result = $NUMBER_CMP{$op}( $lval, $rval );
          }
          else {
            $logger->debug( 'result: ' . $STRING_CMP{$op}( $lval, $rval, $self ) // $EMPTY );

            my $cg;
            ( $result, $cg ) = $STRING_CMP{$op}( $lval, $rval, $self );

            # grab capture group from regular expression if op is --re
            if ( $result && $op eq '--re' ) {
              map { $_[0]->{$_} = $cg->{$_}; } keys %{$cg};
            }
          }
        };

        die "internal error evaluating expression op: $op lval:[$lval] rval:[$rval]\n$EVAL_ERROR\n"
          if $EVAL_ERROR;

        return $result;
      }
    }
    else {

      my $v = $l->eval(@_);

      return $v;
    }
  }
  elsif ( $l && $l =~ /^--(not|blessed|cached|defined|array|scalar|object|hash|exists)$/oixsm ) {
    $logger->debug("found a unary operator: [$l]");

    # unary operators
    $op = lc $l;
    $l  = shift @args;

    $logger->debug("Argument to unary(`op') operator is [$l]");

    # we expect the argument to the unary operator to be a TagX::Term
    # or something that can('eval') or can('exists')

    $UNARY_FUNCS{object} = $UNARY_FUNCS{hash};

    $op =~ s/^--//xsm;
    my $retval = $UNARY_FUNCS{$op}->( $op, $l, @_ );

    return ref $retval ? $retval : $retval ? 1 : 0;
  }
  else {
    die "unsupported unary operator: $l\n";
  }
}

1;
