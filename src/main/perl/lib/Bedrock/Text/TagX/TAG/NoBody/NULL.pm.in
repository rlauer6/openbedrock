package TagX::TAG::NoBody::NULL;

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

use strict;
use warnings;

use Bedrock qw(is_array is_hash);
use Bedrock::Array;
use Bedrock::BedrockJSON;
use Bedrock::Constants qw(:chars :booleans);
use Bedrock::Dump;
use Bedrock::Hash;
use Bedrock::Object qw(create_object);
use Bedrock::Serializer qw(evolve devolve);
use Bedrock::XML;
use English qw(-no_match_var);
use List::Util qw(pairs);
use MIME::Base64;
use Scalar::Util qw(reftype blessed openhandle);

use Role::Tiny::With;
with 'Bedrock::Logger';

use parent qw(Exporter TagX::TAG::NoBody);

########################################################################
sub _promote {
########################################################################
  my ($value) = @_;

  eval { evolve($value); };

  die "could not serialize value: [$EVAL_ERROR]\n"
    if $EVAL_ERROR;

  return $value;
}

########################################################################
sub param {
########################################################################
  my ( $options, $tx, $obj ) = @_;

  my $name = _get_name_from_options($options);

  # create a Bedrock::RecordSet object
  if ( $options->{recordset} && ref $obj ) {

    die 'invalid object type: must be array'
      if !is_array($obj);

    $obj = Bedrock::RecordSet->new($obj);
  }

  if ( is_array($obj) && $options->{coalesce} ) {
    my ($value) = grep { defined $_ } @{$obj};

    return $tx->param( $name => $value );
  }

  return $tx->param( $name => $obj );
}

########################################################################
sub define_options {
########################################################################
  return (
    'base64'          => undef,
    'bedrock-xml'     => undef,
    'cache=s'         => undef,
    'coalesce'        => undef,
    'create-object=s' => undef,
    'default=s'       => undef,
    'define-array=s'  => undef,
    'define-object=s' => undef,
    'define-var=s'    => undef,
    'escape'          => undef,
    'encode'          => undef,
    'expand-all'      => undef,
    'expand-args'     => undef,
    'expand'          => undef,
    'file'            => undef,
    'filter-hash=s'   => undef,
    'handle'          => undef,
    'json'            => undef,
    'lc'              => undef,
    'recordset'       => undef,
    'range=s'         => undef,
    'sleep=s'         => undef,
    'serializer=s'    => undef,
    'uc'              => undef,
    'xml'             => undef,
    'yaml'            => undef,
  );

}

########################################################################
sub finalize {
########################################################################
  my ( $self, $tx, @context ) = @_;

  my %options = $self->define_options;

  my @argv = $self->parse_options( \%options, @context );

  $self->dump_options( \%options, @argv );

  if ( $options{sleep} ) {
    sleep $options{sleep};
    return;
  }

  my $tag_opts = $self->parse_tag_name( $argv[0] );

  my ( $type, $name, $default ) = @{$tag_opts}{qw(type name default)};

  $self->get_logger->debug(
    Dumper(
      [ type    => $type,
        name    => $name,
        default => $default,
        argv    => \@argv
      ]
    )
  );

  if ($name) {
    shift @argv;
  }

  $type = $type eq 'hash' ? 'object' : $type;

  die "Argument for --handle option and must be a handle.\n"
    if $options{handle} && !( openhandle( $options{handle} ) && @argv );

  die "Argument for --file option and must be filename.\n"
    if $options{file} && !@argv;

  # synonymous tags
  if ( exists $options{expand} ) {
    $options{'expand-args'} = delete $options{expand};
  }

  if ( $options{'create-object'} ) {
    $options{'define-object'} = $options{'create-object'};
    delete $options{'create-object'};
  }

  $self->get_logger->debug( sprintf 'default value is: [%s]', $default // '<undef>' );

  if ( !$name ) {
    $name = _get_name_from_options( \%options );
  }

  if ( !$name ) {
    $name = $UNDERSCORE;

    my $val = $context[0]->{$UNDERSCORE};

    if ( exists $context[0]->{$UNDERSCORE} ) {

      $self->get_logger->warn(
        sub {
          sprintf 'You are about to overwrite the default variable _ => (%s)!', $val // '<undef>';
        }
      );
    }
  }

  ($type) ||= grep { $options{ 'define-' . $_ } } qw(var array object);
  $type   ||= 'var';

  $options{ 'define-' . $type } = $name;

  if ( exists $options{cache} ) {
    push @argv, $tx->read_cache( $options{cache} );
  }

  # --base64 option tells Bedrock that the argument to the <null> tag is
  # base64 encoded and should be decoded, before applying other options.
  # Currently this is available for a single argument (scalar or array)
  #
  # The string is base64 encoded
  #   <null:decoded --base64 $input.encoded>
  #
  # The string is a base64 encoded JSON string
  #   <null:decoded --json --base64 $input.encoded>

  if ( exists $options{base64} ) {
    @argv = $self->handle_base64(@argv);
  }

  if ( $options{range} ) {
    my $range = $tx->parse_range( $options{range} );

    die "invalid range"
      if !$range;

    @argv = @{$range};
  }

  my %type_rules = (
    var => sub {
      if ( @argv == 1 ) {
        $self->_single_value( \%options, $tx, @argv );
      }
      elsif ( @argv > 1 ) {
        $self->_multiple_values( \%options, $tx, @argv );
      }
      else {
        param( \%options, $tx, $default );
      }
    },
    array => sub {
      if ( $options{expand} || $options{'expand-args'} ) {
        param( \%options, $tx, Bedrock::Array->new( map { is_array($_) ? @{$_} : $_; } @argv ) );
      }
      else {
        param( \%options, $tx, Bedrock::Array->new(@argv) );
      }
    },
    object => sub {
      my $obj = $self->define_object( \%options, @argv );

      param( \%options, $tx, $obj );
    },
  );

  my ($obj_type) = grep { $options{ 'define-' . $_ } } qw(var array object);

  $self->get_logger->debug(
    sub {
      return sprintf 'Creating an object of type: [%s], named: [%s]', $obj_type, $options{ 'define-' . $obj_type };
    }
  );

  die "$obj_type not defined!\n"
    if !$obj_type;

  $type_rules{$obj_type}->();

  return;
}

########################################################################
sub _get_name_from_options {
########################################################################
  my ($options) = @_;

  my ($name) = grep { defined $options->{ 'define-' . $_ } } qw(var array object);

  return
    if !$name;

  return $options->{ 'define-' . $name };
}

########################################################################
sub handle_base64 {
########################################################################
  my ( $self, @argv ) = @_;

  if ( @argv == 1 && !ref $argv[0] ) {
    $argv[0] = decode_base64( $argv[0] );
  }
  elsif ( is_array( $argv[0] ) ) {
    @argv = map { decode_base64($_); } @{ $argv[0] };
  }
  else {
    die sprintf
      "'Use --base64 with single argument: scalar or array reference - not %s\n",
      reftype( $argv[0] );
  }

  return @argv;
}

########################################################################
sub define_object {
########################################################################
  my ( $self, $options, @argv ) = @_;

  my $value = $argv[0];

  if ( @argv == 1 ) {
    return Bedrock::Hash->new( %{$value} )
      if is_hash($value);

    return Bedrock::Hash->new( @{$value} )
      if is_hash($value);

    my $json = Bedrock::JSON->new($value);

    return $json->decode;
  }

  ########################################################################
  # not sure what the exact use case is for this but...
  # since we are trying to create a hash from a list:
  # --------------------------------------------------------------------
  # 1. if the first element is an array then unravel it and prepend it to the rest of the list
  # 2. if the first element is a hash then unravel it and prepend it to the rest of the list
  # 3. repeat above for last element of the list
  # 4. if we end up with an odd
  # --------------------------------------------------------------------
  # array, scalar, scalar, ...
  # hash, scalar, scalar, ...
  # scalar, ..., array
  # scalar, ..., hash
  ########################################################################

  if ( reftype($value) && reftype($value) =~ /HASH|ARRAY/xsm ) {
    shift @argv;

    if ( is_array($value) ) {
      $self->get_logger->debug(
        sub {
          sprintf "Prepending the following array to (%s)\n(%s)", join( $EMPTY, @argv ), @{$value};
        }
      );

      unshift @argv, @{$value};
    }
    else {
      $self->get_logger->debug(
        sub {
          sprintf "Prepending the following array to (%s)\n(%s)", join( $EMPTY, @argv ), %{$value};
        }
      );

      unshift @argv, %{$value};
    }
  }
  elsif ( $argv[-1] && reftype( $argv[-1] =~ /HASH|ARRAY/xsm ) ) {
    $value = pop @argv;

    if ( is_array($value) ) {
      $self->get_logger->debug(
        sub {
          return sprintf "Appending the following array to (%s)\n(%s)", join( $EMPTY, @argv ), @{$value};
        }
      );

      push @argv, @{$value};
    }
    else {
      $self->get_logger->debug(
        sub {
          return sprintf "Appending the following array to (%s)\n(%s)", join( $EMPTY, @argv ), %{$value};
        }
      );

      push @argv, %{$value};
    }
  }

  die sprintf "Odd number of elements [%d] in object creation list\n", scalar @argv
    if @argv % 2;

  $self->get_logger->debug(
    sub {
      return sprintf "Key-value pairs for object creation are\n(%s)", join $SPACE, @argv;
    }
  );

  return Bedrock::Hash->new(@argv);
}

########################################################################
sub _read_scalar_from_file {
########################################################################
  my ( $self, $tx, $fh_or_file ) = @_;

  local $RS = undef;

  return <$fh_or_file>
    if openhandle $fh_or_file;

  die "no handle or file specified\n"
    if !$fh_or_file;

  my $file = $tx->find_file( $fh_or_file, undef, $DOT );

  die "error: can't read [$fh_or_file]. Either it does not exist or is empty\n"
    if !$file || !-s $file;

  open my $fh, '<', $file
    or die sprintf "Could not open file [%s] for reading.\n", $file;

  my $scalar = <$fh>;

  close $fh;

  return $scalar;
}

########################################################################
sub _multiple_values {
########################################################################
  my ( $self, $options, $tx, @argv ) = @_;

  my ( $default, $json, $expand, $expand_all ) = @{$options}{qw( default json expand-args expand-all)};

  $self->get_logger->debug(
    sub {
      sprintf 'Defining an array with (%s)[%s items]', join( $EMPTY, @argv ), scalar @argv;
    }
  );

  # check to see if it's possible that this is just a trick
  # of quoting that resulted in the parser creating multiple
  # arguments - qw{ a b c d }
  my $arg = join $SPACE, grep {defined} @argv;

  return param( $options, $tx, Bedrock::Array->new( split /\s+/xsm, $1 ) )
    if $arg =~ /^\s*qw[{(]\s*([^)}]+)\s*[}]\s*$/xsm;

  if ($json) {

    my $json_string = $arg // $default;

    my $handler = $self->io_handler( $tx, 'JSON' );

    my $ref = $handler->in($json_string);

    return param( $options, $tx, $ref );
  }

  # arguments may be taken solely as list, ony first as expanded, or
  # all as expanded...

  # expand none
  return param( $options, $tx, Bedrock::Array->new(@argv) )
    if !is_array( $argv[0] ) || !( $expand || $expand_all );

  # expand one or all
  my @array = $expand ? ( @{ shift @argv }, @argv ) : map { is_array($_) ? @{$_} : $_ } @argv;

  return param( $options, $tx, Bedrock::Array->new(@array) );
}

########################################################################
sub _single_value {
########################################################################
  my ( $self, $options, $tx, @argv ) = @_;

  my ( $lc, $uc, $encode, $default ) = @{$options}{qw(lc uc encode default)};

  my ( $file, $handle ) = @{$options}{qw( file handle)};

  my $value = $argv[0];

  if ( defined $options->{escape} ) {
    my %sub_strings = (
      $LITERAL_NEWLINE => "\n",
      $LITERAL_TAB     => "\t",
      $LITERAL_RETURN  => "\r",
    );

    $value =~ s/(\\n|\\t|\\r)/$sub_strings{$1}/gxsm;
  }

  $self->get_logger->debug( Dumper( [ single_value => $value ] ) );

  if ( $file || $handle ) {
    $value = $self->_read_scalar_from_file( $tx, $value );
  }

  my @rules = (
    json => sub {
      return if !exists $options->{json};

      my $handler = $self->io_handler( $tx, 'JSON' );

      $value //= $default;

      $self->get_logger->debug(
        Dumper(
          [ json    => $value,
            options => $options
          ]
        )
      );

      $value
        = $encode
        ? $handler->serialize( $value, $options )
        : $handler->deserialize( $value, $options );

      if ( !$encode ) {
        _promote($value);
      }

      return param( $options, $tx, $value );
    },
    'bedrock-xml' => sub {
      return if !exists $options->{'bedrock-xml'};

      my $handler = $self->io_handler( $tx, 'BedrockXML' );
      $value //= $default;

      $value
        = $encode
        ? $handler->serialize( $value, { %{$options}, @argv } )
        : $handler->deserialize( $value, { %{$options}, @argv } );

      if ( !$encode ) {
        _promote($value);
      }

      return param( $options, $tx, $value );
    },
    xml => sub {
      return if !exists $options->{xml};

      my $handler = $self->io_handler( $tx, 'XML' );

      $value //= $default;

      $value
        = $encode
        ? $handler->serialize( $value, { %{$options}, @argv } )
        : $handler->deserialize( $value, { %{$options}, @argv } );

      if ( !$encode ) {
        _promote($value);
      }

      return param( $options, $tx, $value );
    },
    yaml => sub {
      return if !exists $options->{yaml};

      my $handler = $self->io_handler( $tx, 'YAML' );

      $value //= $default;

      $value
        = $encode
        ? $handler->serialize( $value, { %{$options}, @argv } )
        : $handler->deserialize( $value, { %{$options}, @argv } );

      if ( !$encode ) {
        _promote($value);
      }

      return param( $options, $tx, $value );
    },
    serializer => sub {
      return if !defined $options->{serializer};

      my $handler
        = $self->io_handler( $tx, $options->{serializer}, { 'out', @argv } );

      $value //= $default;

      $value
        = $encode
        ? $handler->serialize( $value, { %{$options}, @argv } )
        : $handler->deserialize( $value, { %{$options}, @argv } );

      if ( !$encode ) {
        _promote($value);
      }

      return param( $options, $tx, $value );
    },
    'filter-hash' => sub {
      my ( $filter_spec, $mode ) = @{$options}{qw( filter-hash filter-mode)};

      $mode //= 'exclude';

      return if !defined $filter_spec || !is_array($value);

      my %wanted = eval {
        my $filter = Bedrock::JSON::decode($filter_spec);

        # it has to be a hash or array!
        if ( is_array($filter) ) {
          die 'filter-hash array must have even number of elements (key/value pairs)'
            if @{$filter} % 2 != 0;

          return @{$filter};
        }

        return %{$filter};
      };

      die $EVAL_ERROR
        if $EVAL_ERROR;

      die "filter-mode must be 'include' or 'exclude', got: '$mode'"
        if $mode ne 'include' && $mode ne 'exclude';

      $self->get_logger->debug( sub { Dumper( [ 'filter-hash' => $mode, wanted => \%wanted ] ) } );
      my $filtered_array = filter( array => $argv[0], wanted => \%wanted, mode => $mode );

      return param( $options, $tx, $filtered_array );
    },
    'expand-args' => sub {
      return if !exists $options->{'expand-args'} || !is_array($value);

      return param( $options, $tx, Bedrock::Array::Reference->new($value) );
    },
    'default' => sub {
      $value //= $default;
      $value = $lc ? lc $value : $uc ? uc $value : $value;

      $self->get_logger->debug( sprintf 'setting single value: %s', $value // '<undef>' );

      return param( $options, $tx, $value );
    },
  );

  # first rule wins...
  foreach my $r ( pairs @rules ) {
    my ( $rule, $sub ) = @{$r};

    return if $sub->();
  }

  # value might be a scalar, array or hash - promote $value to a
  # Bedrock::Array or Bedrock::Hash if it is a reference
  return param( $options, $tx, create_object($value) );
}

########################################################################
sub filter {
########################################################################
  my (%args) = @_;

  my ( $array, $mode, $wanted ) = @args{qw(array mode wanted)};

  my $filtered_array = Bedrock::Array->new();

  foreach my $item ( @{$array} ) {
    next if !is_hash($item);

    my $match = _filter( $item, $wanted );
    next if $mode eq 'include' && !$match;
    next if $mode eq 'exclude' && $match;

    push @{$filtered_array}, $item;
  }

  return $filtered_array;
}

########################################################################
sub _filter {
########################################################################
  my ( $item, $wanted ) = @_;

  for my $k ( keys %{$wanted} ) {
    return $TRUE if exists $item->{$k} && $item->{$k} eq $wanted->{$k};
  }

  return $FALSE;
}

sub group { return 'Core Data & Variable Management'; }

1;

__END__

=pod

=head1 TAG - C<E<lt>nullE<gt>>

=head1 PURPOSE

The C<null> tag is the I<do something> tag or more correctly I<the
possibly do something but donE<039>t output anything> tag.  It
provides a mechanism to invoke methods on objects and create (and
translate) Bedrock objects of any type. No text is inserted at the
point the tag is encountered. Use the C<E<lt>varE<gt>> tag for
inserting text.

=head1 SYNTAX

 <null[:type][:var-name] [options] [list | expression]>

=head1 DESCRIPTION

Use the C<E<lt>nullE<gt>> tag to create Bedrock objects or invoke methods on
objects when no output is desired.

=head1 EXTENDED NAMING

The C<null> tag supports an extended naming syntax that includes the object type.

  <null:type:name ... >

I<type> should be one of:

=over 5

=item hash

=item array

=item scalar

=back

If I<type> is not provided Bedrock will create the appropriate object
based on the argument(s).  If the argument is a list, then Bedrock
will create an array.  One element lists are created as scalars.  If the
argument is an expression, then the object type will be determined by
the the result of the expression.

=head1 EXAMPLES

=over 5

=item * Create a Bedrock hash object

 <null:hash --define-var=foobar foo bar buz biz>
 <null:hash:foobar foo bar buz biz>

=item * Create a Bedrock array object

 <null:fruits apple orange pear lime lemon>
 <null:array:fruit apple orange pear lime lemon>

=item * Create a scalar

 <null:perl TIMTOWDI>

=back

=head1 OPTIONS

 --base64
 --bedrock-xml
 --cache = "key-name"
 --default
 --define-array  = "var-name"
 --define-object = "var-name"
 --define-var    = "var-name"
 --encode
 --expand-all
 --expand-args
 --file
 --filter-hash
 --handle
 --json
 --lc
 --range
 --recordset
 --sleep
 --uc
 --xml

=over 5

=item --base64

This option tells Bedrock that the argument to the C<E<lt>nullE<gt>> tag is 
a I<base64> encoded string and should be decoded before applying other options.

Currently this option will only work with a single argument which can either
be a scalar or an array reference.

Examples:

The string is base64 encoded

 <null:decoded --base64 $input.encoded>

The string is a base64 encoded JSON string

 <null:decoded --json --base64 $input.encoded>

=item --bedrock-xml

Consider the input to be a scalar string (or an open filehandle) that
represents a valid Bedrock XML object (or valid Bedrock XML file).
This has the effect of essentially deserializing the string file into
a Bedrock object.

 <plugin:FileIO ($env.BEDROCK_CONFIG_PATH + "/tagx.xml")>
 <null:tagx --bedrock-xml $FileIO.handle>
 <trace --output $tagx>


 <null:foo a b c d>
 <trace --output $foo>

 <null:xml $foo.bedrock_xml()>

 <null:foo --bedrock-xml $xml>
 <trace --output $foo>

=item --cache

Name of the key to retrieve from the cache. The value is treated as an
argument to the tag. For example, to retrieve a JSON encoded object
from the cache and convert this to a Bedrock object you might do
something like this:

 <sink --cache="foo">
 {
   "foo" : "bar"
 }
 </sink>
 
 <null:foo --cache=foo --json>
 <trace --output $foo>

=item --create-object

I<Deprecated> use C<--define-object>.

=item --default

Provides a default value if the argument is B<undefined>.

 <null:foo --default="red" $input.color>

If you want to provide a default value when a value is B<not true> in
the B<Perl> sense, then try this:

 <null:foo ($input.color --or "red")>

=item --define-var

Create a scalar, array or object (only if no content).

=item --define-array

Create a Bedrock array.

=item --define-object

Create a Bedrock hash. All Bedrock hashes are ordered, with the
exception of those created using the C<--json> option.

 <null --define-object="foo" red 1 green 2 blue 3>

=item --encode

Encodes the object if any of the options:

 --bedrock-xml

 --json

 --xml
 
 --yaml

...are also present.  Normally these options indicate to Bedrock, how to
I<decode> the object during the assignment.  By using the C<--encode>
option you are asking Bedrock to create a scalar from the object and
encode it as a JSON or XML object.

 <null:env_xml --encode --bedrock-xml $env>

=item --escape

Escapes certain special characters (\n, \r, \t)

 <null:hello --escape "Hello World!\\n">

...is sometimes preferable to:

 <sink:foo>Hello World!
 </sink>

Normally Bedrock would interpret the string literally. Using the
C<--escape> option allows you to use the '\' character to indicate
special character like '\n'.

=item --expand-args

=item --expand-all

Expand array objects into lists.  If the C<--define-var> option is
used, this only applies to the first object in the list.  If the
C<--define-array> option is used, then all array references are
expanded into lists.  To force the expansion of all objects into a
list, use the C<--expand-all> option.

=item --file

Indicates that the argument is the name of a file and that it should
be slurped. If the file is not found or is empty, an exception will be
thrown.

 <null:config --bedrock-xml --file ($env.CONFIG_PATH + "/tagx.xml")>

You can add options as shown above that tell Bedrock how to interpret
the file.

 --bedrock-xml
 --json
 --xml
 --yaml

I<NOTE: Filenames are considered relative to the current working
directory. You can use the '~' to force the search path for the file
to include the C<DOCUMENT_ROOT> setting. Bedrock prohibits '..' or a
leading '/' typically used to traverse a file system B<UNLESS> you
specifically allow absolute paths by setting C<ABSOLUTE_PATHS> to a
true value in your configuration file.>

=item --handle

Indicates that the argument is a file handle and the file should be
slurped.

 <open:fh --mode="r" $file>
 <null:text --file $fh>

=item --json

For C<--define-var> consider the string to be a JSON string.

 <null:foo --json '{"red" : 1, "green" : 2, "blue" : 3}'>

 <null --define-var='foo' --json '{"red" : 1, "green" : 2, "blue" : 3}'>

I<HINT: Use the C<sink> tag instead.>

 <sink:foo --json>
 {
   "red": 1,
   "green": 2,
   "blue": 3
 }
 </sink>

I<PRO TIP: Use C<--yaml>>

 <sink:foo --yaml>
 red: 1
 green: 2
 blue: 3
 </sink>
 <trace --output $foo>
 
=item --range

Creates an array of integers. Similar to the range operator in
Perl. The argument to the range option should be a comma delimited
start and stop value (actually range will accept \d+\D+\d+, e.g: 1..10)

 <null:foo --range="1,10">

=item --recordset

Creates an object that is a C<Bedrock::Recordset> from an array.

=item --sleep

 <null --sleep=2>

Sleep for 2 seconds.

 <null --sleep>

Sleep for 1 second.

I<Note: If you find yourself using this tag, you probably should be
writing a plugin.>

=item --xml

For C<--define-var> consider the string to be an XML string.  Consider using
the C<< <sink> >> tag for this purpose.

 <null $config.XML('in', 'KeepRoot', 0)>
 <null:foo --xml '<colors\><red\>1</red\><green\>2</green\><blue\>2</blue\></colors\>'>

=back

=head1 NOTE

Arrays and hashes have a boatload of useful methods.  See the
documentation on the C<E<lt>hashE<gt>> and C<E<lt>arrayE<gt>> tags for
a description of the methods associated with those objects.

=head1 EXAMPLES

=over 5

=item * Create an object as the return result from some method:

 <null --define-var="x" $object.method()>

=item * Create a hash

 <null --define-object="foo" key1 value1 key2 value2>
 <null:hash:foo key1 value1 key2 value2>
 <null:foo --json '{key1 : "value1", key2 : "value2"}'>

Note that quotes around keys are not required.

=item * Create an array

 <null --define-array="x" 1 2 3 4>

 <null:array:x 1 2 3 4>

I<Bedrock assumes you want to create a list from a list ;-)>

 <null:x 1 2 3 4>

 <null:x --json '[1,2,3,4]'>

=item * Invoke a method, no return value

 <null $header.see_other("/new_url")>

=back

=head1 SEE ALSO

C<E<lt>hashE<gt>>, C<E<lt>arrayE<gt>>, C<E<lt>recordsetE<gt>>, C<Bedrock::Array>, C<Bedrock::Hash>

=cut
