#!@PERL@ -w

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

package TagX::TAG::WithBody::If;

use strict;
use warnings;

BEGIN {
  use Exporter ();
  use vars qw ($VERSION @ISA);
  @ISA = qw (Exporter);
}

use TagX::Log;
use TagX::TAG::WithBody;
use Scalar::Util qw/reftype looks_like_number/;
use Cwd;
use Data::Dumper;

@ISA = qw (TagX::TAG::WithBody);

sub file_test {
  my $file;

  if ( $_[1] =~/^(\S*)/ ) {
    $file = $1;
  } else {
    die "no filename specified: usage: filename --file file-test-op\n";
  }
  
  for ( $file ) {
    /^\~([^~]*)$/  && do {
      $file = getcwd() . '/' . $1;
      last;
    };
    
    /^[^\/~]/ && do {
      $file = $ENV{DOCUMENT_ROOT} . '/' . $file;
      last;
    }; 
  }

  
  $_[0] =~/^-?([rwxoRWXOezsfdlpSbctugkTBMAC])/ && do {
    return eval "-$1 \"". $file ."\" ? 1 : 0"; 
  };
  
  die "invalid file test operation: '$_[0] $_[1]'";

}

sub end_if {
  my $self = shift;
  $self->parent;
}

sub else_obj {
  my $self = shift;
  if ( @_ == 1 ) {
    $self->{'else'} = shift;
    $self->{'else'}
    ->parent_if( exists $self->{'parent_if'} ? $self->{'parent_if'} : $self );
    $self->{'else'}->parent( $self->parent );
  }
  $self->{'else'};
}

sub end_child {
  my $self = shift;
  $self->end_if;
}

sub closure {
  '/if';
}

sub evaluate {
  my $self = shift;
  my ($args, $context) = @_;

  if ( @{$args} == 1 ) {
    return $args->[0];
  }
  elsif ( @{$args} == 2 ) {

    # unary op
    my ( $op, $v ) = @{$args};

    for ($op) {
      $_ eq '--not'    and return not $v;
      if ( $_ eq '--exists' ) {
	my $what = $self->{'__argv'}->[1];

	if ( ref($what) eq 'TagX::Term::Var' ) {

	  return (defined $what->exists(@{$context}) ? 1 : 0);
	}
	elsif ( ref($what) ) {
	  return 1;
	}
	else {
	  return defined $v;
	}
      }

      $_ eq '--object' and return ref($v) && reftype($v) eq 'HASH';
      $_ eq '--hash'   and return ref($v) && reftype($v) eq 'HASH';
      $_ eq '--blm'    and return ref($v) =~ /blm/i;
      $_ eq '--plugin' and return ref($v) =~ /blm/i;
      $_ eq '--array'  and return ref($v) && reftype($v) eq 'ARRAY';
      $_ eq '--scalar' and return !ref($v);
      $_ eq '--defined' and return defined $v;

      die "invalid unary operator in `if/else' statement: $_\n";
    }
  }
  elsif ( @{$args} == 3 ) {
    my ( $lhs, $cond, $rhs ) = @{$args};
    my $ref = ref($lhs);
    {
      no warnings qw/uninitialized/;
      
      for ( lc($cond) ) {
	if ( looks_like_number($lhs) && looks_like_number( $rhs ) ) {
	  $_ eq '--gt'  and return $lhs > $rhs;
	  $_ eq '--ge'  and return $lhs >= $rhs;
	  $_ eq '--lt'  and return $lhs < $rhs;
	  $_ eq '--le'  and return $lhs <= $rhs;
	  $_ eq '--eq'  and return $lhs == $rhs;
	  $_ eq '--ne'  and return $lhs != $rhs;
	}
	else {
	  $_ eq '--gt'  and return $lhs gt $rhs;
	  $_ eq '--ge'  and return $lhs ge $rhs;
	  $_ eq '--lt'  and return $lhs lt $rhs;
	  $_ eq '--le'  and return $lhs le $rhs;
	  $_ eq '--eq'  and return $lhs eq $rhs;
	  $_ eq '--ne'  and return $lhs ne $rhs;
	  $_ eq '--file' and return file_test( $lhs, $rhs);
	}

	$_ eq '--and' and return $lhs && $rhs;
	$_ eq '--or'  and return $lhs || $rhs;
	$_ eq '--ref' and return ref($lhs) =~ /$rhs/;

	if ( $_ eq '--reftype' ) {
	  return ! ref($lhs) if $rhs eq 'SCALAR';
	  return ref($lhs) && reftype($lhs) eq $rhs;
	}

	if ($_ eq '--re') {
	  my $re = ($rhs =~ /^qr/) ? eval $rhs : eval "qr/\$rhs/";
	  if ( $^V ge 'v5.10') {
	    $lhs =~/$re/;

	    my %capture_group = %+;
	    return $lhs =~ /$re/ ? (1,\%capture_group) : undef;
	  }
	  else {
	    return $lhs =~ /$re/;
	  }
	}

	if ( $_ eq '--exists' ) {
	  die "invalid argument for '--exists': HASH --exists KEY\n"
	    unless ref($lhs) && reftype($lhs) eq 'HASH';

	  return ( exists $lhs->{$rhs} ) ? 1 : 0;
	
	}
      }
      
      die "invalid binary operator in `if/else' statement: $_\n";
    }
  }
  else {
    die 'invalid number of args(' . scalar(@{$args}) . " to 'if/else' statement.\n" . join(",", @{$args});
  }
}

sub finalize {
  my ( $self, $tx, @context ) = @_;

  my %options;

  my @argv = $self->parse_options( \%options, @context );
  my $verbose = $self->verbose;

  if ($verbose) {
    my ( $key, $value );
    my $str = "$self: Options are\n";
    while ( ( $key, $value ) = each %options ) {
      $str .= "\t$key .. ($value)\n";
    }
    $str .= "Arguments ..(@argv)";
    &log_message( $self, $str );
  }

  # If no args defined, means we are executing the else branch
  unless ( @argv > 0 ) {
    $self->SUPER::finalize( $tx, @context );
    return;
  }

  my ($v, $cg) = $self->evaluate(\@argv, \@context);

  map { $context[0]->{$_} = $cg->{$_};} keys %{$cg}
    if $v;

  $v = !$v if ref($self) =~ /unless/i;

  &log_message( $self,
       "Statement (@argv) evaluated to ($v): " . ( $v ? "(true)" : "(false)" ) )
  if $verbose;
  if ($v) {
    &log_message( $self, "Condition succeeded .." ) if $verbose;
    $self->SUPER::finalize( $tx, @context );
  }
  elsif ( $self->else_obj ) {
    &log_message( $self, "Condition failed .. trying <else[if]> branch" )
    if $verbose;
    $self->else_obj->finalize( $tx, @context );
  }
}

=pod

=head1 TAG - C<E<lt>ifE<gt>>

=head1 PURPOSE

Conditional inclusion of a block of text.

=head1 SYNTAX

  <if boolean-expression >
   ...
  <elseif test>
  <elsif test>
   ...
  <else>
   ...
  </if>

Where I<boolean-expression> is a Bedrock expression that evaluates to
true or false (in the Perlish sense).

Bedrock dyadic (has two arguments) boolean expressions include:

 arg1  --eq      arg2
 arg1  --ne      arg2
 arg1  --lt      arg2
 arg1  --le      arg2
 arg1  --gt      arg2
 arg1  --ge      arg2
 arg1  --re      regexp
 arg1  --or      arg2
 arg1  --and     arg2
 arg1  --ref     arg2
 arg1  --reftype arg2
 arg1  --file    arg2

Bedrock monadic (has 1 argument) boolean expressions include:

 --not     arg1 => true if ! arg1
 --exists  arg1 => true if exists arg1
 --defined arg1 => true if defined arg1

 --scalar  arg1 => true if arg1 is a scalar
 --array   arg1 => true if arg1 is an array object
 --hash    arg1 => true if arg1 is a hash object
 --plugin  arg1 => true if arg1 is a Plugin

Deprecated

 --blm    arg1 => true if arg1 is a BLM 
 --object arg1 => true if arg1 is a hash object

=head1 OPTIONS

None

=head1 DESCRIPTION

The C<E<lt>ifE<gt>> tag pedantically is used to I<conditionally
assemble> portions of a document.  In other words the statement is
designed to conditionally include 1 of possibly several different
alternate snippets of a Bedrock page to be processed.  This being a
I<templating> language, you should not consider this a flow control
mechanism, despite what you might read elsewhere in Bedrock
documentation!  It will mess with your head.

If you find yourself using the C<E<lt>ifE<gt>> tag for gnarly business
logic (i.e. programming), write an I<Application Plugin> please.

=head1 EXAMPLES

 <if $input.name>
   <null --define-var="results" $foo.search($input.name)>
   <foreach $results>
      <var $fname> <var $lname><br>
   </foreach>
 <else>
   <form>
     <input type="name" size="40" value="<var $input.name>">&nbsp;
     <input type="submit">
   </form>
 </if>

=head1 NOTES

=over 5

=item 1.

When two arguments are used C<arg1> must be a Bedrock object

 <if $foo --eq 1>

not

 <if 1 --eq "foo">

=item 2.

Comparisons are done in the alpha-numeric sense, if either argument does not look like a number

=item 3.

Compound tests (more than one expression) are allowed, but use parentheses liberally

=item 4.

Whitespace is ALWAYS preserved by Bedrock!

If you donE<039>t want extra space in your HTML then construct accordingly.
In other words, obsessive indenting may look good to you in your raw
Bedrock page, but the resulting HTML will reflect your indenting.
Bedrock is essentially a templating tool, thus it interprets pages
literally.

The snippet below illustrates where whitespace (.) and newlines (^) will be preserved.

  <if $input.action --eq "save">^
  ...<null $foo.save()>^
  </if>^

If you wanted a cleaner HTML output youE<039>d probably want:

  <if $input.action --eq "save"><null $foo.save()></if>

Of course, there is always C<E<lt>sinkE<gt>>.

=item 5.

C<--re> evaluates simple regular expressions using the following Perl pseudo code

   $lhs =~ /$rhs/

...however you can make them a tad more complex by using a quoted
regular expression.

 <if $input.name --re "qr/foo/i">foo!</if>

The regular expression you pass is (C<$rhs>). I<Note that the Bedrock
parser will look for 'qr' at the beginning of your string to determine
if it is a regular expression.>

=item 6.

C<--ref> evaluates to true if C<ref($arg1) = $arg2>

  <if $input --ref "BLM:Startup::Input">
    This is the input object!
  </if>

=item 7.

C<--reftype> evaluates to true if C<ref($arg1) and reftype($arg1) = $arg2>

  <if $foo --reftype 'HASH'>
    This is a HASH!
  </if>

C<$obj.reftype()> will also return the object type ('HASH', 'ARRAY' or 'SCALAR').

=item 8.

Bedrock 2 introduces the C<E<lt>caseE<gt>> tag.  If it feels better using if, then use if, else use  C<E<lt>caseE<gt>>.

  <if $input.radio_1>checked=checked </if>
  <case $input.radio_1 "checked=checked ">

=item 9.

C<--file> is equivalent to PerlE<039>s file test operators

=over 10

=item *

The left hand argument should be file test operator (e.g. C<-s>).  The
right hand argument should be a file name.

=item *

If the right hand argument begins with a '/' (back-slash), then the
argument is considered the fully qualifed pathname of a file.

=item *

If the right hand argument begins with a tilde (C<~>), then the
argument is considered to be a file in the current working directory.

=item *

If the argument begins with neither a back-slash or tilde, then the
argument is considered to be a pathname relative to the document root.

=back

=item 10.

Named capture groups in regular expressions

If you are running a version of perl > 5.10, then you regular
expression may include named capture groups.

 <if $path_info --re '/members\/(?<letter\>[a-zA-Z])$'>
   <null:contact $contact.get_contacts_by_letter($letter, $letter, 'member')>
 </if>

B<Please note the escaping of the final E<gt> (greater than) symbol.  If
you do not escape the E<gt>, Bedrock will report a syntax error while trying to
parse your statement. Alternately, avoid using the E<lt>, E<gt> symbols as delimiters.>

 <if $path_info --re "/members\/(?'letter'[a-zA-Z])$">
   <null:contact $contact.get_contacts_by_letter($letter, $letter, "member")>
 </if>

=back

=head1 SEE ALSO

C<E<lt>caseE<gt>>, C<E<lt>unlessE<gt>>, C<E<lt>iifE<gt>>, C<E<lt>whileE<gt>>

=cut

#
# Revision 1.36  2013/03/27 18:54:42  rlauer
# - regular expression was not being used as documented, instead eq was
#
# Revision 1.35  2013/03/21 16:31:56  rlauer
# - --file operator did not properly implement absolute filename testing according to the documentation.
#
# Revision 1.34  2013/01/21 00:09:50  rlauer
# - add support for --file (file test operators)
#
# Revision 1.33  2012/07/17 15:36:31  lauerrc
# - in the evaluate() method we now use numeric comparison operators if both the lhs and the rhs of the expression look like numbers
#
# Revision 1.32  2011/11/30 19:33:21  eutl420
# - pod update to --re section
#
# Revision 1.31  2011/11/30 19:04:36  eutl420
# - pod typo
#
# Revision 1.30  2011/11/30 19:02:16  eutl420
# - pod changes, see "unless", explain use of two argument form, i.e. arg1 must be object
#
# Revision 1.29  2011/09/21 20:40:23  eutl420
# - allow qr// in --re expressions
# - pod tweaks
#
# Revision 1.28  2011/09/15 16:05:33  eutl420
# - perl tidy
#
# Revision 1.27  2011/09/09 19:45:49  eutl420
# - in the evaluate method, parse_options is now returning unknown options with '--' intact, so we should test for those
#   instead of naked operators
# - also need to add tests for --scalar, --array, --plugin
# - tweaked pod for better example
#
# Revision 1.26  2011/08/18 21:51:00  eutl420
# - invert test if the tag is an unless tag in finalize()
#
# Revision 1.25  2011/07/17 16:56:09  eutl420
# - build .pm from .pm.in, added @PERL@
#
# Revision 1.24  2011/07/12 19:10:37  eutl420
# set VERSION from cvs Revision keyword
#
# Revision 1.23  2010/11/29 15:57:54  rlauer
# - change openbedrock.org to openbedrock.net
#
# Revision 1.22  2010/11/17 02:49:52  rlauer
# - pod cleanup
#
# Revision 1.21  2010/11/17 02:45:52  rlauer
# - pod cleanup
#
# Revision 1.20  2010/11/17 02:27:35  rlauer
# - pod cleanup
#
# Revision 1.19  2009/12/31 18:38:42  rlauer
# updated docs
#
# Revision 1.18  2009/12/31 12:43:37  rlauer
# docs tweaks
#
# Revision 1.17  2009/12/31 00:02:05  rlauer
# use UNIVERSAL::isa to catch weird objects
#
# Revision 1.16  2008/12/03 14:01:20  alans
# We now use a hard-coded value for module $VERSION rather than a munged
# version of the expansion of the CVS 'Revision' keyword; the module API
# version number is orthogonal to the identifyer in the source control
# system.
#
# This change makes it safe to import the Bedrock codebase into another
# source repository (even CVS!) without breaking the code. We hit this
# issue when we first released the libbedrock-perl .deb package, for which
# we imported the release tarball sources into the 'bedrock-deb' CVS project.
#
# Revision 1.15  2008/12/02 15:02:31  rlauer
# Added pod for tag reference
#
# Revision 1.14  2008/11/25 19:04:13  rlauer
# changes based on Bedrock 2.0 migration plan
#
# Revision 1.13  2008/11/12 13:49:58  rlauer
# pod changes
#
# Revision 1.12  2008/11/12 13:40:54  rlauer
# - added pod, added --blm, --object --scalar, --array boolean operators
#
# Revision 1.11  2001/02/14 15:35:50  sridhar
# Added copyright and GPL notice
#
# Revision 1.10  2000/12/15 14:30:20  sridhar
# Subclass overrides method `closure()' to report the correct way for
# closing the tag.
#
# Revision 1.9  2000/05/31 14:42:23  sridhar
# Rigorous version numbering and usage
#
# Revision 1.8  2000/04/24 12:49:13  sridhar
# Using TagX::Log for logging messages
#
# Revision 1.7  2000/04/13 15:21:01  sridhar
# Using new parse_options method with support for expressions
#
# Revision 1.6  2000/03/23 20:37:12  sridhar
# 1. Returns the value when evaluating checking a value
# 2. Added --exists condition
#
# Revision 1.5  2000/03/21 16:14:12  sridhar
# 1. Major changes: Support for expressions joined with --and and
# --or. Also supports --not. Expressions delimited by '(' and ')'.
# 2. Expression parser is recursive
#
# Revision 1.4  2000/03/06 18:36:26  sridhar
# 1. Bug-fix: when an argument to tag was `0', not being seen due to
# checking for non-zero value
# 2. Added more verbose logging only on demand using the --verbose
# option.
#
# Revision 1.3  2000/02/23 13:43:01  sridhar
# Bug fix: $_ was being used/set without local($_) declaration.
#
# Revision 1.2  2000/02/08 15:37:45  sridhar
# Added support for being able to use Exporter version numbers
#
# Revision 1.1  2000/02/08 15:25:01  sridhar
# Added TagX
#
#

1;
