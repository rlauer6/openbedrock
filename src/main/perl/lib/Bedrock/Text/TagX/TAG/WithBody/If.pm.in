#!/usr/bin/perl -w

# ident "@(#) $Header$"
# ident "@(#)        $Name$"

package TagX::TAG::WithBody::If;

use strict;

BEGIN
{
    use vars	qw ($VERSION @ISA);
    $VERSION	= do { my @r = (q$Revision$ =~ /\d+/g); sprintf "%d."."%02d" x $#r, @r };
}

use Getopt::Long;

use TagX::TAG::WithBody;

@ISA = qw (TagX::TAG::WithBody);

sub end_if
{
    my $self = shift;
    $self->parent;
}

sub else_obj
{
    my $self = shift;
    if ( @_ == 1 ) {
	$self->{'else'} = shift;
	$self->{'else'}->parent_if ( exists $self->{'parent_if'} ? $self->{'parent_if'} : $self );
	$self->{'else'}->parent ( $self->parent );
    }
    $self->{'else'};
}

sub end_child
{
    my $self = shift;
    $self->end_if;
}

sub evaluate
{
    my ($lhs, $rhs, $cond) = @_;
    for (lc($cond)) {
	$_ eq 'gt' and return $lhs gt $rhs;
	$_ eq 'ge' and return $lhs ge $rhs;
	$_ eq 'lt' and return $lhs lt $rhs;
	$_ eq 'le' and return $lhs le $rhs;
	$_ eq 'eq' and return $lhs eq $rhs;
	$_ eq 'ne' and return $lhs ne $rhs;
	$_ eq 're' and return $lhs =~ /$rhs/ ? 1 : 0;
    }
}

sub finalize
{
    my ($self, $tx, @context) = @_;

    # If no args defined, means we are executing the else branch
    unless ( exists $self->{'argv'} ) {
	$self->SUPER::finalize( $tx, @context );
	return;
    }

    local (@ARGV) = @{$self->{'argv'}};
    my %options = ('verbose' => 0);
    &Getopt::Long::config("pass_through");
    &GetOptions(\%options,
		'verbose!');
    my $verbose = $options{'verbose'};

    $tx->log_message ( MSG_DEBUG => "$self: Arguments are\n\t(@ARGV)" ) if $verbose;
    my (@v, $cond);
    local( $_ );
    while (defined($_ = shift @ARGV)) {
	if ( /^--name$/oi ) {
	    my $name = shift @ARGV;
	    my $data = $self->resolve_name ( $name, @context );
	    $tx->log_message( MSG_DEBUG => "$self: $name = <$data>" ) if $verbose;
	    push @v, defined( $data ) ? $data : '';
	    next;
	}

	if ( /^--(eq|ne|gt|ge|lt|le|re)$/oi ) {
	    $cond = $1;
	    next;
	}

	push @v, $_;
    } continue {
	if ( defined( $cond ) and @v == 2 ) {
	    my $str = "$self: Condition { $v[0] $cond $v[1] } evaluated to " if $verbose;
	    @v = (&evaluate( @v, $cond ));
	    $tx->log_message( MSG_DEBUG => $str . $v[0] ) if $verbose;
	    $cond = undef;
	}
    }
    $tx->log_message( MSG_DEBUG => "Condition evaluated to ($v[0]): " . ($v[0] ? "(true)" : "(false)" ))
	if $verbose;
    return unless defined( $v[0] );
    if ( $v[0] ) {
	$tx->log_message( MSG_DEBUG => "Condition succeeded .." ) if $verbose;
	$self->SUPER::finalize ( $tx, @context );
    } elsif ( $self->else_obj ) {
	$tx->log_message( MSG_DEBUG => "Condition failed .. trying <else[if]> branch" ) if $verbose;
	$self->else_obj->finalize ( $tx, @context );
    }
}

#
# Name of Release: $Name$
# $Log$
# Revision 1.4  2000/03/06 18:36:26  sridhar
# 1. Bug-fix: when an argument to tag was `0', not being seen due to
# checking for non-zero value
# 2. Added more verbose logging only on demand using the --verbose
# option.
#
# Revision 1.3  2000/02/23 13:43:01  sridhar
# Bug fix: $_ was being used/set without local($_) declaration.
#
# Revision 1.2  2000/02/08 15:37:45  sridhar
# Added support for being able to use Exporter version numbers
#
# Revision 1.1  2000/02/08 15:25:01  sridhar
# Added TagX
#
#

1;
