#!/usr/bin/perl -w

# ident "@(#) $Header$"
# ident "@(#)        $Name$"

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.org
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

package TagX::TAG::WithBody::Sink;

use strict;
use SIS::Hash;
use SIS::Array;
use SIS::JSON;

BEGIN
{
    use Exporter ();
    use vars	qw ($VERSION @ISA);
    $VERSION    = 1.07;
}

use TagX::TAG::WithBody;

@ISA = qw (TagX::TAG::WithBody);

sub closure
{
    '/sink';
}

sub evolve {
  my $this = shift;

  if (ref($this) eq 'HASH') {
    foreach (keys %$this) {
      $this->{$_} = evolve($this->{$_});
    }
    return SIS::Hash->new(%$this);
  }
  elsif (ref($this) eq 'ARRAY') {
    for (my $idx=0; $idx<@$this; $idx++) {
      $this->[$idx] = evolve($this->[$idx]);
    }
    return SIS::Array->new(@$this);
  }
  else {
    return $this;
  }
}

sub finalize
{
    my ($self, $tx, @context) = @_;

    my %options = ('define-var=s' => undef,
		   'htmlencode'	=> undef,
		   'htmldecode'	=> undef,
		   'json' => undef);
    my @argv = $self->parse_options (\%options, @context);
    my $verbose = $self->verbose;
    if ( $verbose ) {
	my ($key, $value);
	my $str = "$self: Options are\n";
	while (($key, $value) = each %options) {
	    $str .= "\t$key .. ($value)\n";
	}
	$str .= "Arguments ..(@argv)";
	&log_message( $self, $str );
    }

    # Bedrock2 syntax <sink:var  args>
    if ($argv[0] =~/^:/) {
      my $varname = shift @argv;
      $varname =~s/^:(.*?)$/$1/;
      $options{'define-var'} = $varname if $varname;
    }

    die "Use only one of --HTMLEncode or --HTMLDecode here.\n"
	if $options{'htmlencode'} and $options{'htmldecode'};

    # Create a frame for output. Note very carefully that we assign the frame
    # to a lexical variable. Hence the destructor for the frame is not invoked
    # When the lexical variable goes out of scope, the frame is destructed,
    # and since we didn't invoke $frame->end_frame, the contents of the frame
    # are discarded.
    my $frame = $tx->out_handle->create_frame;
    &log_message( $self, "Entering <SINK> block .." ) if $verbose;
    $self->SUPER::finalize ( $tx, @context );
    &log_message( $self, "Exited <SINK> block" ) if $verbose;

    my $data = $tx->out_handle->frame_data;
    if ( $options{'htmlencode'} ) {
	$data =~ s/\x26/\&amp;/g;
	$data =~ s/\x3c/\&lt;/g;
	$data =~ s/\x3e/\&gt;/g;
	&log_message ( $self, "HTMLEncoded to <$data>" ) if $verbose > 1;
    } elsif ( $options{'htmldecode'} ) {
	$data =~ s/\x26amp;/\x26/g;
	$data =~ s/\x26lt;/\x3c/g;
	$data =~ s/\x26gt;/\x3e/g;
	&log_message ( $self, "HTMLDecoded to <$data>" ) if $verbose > 1;
      } elsif ($options{'json'}) {
	$data = SIS::JSON::decode($data);
      }

    if ( exists $options{'define-var'} ) {
	&log_message( $self, "Assigning contents of <SINK> to $options{'define-var'}" ) if $verbose;
	$tx->param( $options{'define-var'} => $data );
    }

    # Expect a single argument which should be a handle to which we print
    # the frame contents to
    if ( @argv == 1 ) {
	my $fd = shift @argv;
	print $fd $data;
	close($fd);
    }

    # at this point, since we didn't end the frame, the contents should be
    # discarded as soon as we leave here.
}

1;

=pod

=head1 TAG sink

=head1 PURPOSE

Capture the block of text encapsulate by the <sink></sink>
construction.  Output is either directed to a scalar, a handle or the
bit bucket.

=head1 SYNTAX

  <sink [--define-var="name"] [--json] [handle]>
  </sink>

  <sink:name [--json] [handle>
  </sink>

=head1 OPTIONS

=over 5

=item --define-var

Name of scalar object that will contain the output.

=item --json

Consider the content of the <sink> block to be JSON and convert it
into a Bedrock object or array.

=item handle

An open handle, typically opened by another BLM.

=back

=head1 EXAMPLES

=over 5

=item Sinking text to a scalar object

  <sink --define-var="foo">This is a test of sinking some text to the scalar "foo"</sink>
  <var $foo>

  <sink:foo --json>
  [ 
    {item: "chairs", price: "100"},
    {item: "cooler", price: "29.95"},
    {item: "corona", price: "32"}
  ]
  </sink>

=item Sinking text to a handle

  <blm:FileIO --define-var="fd" ($config.LOGDIR+"/foo.bar") "w">
  <sink $fd.handle>Write this line to the specified handle</sink>
  <null $fd.close()>

=back 

=cut


#
# Name of Release: $Name$
# $Log$
# Revision 1.13  2009/12/16 23:23:39  rlauer
# use SIS::JSON, docs, sink:name tag syntax
#
# Revision 1.12  2009/12/15 12:39:58  rlauer
# promote objects to bedrock objects with evolve
#
# Revision 1.11  2009/12/09 12:53:23  rlauer
# very loose JSON decode - support bare word keys and embedded new lines
#
# Revision 1.10  2009/12/07 00:34:39  rlauer
# make object from sink tag a SIS::Hash or SIS::Array
#
# Revision 1.9  2009/12/06 23:56:28  rlauer
# JSON support
#
# Revision 1.8  2008/12/03 14:01:21  alans
# We now use a hard-coded value for module $VERSION rather than a munged
# version of the expansion of the CVS 'Revision' keyword; the module API
# version number is orthogonal to the identifyer in the source control
# system.
#
# This change makes it safe to import the Bedrock codebase into another
# source repository (even CVS!) without breaking the code. We hit this
# issue when we first released the libbedrock-perl .deb package, for which
# we imported the release tarball sources into the 'bedrock-deb' CVS project.
#
# Revision 1.7  2008/11/12 12:51:31  rlauer
# added pod
#
# Revision 1.6  2008/02/28 18:14:43  rlauer
# Applying Rob's patches to make Bedrock work on Apache 2.x
# (plus a few other obvious bug fixes).
#
# Revision 1.5  2002/02/13 17:56:56  sridhar
# File handle arg is not closed.
#
# Revision 1.4  2001/02/14 15:35:50  sridhar
# Added copyright and GPL notice
#
# Revision 1.3  2000/12/15 14:30:20  sridhar
# Subclass overrides method `closure()' to report the correct way for
# closing the tag.
#
# Revision 1.2  2000/11/21 19:18:05  sridhar
# 1. Added --define-var option to <sink> for defining a variable with the
# contents if <sink></sink>.
# 2. Added --HTMLEncode and --HTMLDecode options to encoding and
# decoding the contents of <sink></sink>
#
# Revision 1.1  2000/10/19 20:57:18  sridhar
# Moved Hush.pm to Sink.pm and added capability for passing an argument
# to <sink> tag which should be a handle to which the output of the body
# of <sink> can be written
#
#
