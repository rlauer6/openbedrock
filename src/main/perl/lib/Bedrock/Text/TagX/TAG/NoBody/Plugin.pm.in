package TagX::TAG::NoBody::Plugin;

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2024, TBC Development Group, LLC.
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

use strict;
use warnings;

use Bedrock::Constants qw(:chars :booleans);
use Bedrock::Dump;
use Bedrock::Hash;
use Bedrock::Plugin;
use English    qw(-no_match_vars);
use List::Util qw(any none);
use Symbol;

use parent qw (Exporter TagX::TAG::NoBody);

use Role::Tiny::With;
with 'Bedrock::Logger';

########################################################################
sub define_options {
########################################################################
  return (
    'define-var=s'    => undef,
    'dir-prefix=s'    => undef,
    'define-plugin=s' => undef,
    'tie'             => undef,
    'init=s'          => undef,
  );
}

########################################################################
sub option_names {
########################################################################
  my ($self) = @_;

  return map { /(^[^=]+)/xsm && $1; } ( $self->define_options );
}

########################################################################
sub finalize {
########################################################################
  my ( $self, $tx, @context ) = @_;

  my %options = $self->define_options;

  local $TagX::TAG::PROHIBIT_UNKNOWN_OPTIONS = 0;

  my @argv = $self->parse_options( \%options, @context );

  my %extra_options;

  foreach my $val (@argv) {
    next if $val !~ /^\-\-(.*)$/xsm;

    $extra_options{$1} = undef;
  }

  %options = ( %extra_options, $self->define_options );

  # reset parse_options() so it will re-evaluate arguments with new options
  delete $self->{__args_to_eval};

  @argv = $self->parse_options( \%options, @context );

  $self->dump_options( \%options, @argv );

  my $define_var = $options{'define-var'} //= $UNDERSCORE;
  $tx->var_exists($define_var);

  my $config = $tx->config;  # $tx->out_handle->{Handle}->{'CONFIG'};

  my $tag = shift @argv;

  my ( $name, $class ) = $self->parse_tag_name( $tag, $options{'plugin-name'} );
  $tx->var_exists($name);

  my $plugin = $class =~ /^BLM/xsm ? $class : 'BLM::' . $class;

  # still undecided about this
  if ( exists $options{'dir-prefix'} ) {
    if ( none { $options{'dir-prefix'} eq $_ } @INC ) {
      push @INC, $options{'dir-prefix'};
    }
  }

  # adhoc addition of a PLUGIN path
  if ( $config->{PLUGIN_PATH} ) {
    my @plugin_inc = split /:/xsm, $config->{PLUGIN_PATH};

    foreach my $path (@plugin_inc) {
      next if any { $path eq $_ } @INC;
      push @INC, $path;
    }
  }

  $class = $self->loadPlugin( $plugin, $class );

  my $plugin_obj = eval {

    if ( $class->can('new') && !exists $options{tie} ) {  ## no critic (ProhibitCascadingIfElse)
      $self->get_logger->trace(
        sub {
          Bedrock::Dump::compact [ 'args', \@argv ];
        }
      );

      return $class->new(@argv);
    }
    elsif ( $class->can('TIEHASH') ) {
      $self->get_logger->debug(
        sub {
          ref($self) . " creating an object of class $class";
        }
      );

      my $obj = {};
      tie %{$obj}, $class, @argv;  ## no critic (ProhibitTies)

      return $obj;
    }
    elsif ( $class->can('TIEARRAY') ) {
      $self->get_logger->debug(
        sub {
          ref($self) . "creating an array of class $class";
        }
      );

      my $obj = [];
      tie @{$obj}, $class, @argv;  ## no critic (ProhibitTies)

      return $obj;
    }
    elsif ( $class->can('TIEHANDLE') ) {
      $self->get_logger->debug(
        sub {
          ref($self) . " creating a handle of class $class";
        }
      );

      my $obj = gensym;
      tie *{$obj}, $class, @argv;  ## no critic (ProhibitTies)

      return $obj;
    }
    elsif ( $class->can('TIESCALAR') ) {
      $self->get_logger->debug(
        sub {
          ref($self) . " creating a scalar of class $class";
        }
      );

      my $obj = $argv[0];
      tie $obj, $class;            ## no critic (ProhibitTies)

      return $obj;
    }
    else {
      die "Attempt to load invalid module: $class.\n";
    }
  };

  my $err = $EVAL_ERROR;

  print {*STDERR} $EVAL_ERROR;

  die "unable to  load plugin ($class)\n$err"
    if !$plugin_obj || $err;

  if ( ref $plugin_obj ne $class ) {
    bless $plugin_obj, $class;
  }

  # call the plugin's init_plugin() if it exists...note that <plugin>
  # can be used to access "some" Perl modules that are not derived
  # from Bedrock::Plugin

  my @params;
  my $init_method;

  # new BLM::Plugin classes get called with different parameters
  # than legacy Bedrock::Plugins...
  if ( $self->isa_plugin( $class, $options{init} ) ) {
    $init_method = $options{init} // 'init_plugin';

    my %valid_options = $self->define_options;

    @params = (
      params        => \@argv,
      config        => $config,
      options       => \%options,
      context       => \@context,
      valid_options => [ keys %valid_options ],
    );
  }
  else {
    # legacy Bedrock::Plugins and others just get the plugin args
    @params      = @argv;
    $init_method = 'init_plugin';
  }

  if ( ref $plugin_obj && $plugin_obj->can($init_method) ) {
    if ( $plugin_obj->can('verbose') ) {
      $plugin_obj->verbose( $self->verbose );
    }

    if ( $plugin_obj->can('options') && !$self->isa_plugin( $class, $options{init} ) ) {
      $plugin_obj->options( Bedrock::Hash->new(%options) );
    }

    $tx->{VARS}->{$define_var} = $plugin_obj->$init_method(@params);
  }

  $tx->{VARS}->{$name} = $plugin_obj;

  return $tx->{VARS}->{$name};
}

########################################################################
sub parse_tag_name {
########################################################################
  my ( $self, $tag, $plugin_name ) = @_;

  my ( $name, $type ) = @{ $self->SUPER::parse_tag_name($tag) // {} }{qw(name type)};

  return ( $plugin_name, $name || $type )
    if $plugin_name;

  $name =~ s/:://gxsm;

  return ( $name, $type );
}

########################################################################
sub isa_plugin {
########################################################################
  my ( $self, $class, $init ) = @_;

  return $TRUE
    if $init || $class eq 'BLM::Plugin';

  my $isa = do {
    no strict 'refs';  ## no critic (ProhibitNoStrict)
    \@{ $class . '::ISA' };
  };

  return $init || any { $_ eq 'BLM::Plugin' } @{ $isa || [] };
}

########################################################################
sub loadPlugin {
########################################################################
  my ( $self, $plugin, $class ) = @_;

  # We first look for a BLM, then a class with the name which allows
  # us to use some Perl modules as Plugins.
  if ( $self->loadModule($plugin) ) {
    $class = $plugin;
  }
  elsif ( $self->loadModule($class) ) {
    $self->get_logger->debug(
      sub {
        sprintf 'Successfully loaded Perl class (%s)', $class;
      }
    );
  }
  else {
    die sprintf
      "Could not load module: [%s]. Check your syntax or your plugin path.\n",
      $class;
  }

  return $class;
}

########################################################################
sub loadModule {
########################################################################
  my ( $self, $module ) = @_;

  $module =~ s/::/\//gxsm;

  if ( $module !~ /[.]pm$/xsm ) {
    $module = $module . '.pm';
  }

  return $module
    if $INC{$module};

  eval { require $module; };

  print {*STDERR} $EVAL_ERROR;

  if ($EVAL_ERROR) {
    $self->get_logger->error(
      sub {
        "module: [$module] not loaded!\n" . $EVAL_ERROR;
      }
    );
  }

  $self->get_logger->debug(
    sub {
      "module: [$module] loaded successfully";
    }
  );

  return $EVAL_ERROR ? undef : $module;
}

1;

## no critic (RequirePodSections)

__END__

=pod

=head1 TAG - C<E<lt>pluginE<gt>>

=head1 PURPOSE

Load a Bedrock Plugin or Perl module.

=head1 SYNTAX

 <plugin:[plugin name][:name] [--define-var="name"] args...>

=head1 DESCRIPTION

Plugins are one of the ways you can extend Bedrock.  They are
typically used to provide some functionality specific to your
application or add features not found in core Bedrock.

Plugins are used to invoke methods that perform actions or that return
objects or snippets of HTML for assembling your page.

=head1 OPTIONS

 --define-var    = "var-name"
 --define-plugin = "var-name"
 --tie

=over 5

=item --define-var

Name of the object that will hold the return value of the Plugin's
initialization function.  If not supplied the value will be returned
in C<$_>.

=item --define-plugin

Name of the object that will be instantiated as the given Plugin.
If not supplied the object will have the same name as the Plugin.  Some examples:

 <plugin:Mail>  <=> <plugin:Mail --define-plugin="Mail">

 <plugin:BLM::Mail> <=> <plugin:BLM::Mail --define-plugin="BLMMail">

I<By convention, you we don't use the full package name when
specifying the plugin, although you are more than welcome to do that
if you would like.>

=item --tie

Forces the plugin to tie the plugin class rather than invoking a
C<new()> method.  Some Perl classes are designed to be tied, while
others may expect (by convention) that you instantiate them using a
C<new()> method.  This option forces a tie even if the Perl class
contains a C<new()> method.

=back

=head1 NOTES

Plugins are just Perl modules that have implemented a C<new()> method
or ones that can be C<tie>'d.  Bedrock will pass any of the argument
in the tag to the C<new()> constructor.

You can also subclass the Perl C<Bedrock::Plugin> class.  Typically a
plugin will look something like this:

 package BLM::Foo;

 use strict;
 use warnings;

 use parent qw/Bedrock::Plugin/;

 use Bedrock::Hash;
 use Bedrock::Array;
 
 sub init_plugin {
   my $self = shift;
   my @args = @_;

   "Greetings from Foo!";
 }
 
 sub hello_world {
   my $self = shift;

   "Hello World!";
 }

 sub return_a_hash {
   return Bedrock::Hash->new( color => "red", value => 1 );
 }

 sub return_an_array {
   return Bedrock::Array->new( qw/red blue green/ );
 }

 1;


=head1 TIPS

=over 5

=item Returning a value upon initialization

You should override the C<init_plugin> method if you want to return a
value upon instantiation.

=item Passing arguments to the plugin

The plugin tagE<039>s arguments are passed to your initialization
function or the new constructor if your are instantiating a Perl
module.

 <plugin:FileIO "foo.txt" "r">

=item Plugin location

Plugins should be placed in a location where Perl will find them.
They should be part of the C<BLM> namespace.

 package BLM::Foo;

I<In case you are wondering - C<BLM> = Bedrock Loadable Module.>

If you are running Bedrock as a CGI, you probably want to make sure
the the C<perl.INC> file points to a directory that contains your
plugins.

If you are running C<mod_perl> you probably want to use
C<PerlSwitches> or create a C<startup.pl> script that adds the
necessary paths to C<@INC>.

You can also set the configuration variable C<PLUGIN_PATH> in F<tagx.xml>.

 <scalar name="PLUGIN_PATH">/var/www/vhosts/mysite.com/share/perl5</scalar>


=item Retrieving plugin options

Options you might pass to the plugin (e.g. --define-var) can be
retrieved using the C<options()> method> if youE<039>ve subclassed
C<Bedrock::Plugin>

=back

=head1 EXAMPLE

Load the C<BLM::FileIO> plugin and write something to the error log.

 <plugin:FileIO --define-plugin="fd" "/var/www/mysite.com/logs/error.log" "w+">

 <null $fd.write("Hello World!\n");
 <null $fd.close>

 <plugin:FileIO "/var/www/mysite.com/logs/error.log" "w+">
 <null $FileIO.write("Hello World!\n");
 <null $FileIO.close>

=head1 USING OTHER PERL MODULES

Beginning with Bedrock 2.3.1 you can load most Perl modules and use
their methods as if they were a true Bedrock Plugin.  

Keep in mind that objects returned by these Perl modules are B<not>
I<Bedrock> objects.  Use the C<E<lt>hashE<gt>> and C<E<lt>arrayE<gt>>
tags to coerce these objects into Bedrock objects.

Example. Assume method C<bar()> returns an array.

 <plugin:Foo>

 <array:bar --expand-args $Foo.bar()>

Example. Assume method c<bas()> returns a hash reference.

 <plugin:Foo>
 
 <hash:baz $Foo.baz()>

=head1 SEE ALSO

C<Bedrock::Plugin>, C<Bedrock::Application::Plugin>, C<E<lt>pebbleE<gt>>, 
C<E<lt>hashE<gt>>, C<E<lt>arrayE<gt>>, 

=cut
