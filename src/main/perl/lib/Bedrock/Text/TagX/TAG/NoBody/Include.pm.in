#!/usr/bin/perl -w

# ident "@(#) $Header$"
# ident "@(#)        $Name$"

package TagX::TAG::NoBody::Include;

use strict;

BEGIN
{
    use vars	qw ($VERSION @ISA);
    $VERSION	= do { my @r = (q$Revision$ =~ /\d+/g); sprintf "%d."."%02d" x $#r, @r };
}

use POSIX qw (strftime);

use TagX::TAG::NoBody;

@ISA = qw (TagX::TAG::NoBody);

sub finalize
{
    my ($self, $tx, @context) = @_;

    my %options = ('file=s'			=> undef,
		   'dir-prefix=s'		=> undef,
		   'access-time=s'		=> undef,
		   'access-time-format=s'	=> undef,
		   'modify-time=s'		=> undef,
		   'modify-time-format=s'	=> undef,
		   'change-time=s'		=> undef,
		   'change-time-format=s'	=> undef);
    my @argv = $self->parse_options (\%options, @context);
    my $verbose = $self->verbose;

    if ( $verbose ) {
	my ($key, $value);
	my $str = "$self: Options are\n";
	while (($key, $value) = each %options) {
	    $str .= "\t$key .. ($value)\n";
	}
	$str .= "Arguments are\n@argv";
	$tx->log_message( MSG_DEBUG => $str );
    }

    my $file;
    if ( exists $options{'file'} ) {
	$file = delete $options{'file'};
    } elsif ( @argv ) {
	$file = shift @argv;
    } else {
	$tx->log_message( MSG_ERROR => "$self: No file specified" );
	return;
    }

    my $dir;
    if ( defined( $dir = delete $options{'dir-prefix'} ) ) {
	$tx->log_message( MSG_DEBUG => "Dir prefix is <$dir>" ) if $verbose;
	$file = $dir . '/' . $file;
    }

    $tx->log_message( MSG_DEBUG => "Including file <$file>" ) if $verbose;

    my $more = keys %options;
    my @sv;
    if ( $more ) {
	@sv = stat ( $file );
	return unless @sv;
    }

    my $subform = $tx->new ( $file );

    if ( $more ) {
	if ( exists $options{'access-time'} ) {
	    my $value = $sv[8];
	    $value = &strftime ( $options{'access-time-format'}, localtime( $value ) )
		if exists $options{'access-time-format'};
	    $subform->param ( $options{'access-time'}, $value );
	}

	if ( exists $options{'modify-time'} ) {
	    my $value = $sv[9];
	    $value = &strftime ( $options{'modify-time-format'}, localtime( $value ) )
		if exists $options{'modify-time-format'};
	    $subform->param ( $options{'modify-time'}, $value );
	}

	if ( exists $options{'change-time'} ) {
	    my $value = $sv[10];
	    $value = &strftime ( $options{'change-time-format'}, localtime( $value ) )
		if exists $options{'change-time-format'};
	    $subform->param ( $options{'change-time'}, $value );
	}
    }

    $subform->output;
}

#
# Name of Release: $Name$
# $Log$
# Revision 1.5  2000/04/13 15:16:48  sridhar
# 1. Using new parse_options method with support for expressions
# 2. Passing filename to TagX->new() method to enable filename logging
# with log messages.
#
# Revision 1.4  2000/03/09 14:16:32  sridhar
# Added new option --dir-prefix, the value of which should be a
# variable resolving into a directory.
#
# Revision 1.3  2000/03/06 18:32:00  sridhar
# Replaced all ad-hoc message logging with message logging only on demand
# -- via --verbose flag specified as a tag option
#
# Revision 1.2  2000/02/08 15:37:44  sridhar
# Added support for being able to use Exporter version numbers
#
# Revision 1.1  2000/02/08 15:24:58  sridhar
# Added TagX
#
#

1;
