#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

package TagX::TAG::NoBody::Include;

use strict;
use warnings;

use Bedrock;
use Bedrock::Constants qw(:chars :booleans);
use Cwd;
use English qw(-no_match-vars);
use POSIX qw (strftime);
use TagX::Error;

use Role::Tiny::With;
with 'Bedrock::Logger';

use parent qw( Exporter TagX::TAG::NoBody);

########################################################################
sub define_options {
########################################################################
  return (
    'file=s'       => undef,
    'dir-prefix=s' => undef,
    'source'       => undef,
    'optional'     => undef,
  );
}

########################################################################
sub finalize {
########################################################################
  my ( $self, $tx, @context ) = @_;

  my %options = $self->define_options;

  my @argv = $self->parse_options( \%options, @context );

  $self->dump_options( \%options, @argv );

  my $dir_prefix = delete $options{'dir-prefix'};

  $self->get_logger->debug( Dumper( [ dir_prefix => $dir_prefix ] ) );

  if ($dir_prefix) {
    die "ABSOLUTE_PATHS is disabled\n"
      if !$tx->config->{ABSOLUTE_PATHS};

    $self->get_logger->warn('--dir-prefix is a deprecated feature, set the INCLUDE_DIR in the configuration.');
  }

  my $file = $options{'file'};

  if ( !$file && @argv ) {
    my $tag_opts = $self->parse_tag_name( $argv[0] );
    $file = $tag_opts->{name} || "@argv";
  }

  die "no file specified to include\n"
    if !$file;

  die "invalid filename\n"
    if $file =~ /^[.][.]?/xsm;

  die "invalid path for file\n"  # paths must be relative or use --dir-prefix
    if $file =~ /^\//xsm && !$options{file};

  if ( $file !~ /[.].+$/xsm ) {
    $file .= '.inc';
  }

  my @file_paths;

  # -- if a prefix exists, we assume an absolute path (might want to
  # reconsider this 'feature')
  if ($dir_prefix) {
    push @file_paths, sprintf '%s/%s', $dir_prefix, $file;
  }
  else {
    # relative to DOCUMENT_ROOT
    if ( $file =~ /^\~(.*)$/xsm ) {
      my $document_root = $ENV{DOCUMENT_ROOT} // cwd;

      $self->get_logger->debug( sprintf 'DOCUMENT_ROOT: [%s]', $document_root );

      $file =~ s/^\~/$document_root\//xsm;

      push @file_paths, $file;
    }
    elsif ( my $include_dir = $tx->getoption('INCLUDE_DIR') ) {
      push @file_paths, sprintf '%s/%s', cwd(), $file;

      my @include_paths = split /:/xsm, $include_dir;

      # do not allow invalid paths or paths that are owned by root
      @include_paths = grep { -d $_ } @include_paths;

      push @file_paths, map { sprintf '%s/%s', $_, $file } @include_paths;
    }
    else {
      push @file_paths, $file;
    }
  }

  my $include_file;
  my @status_info;

  $self->get_logger->debug(
    sub {
      sprintf "include search list:\n %s", join "\n", @file_paths;
    }
  );

  foreach my $inc_file (@file_paths) {

    if ( -d $inc_file ) {
      $self->get_logger->warn( sprintf 'skipping [%s] not a file', $inc_file );
      next;
    }

    if ( !-r $inc_file ) {
      $self->get_logger->warn( sprintf 'skipping [%s] not readable', $inc_file );
      next;
    }

    next if !-e $inc_file;

    @status_info = ( $inc_file, stat $inc_file );
    last;
  }

  if (@status_info) {
    $file = pop @status_info;

    $self->get_logger->debug(
      sub {
        "include: found file [$file]";
      }
    );

    if ( !$options{'source'} ) {

      # need to pass context object, but keep same output handle
      my $subform = $tx->new( $file, $tx->ctx );
      $subform->out_handle( $tx->out_handle );

      my @file_times = @status_info[ ( 8, 9, 10 ) ];

      foreach my $v (qw/access modify change/) {
        if ( $options{ $v . '-time' } ) {
          my $value = shift @file_times;

          if ( $options{ $v . '-time-format' } ) {
            $value = strftime( $options{ $v . '-time-format' }, localtime $value );
          }

          $subform->param( $options{ $v . '-time' }, $value );
        }
      }

      $subform->output;
    }
    else {
      my $obj = TagX::Error->new($file);

      $tx->out_handle->print( $obj->view_source );
    }
  }
  elsif ( !$options{'optional'} ) {
    die "file not found\n";
  }

  return;
}

1;

## no critic (RequirePodSections)

__END__

=pod

=head1 TAG - C<E<lt>includeE<gt>>

=head1 PURPOSE

Merge a file into the current Bedrock page at the insertion point of
the tag.

=head1 SYNTAX

  <include[:include-name] [--file=filename] [--dir-prefix=dir] [filename]>

=head1 DESCRIPTION

The C<E<lt>includeE<gt>> tag is used to include a Bedrock snippet within
another Bedrock page.  Typically this is done to make development more
modular.  Sometimes it helps to abtract a page as a series of included
components...sometimes it just makes your application more difficult to maintain.

I<All things in moderation> - Benjamin Franklin.

=head1 OPTIONS

This tag takes an optional argument which should be the filename to
include if you did not set a name using the C<--file> option.  Either
provide the filename as an argument, use the C<--file> option, or specify
C<:include-name> (but only use one of them).

 --file       = filename
 --dir-prefix = directory-name
 --optional
 --source

=over 5

=item --file

Name of the file to include.

=item --dir-prefix

Directory where file is to be found.  See the notes below regarding
how Bedrock looks for include files.

=item --optional

The C<--optional> flag will prevent Bedrock from throwing an exception
if the file is not found.  Using this option is faster than the
construct below, however use it with caution since Bedrock will not
(obviously) not let you know it couldn't find the file!

Alternately:

 <try>
  <include:foo>
 <catch>
 </try>

=item --source

This option is used to create a raw listing of the included file with
line numbers. Additionally, when used with Bedrock's default error page style
sheet (F/css/source.css>), will produce a respectable decorated listing.

It works best when placed in a C<E<lt>preE<gt>> block.

 <pre>
 <include --source "index.roc">
 </pre>

=item File Stats

=over 10

=item access-time

Name of variable to hold the access time of the file.

=item change-time

Name of variable to hold the change time of the file.

=item modify-time

Name of variable to hold the modify time of the file.

=back

=back

=head1 NOTES

=over 5

=item 1. Default path

In the absence of any modifiers that would effect the path resolution
(C<--dir-prefix> C<~>), the F<.inc> file is assumed to be in the same
directory as the file in which it was included or one of the
directories indicated by the configuration variable (C<INCLUDE_DIR>).

=item 2. C<--dir-prefix>

If the C<--dir-prefix> option is used you must also set the
configuration variable C<ABSOLUTE_PATHS> to a true value.  You are
advised to use the C<INCLUDE_DIR> configuration variable to restrict
the directories that can be used for include files. You should also
make sure that those directories have their permissions set
appropriately.


=item 3. Included files are parsed as templates

Files included with this tag are parsed as Bedrock pages.  If you want
to include a file as raw text see the C<BLM::File> plugin. Include
files are evaluated in the same lexical scope as the surrounding
scope.

=item 4. Default suffix is C<.inc>

The default suffix for "include files" is F<.inc> so you don't need to
add the suffix unless your include files have non-standard extensions.

=item 5. C<~/> = DOCUMENT_ROOT

If the filename is prefaced with a C<~>, then the file is presumed to
be found relative to the document root.

=item 6. Absolute Paths

If the filename includes a C</>, then the filename is presumed to be a
fully qualifed path. To specify an absolute filename you must set the
C<ABSOLUTE_PATHS> configuration to a true value.

=back

=head1 EXAMPLE

 <include --file="main.inc" --dir-prefix="/var/www/example-site.com/include">

 <include:test> <=> <include --file="test.inc" --dir-prefix=$config.INCLUDE_DIR>

=head1 SEE ALSO

C<E<lt>pebbleE<gt>>, L<BLM::File>

=cut
