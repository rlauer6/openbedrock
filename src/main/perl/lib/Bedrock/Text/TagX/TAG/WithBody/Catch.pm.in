#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

package TagX::TAG::WithBody::Catch;

use strict;
use warnings;

use English qw/-no_match_vars/;

use Bedrock qw(:booleans);
use Bedrock::RegExp;
use Bedrock::Constants qw{:chars :booleans};

use parent qw (Exporter TagX::TAG::WithBody);

use Role::Tiny::With;
with 'Bedrock::Logger';

########################################################################
sub parent_try {
########################################################################
  my ( $self, @args ) = @_;

  if ( @args == 1 ) {
    $self->{'parent_try'} = shift @args;
  }

  return $self->{'parent_try'};
}

########################################################################
sub end_try {
########################################################################
  my ($self) = @_;

  return $self->parent;
}

########################################################################
sub closure {
########################################################################
  return '/try';
}

########################################################################
sub catch {
########################################################################
  my ( $self, @args ) = @_;

  if ( @args == 1 ) {
    $self->{'catch'} = shift @args;

    $self->{'catch'}->parent_try(
      exists $self->{'parent_try'} ? $self->{'parent_try'} : $self );

    $self->{'catch'}->parent( $self->parent );
  }

  return $self->{'catch'};
}

########################################################################
sub finalize {
########################################################################
  my ( $self, $tx, @context ) = @_;

  my $errstr = $EVAL_ERROR;

  my %options;

  my @argv = $self->parse_options( \%options, @context );

  $self->dump_options( \%options, @argv );

  return $self->SUPER::finalize( $tx, @context )
    if !@argv;

  my $rhs = "@argv";

  my $re = eval { regexp_compile($rhs) };

  die "invalid regular expression\n$EVAL_ERROR"
    if $EVAL_ERROR;

  $EVAL_ERROR = $errstr;  ## no critic (RequireLocalizedPunctuationVars)

  my $matched = is_regexp($re) ? $errstr =~ $re : $errstr =~ /$re/ism;  ## no critic (RequireExtendedFormatting)

  if ($matched) {
    my %capture_groups = %{ regexp_capture_group() };

    for ( keys %capture_groups ) {
      $context[0]->{$_} = $capture_groups{$_};
    }

    return $self->SUPER::finalize( $tx, @context );
  }

  return $self->catch->finalize( $tx, @context )
    if $self->catch;

  die;

  return;
}

1;

## no critic (RequirePodSections)

__END__

=pod

=head1 TAG - C<E<lt>catchE<gt>>

=head1 PURPOSE

Begins the C<catch> portion of exception handling in C<try/catch> block.

=head1 SYNTAX

  <catch [regexp]>

=head1 DESCRIPTION

C<catch> is part of the C<try/catch> mechanism for trapping
application errors.  One or more C<catch> blocks may be defined by
your application.  If the regular expression argument form of C<catch>
is used, then the exception is matched against the regular expression
to determine if the subsequent C<catch> block will handle the exception.

If no regular expression is present, then that C<catch> block will be
the default exception path.

  <try>
  ...
  <catch "error 1">
  ...
  <catch "error 2">
  ...
  <catch>
  ...
  </try>

=over 5

=item regexp

The C<regexp> argument can be a string that simply represents a value
that should be compared to the exception or it can be a true regular
expression.

Regular expressions are identified by Bedrock as strings between two
'/' characters (I</regexp/>) or more formally as a quoted regexp of
the form I<qr/regexp/flags>.

The catch blocks are examined in order. If no regular expression is
matched the default catch block (one that has no regular expression
argument) will be used.

String that do not represent regular expresssions are evaluated in a
case-insensitive manner.

 <catch "qr/error 1/i">

...is equivalent to:

 <catch "error 1">

You can also use capture groups in more complex regular expressions:

 <try>
   <raise "bad apple">
 <catch "qr/^bad\\s*(?'what'.*?)$/i">
   You have a bad something error: bad <var $what>
 </try>

I<Note you must escape backslashes in Bedrock strings!>

=back

=head1 OPTIONS

None

=head1 EXAMPLE

  <try>
    <pebble:mypeb "arg1">
  <catch "connect">
    Could not connect!
  <catch "parameter">
    Illegal parameter!
  <catch>
    Unknown error!
  </try>

=head1 SEE ALSO

C<E<lt>tryE<gt>>, C<E<lt>raiseE<gt>>, 

=cut
