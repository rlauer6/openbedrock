#!@PERL@ -w

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

package TagX::TAG::WithBody::Catch;

use strict;
use warnings;

BEGIN {
  use Exporter ();
  use vars qw ($VERSION @ISA);
  @ISA = qw (Exporter);
}

use TagX::TAG::WithBody;

use English qw/-no_match_vars/;

use Log::Log4perl qw/:easy/;

@ISA = qw (TagX::TAG::WithBody);

sub parent_try {
  my $self = shift;

  $self->{'parent_try'} = shift
    if @_ == 1;
  
  $self->{'parent_try'};
}

sub end_try {
  my $self = shift;
  
  $self->parent;
}

sub closure {
  '/try';
}

sub catch {
  my $self = shift;
  
  if ( @_ == 1 ) {
    $self->{'catch'} = shift;

    $self->{'catch'}->parent_try(
      exists $self->{'parent_try'} ? $self->{'parent_try'} : $self );

    $self->{'catch'}->parent( $self->parent );
  } ## end if ( @_ == 1 )

  $self->{'catch'};
}

sub finalize {
  my ( $self, $tx, @context ) = @_;
  
  my $errstr = $EVAL_ERROR;
  
  my %options;
  my @argv = $self->parse_options( \%options, @context );

  $self->dump_options(\%options, @argv);

  if ( @argv > 0 ) {
    my $rhs = "@argv";
    my $re = ($rhs =~ /^qr/) ? eval $rhs : eval "qr/$rhs/i";
    my $test = ($errstr =~ $re);
    
    if ( $^V ge 'v5.10' && $test) {
      my %capture_group = %+;
      map { $context[0]->{$_} = $capture_group{$_};} keys %capture_group;
    }
    
    # note that the evals above will reset $@ so we reset it so we can
    # continue with other <catch> blocks
    $@ = $errstr;

    if ( $test ) {
      DEBUG sub {
        "Caught exception [$@]\n"
          . " using regexp [$rhs]";
      };
      
      $self->SUPER::finalize( $tx, @context );
      
      DEBUG "Done handling exception";
    }
    elsif ( $self->catch ) {
      DEBUG sub {
        "Trying next exception handler for $@";
      };
      
      $self->catch->finalize( $tx, @context );
    }
    else {
      DEBUG sub {
        "No exception handler for ($@)"
      };
      
      die;
    }
  }
  else {
    # A catch-all exception handler
    DEBUG "<catch> exception handler";

    $self->SUPER::finalize( $tx, @context );
  }
}

=pod

=head1 TAG - C<E<lt>catchE<gt>>

=head1 PURPOSE

Begins the "catch" portion or exception handling portion of a try/catch block.

=head1 SYNTAX

  <catch [regexp]>


=head1 DESCRIPTION

C<catch> is part of the C<try/catch> mechanism for trapping
application errors.  One or more C<catch> blocks may be defined by
your application.  If the regular expression argument form of C<catch>
is used, then the exception is matched against the regular expression
to determine if the subsequent C<catch> block will handle the exception.

If no regular expression is present, then that C<catch> block will be
the default exception path.

  <try>
  ...
  <catch "error 1">
  ...
  <catch "error 2">
  ...
  <catch>
  ...
  </try>

=over 5

=item regexp

C<regexp> can be simple string that represents a regular expression that will be
matched against the current exception to determine if this catch block
should be evaluated.  The catch blocks are examined in order and if no
regular expression is matched the default catch block will be used.

Simple regular expresssions are evaluated in a case-insensitive manner.

You can also use quoted regular expressions, the same kind used by Perl.

 <catch "qr/error 1/i">

...is equivalent to:

 <catch "error 1"

You can also use capture groups in more complex regular expressions:

 <try>
   <raise "bad apple">
 <catch "qr/^bad\\s*(?'what'.*?)$/i">
   You have a bad something error: bad <var $what>
 </try>

I<Note you must escape backslashes in Bedrock strings!>

=back

=head1 OPTIONS

None

=head1 EXAMPLE

  <try>
    <pebble:mypeb "arg1">
  <catch "connect">
    Could not connect!
  <catch "parameter">
    Illegal parameter!
  <catch>
    Unknown error!
  </try>

=head1 SEE ALSO

C<E<lt>tryE<gt>>, C<E<lt>raiseE<gt>>, 

=cut

1;
