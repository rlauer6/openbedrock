#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2026, TBC Development Group, LLC.
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

package TagX::TAG::NoBody::Open;

use strict;
use warnings;

use Bedrock qw(slurp_file to_boolean choose);
use Bedrock::Constants qw(:chars);
use English qw(-no_match_vars);
use IO::File;
use Symbol;
use Text::CSV_XS;
use Scalar::Util qw(reftype);

use parent qw(Exporter TagX::TAG::NoBody);

use Role::Tiny::With;
with 'Bedrock::Logger';

########################################################################
sub define_options {
########################################################################
  return (
    'define-var=s' => $UNDERSCORE,
    'mode=s'       => undef,
    'file=s'       => undef,
    'filter=s'     => undef,
    'slurp'        => undef,
  );

}

########################################################################
sub finalize {
########################################################################
  my ( $self, $tx, @context ) = @_;

  my %options = $self->define_options;

  my @argv = $self->parse_options( \%options, @context );

  $self->dump_options( \%options, @argv );

  my $name     = $options{'define-var'} || $UNDERSCORE;
  my $tag_opts = $self->parse_tag_name( $argv[0] );

  if ( $tag_opts->{name} ) {
    shift @argv;
    $name = $tag_opts->{name};
  }

  die "Specify the file to open using `--file' option or as argument, not both.\n"
    if exists $options{'file'} && @argv;

  my $file = $options{file} ? $options{file} : shift @argv;

  die "File not specified.\n"
    if !length $file;

  my $found_file = $tx->find_file( $file, undef, $DOT );

  die "No file handle specified. Use --define-var to assign a file handle name.\n"
    if !length $name;

  my $mode = $options{'mode'} || 'r';

  # --- SECURITY / CONFIGURATION CHECK ---

  # 1. Check for Write/Append attempts
  if ( $mode =~ /[>wa\+]/xsm ) {
    my $config = $tx->config;

    # Support both scalar config or hashref lookup
    my $allow_write = reftype($config) eq 'HASH' ? $config->{ALLOW_FILE_WRITE} : undef;

    # For CLI scripts, this can be passed via Env or Config
    $allow_write //= $ENV{BEDROCK_ALLOW_FILE_WRITE};

    if ( !to_boolean($allow_write) ) {
      die "Security Violation: Write mode '$mode' is disabled. "
        . "Set <scalar name='ALLOW_FILE_WRITE'>yes</scalar> in configuration to enable.\n";
    }

    $found_file = $file;
  }

  # 2. Pipe Protection (Paranoia check)
  # Even though IO::File->new($file, $mode) uses safe 3-arg open semantics,
  # explicitly creating pipes via the filename (e.g. "cmd |") is often a mistake
  # or exploit attempt in a web context.
  if ( $file =~ /^\s*\||\|\s*$/xsm ) {
    my $config      = $tx->config;
    my $allow_pipes = ref($config) eq 'HASH' ? $config->{ALLOW_FILE_PIPES} : undef;

    if ( !to_boolean($allow_pipes) ) {
      die "Security Violation: Pipe open is disabled by default. Set ALLOW_FILE_PIPES to enable.\n";
    }
  }

  # --------------------------------------

  # slurp file?
  if ( $options{slurp} ) {
    $tx->{VARS}->{$name} = slurp_file $file;
    return;
  }

  if ( exists $options{'filter'} ) {
    die "--filter requires a filter name\n"
      if !$options{filter};

    my $filter = sprintf 'BLM/Filter/%s.pm', $options{filter};
    my $class  = sprintf 'BLM::Filter::%s',  $options{filter};

    eval { require $filter; };

    die sprintf "unknown or malformed filter: %s\n", $options{filter}
      if $EVAL_ERROR;

    my $fh = gensym;

    tie *{$fh}, $class, $file, $mode, @argv; ## no critic (ProhibitTies)

    bless $fh, $class;

    $tx->{VARS}->{$name} = $fh;

    return;
  }

  my $fh = eval { return IO::File->new( $file, $mode ); };

  die sprintf "Could not open file %s with mode = %s\n%s", $file, $mode, $OS_ERROR
    if !$fh || $EVAL_ERROR;

  $tx->{VARS}->{$name} = $fh;

  return;
}

1;

## no critic (RequirePodSections)

__END__

=pod

=head1 TAG - C<E<lt>openE<gt>>

=head1 PURPOSE

Open a file for reading or writing.  Similar to C<E<lt>plugin:FileIOE<gt>>,
but the object created is an C<IO::File> handle.

=head1 SYNTAX

 <open[:name] [options] filename >

=head1 DESCRIPTION

You can use the C<E<lt>openE<gt>> tag to return a handle to a file
that can be used with other tags ( C<E<lt>sinkE<gt>>) or plugins that
accept handles as arguments.

I<Note: Although the name of the object can be defaulted to C<$_> by
omitting the C<--define-var> option, this may be less than useful if
you use the C<open> tag before entering a C<while> loop, something
typically done when reading a file as shown in the example.>

I<The C<E<lt>whileE<gt>> tag will reset C<$_> to the value of the term
in quotes when it is evaluated each time through the loop. Caveat
 Emptor>.

=head1 OPTIONS

 --define-var       = "var-name" (default="_")
 --mode             = "mode"
 --file             = "filename"
 --slurp

=over 5

=item --define-var

Name of the object created.

=item --filter

Experimental: Name of a filter that can read files. Filters available are:

 cvs   => read/write a .csv file
 xls   => read each row of a spreadsheet
 crypt => read/write an encrypted file

=item --mode

The open mode of the file.  Can be one of:

 "r"  -> read
 "w"  -> write/clobber
 "a"  -> append
 "r+" -> reading/writing

=head3 Notes

=over 4

=item 1. File writing must be enabled using the configuration variable
C<ALLOW_FILE_WRITE> or the environment variable C<BEDROCK_ALLOW_FILE_WRITE>.

=item 2. File and directory permission may restrict the reading and writing of files.

=item 3. Set ABSOUTE_PATHS to a true value if you want to allow
writing and reading from or to absolute paths.

=back

=item --file

Fully qualified path of the file to open.

=item --slurp

The file is slurped into a variable.  The file is subsequently closed.

 <open:foo --slurp "foo.txt">

If you want to include a file at a given point in your template, use
C<E<lt>includeE<gt>>.

If you want to read a file in order to deserialize the contents, use
C<E<lt>null --fileE<gt>>.

=back

=head1 METHODS

See perldoc C<IO::Handle> for an explanation of the possible methods
that you can invoke on an open file handle.

=head1 EXAMPLE

 <open:fh --mode="r" $env.BEDROCK_CONFIG_PATH.format(q{%s/tagx.xml})>
 <pre>
 <while $fh><var --htmlencode $_></while>
 </pre>

 <open:fh --mode="w" "foo.txt">
 <null $fh.printf("Hello World!\n")>
 <null $fh.close()>

 <open:fh --mode="w" "foo.txt">
 <sink $fh>
 Hello World!
 </sink>

=head1 SEE ALSO

L<IO::Handle>

=cut
