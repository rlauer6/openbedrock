#!@PERL@ -w

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

package TagX::TAG::NoBody::Open;

use strict;
use warnings;

BEGIN {
  use Exporter ();
  use vars qw ($VERSION @ISA);
  @ISA = qw (Exporter);
}

use TagX::TAG::NoBody;

use English qw/-no_match_vars/;
use IO::File;
use Log::Log4perl qw/:easy/;
use Symbol;
use Text::CSV_XS;

@ISA = qw (TagX::TAG::NoBody);

sub define_options {
  return (
    'define-var=s' => "_",
    'mode=s'       => undef,
    'file=s'       => undef,
    'filter=s'     => undef,
    'slurp'        => undef
  );

} ## end sub define_options

sub finalize {
  my ( $self, $tx, @context ) = @_;

  my %options = $self->define_options;
  
  my @argv = $self->parse_options( \%options, @context );

  $self->dump_options( \%options, @argv );

  my $name = $options{'define-var'} || '_';
  my $tag_opts = $self->parse_tag_name( $argv[0] );

  if ( $tag_opts->{name} ) {
    shift @argv;
    $name = $tag_opts->{name};
  }

  die "Specify the file to open using `--file' option or as argument, not both.\n"
    if exists $options{'file'} and @argv;

  my $file;

  if ( exists $options{file} && $options{file} ) {
    $file = $options{'file'};
  }
  else {
    $file = shift @argv;
  }

  die "File not specified.\n" unless length($file);

  if  ( ! length $name ) {
    die "No file handle specified. Use --define-var to assign a file handle name.";
  }

  my $mode = $options{'mode'} || "r";

  my $fh;

  if ( exists $options{'filter'} ) {
    die "--filter requires a filter name\n"
      if !$options{filter};

    my $filter = "BLM/Filter/" . $options{filter} . '.pm';

    eval {
      require $filter;
    };
    
    die "unknown or malformed filter: ". $options{filter} . "\n"
      if $EVAL_ERROR;

    $fh = gensym;
    tie *{$fh},$filter,  $file, $mode, @argv;
    bless $fh, $filter;
  }
  else {
    $fh = IO::File->new( $file, $mode );
  }

  die "Could not open file $file with mode = $mode" 
    if !$fh;

  # slurp file?
  if ( $options{slurp} ) {
    {
      local $/;
      
      $tx->{'VARS'}->{$name} = <$fh>;
    }
    
    $fh->close;
  }
  else {
    $tx->{'VARS'}->{$name} = $fh;
  }
}

=pod

=head1 TAG - C<E<lt>openE<gt>>

=head1 PURPOSE

Open a file for reading or writing.  Similar to C<E<lt>plugin:FileIOE<gt>>,
but the object created is an C<IO::File> handle.

=head1 SYNTAX

 <open[:name] [options] filename >

=head1 DESCRIPTION

You can use the C<E<lt>openE<gt>> tag to return a handle to a file
that can be used with other tags ( C<E<lt>sinkE<gt>>) or plugins that
accept handles as arguments.

I<Note: Although the name of the object can be defaulted to C<$_> by
omitting the C<--define-var> option, this may be less than useful if
you use the C<open> tag before entering a C<while> loop, something
typically done when reading a file as shown in the example.>

I<The C<E<lt>whileE<gt>> tag will reset C<$_> to the value of the term
in quotes when it is evaluated each time through the loop. Caveat
Emptor>.

=head1 OPTIONS

 --define-var       = "var-name" (default="_")
 --mode             = "mode"
 --file             = "filename"
 --slurp

=over 5

=item --define-var

Name of the object created.

=item --mode

The open mode of the file.  Can be one of:

     "r" -> read
     "w" -> write/clobber
     "a" -> append
     "r+" -> reading/writing

=item --file

Fully qualified path of the file to open.

=item --slurp

The file is slurped into a variable.  The file is subsequently closed.

 <open:foo --slurp "foo.txt">

If you want to include a file at a given point in your template, use
C<E<lt>includeE<gt>>.

If you want to read a file in order to deserialize the contents, use
C<E<lt>null --fileE<gt>>.

=back

=head1 METHODS

See perldoc C<IO::Handle> for an explanation of the possible methods
that you can invoke on an open file handle.

=head1 EXAMPLE

 <open:fh --mode="r" $env.BEDROCK_CONFIG_PATH.format(q{%s/tagx.xml})>
 <pre>
 <while $fh><var --htmlencode $_></while>
 </pre>

 <open:fh --mode="w" "foo.txt">
 <null $fh.printf("Hello World!\n")>
 <null $fh.close()>

 <open:fh --mode="w" "foo.txt">
 <sink $fh>
 Hello World!
 </sink>

=head1 SEE OTHER

C<IO::Handle>

=cut

1;
