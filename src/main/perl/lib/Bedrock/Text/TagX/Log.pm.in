#!@PERL@ -w

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

package TagX::Log;

use strict;
use warnings;

use Bedrock::Log::Spooler;
use Date::Format;
use Data::Dumper;

BEGIN {
  use Exporter ();
  use vars qw (@ISA @EXPORT @EXPORT_OK);
  @ISA = qw (Exporter);
}

@EXPORT = qw (log_message);
@EXPORT_OK = qw (start_logger);

use Scalar::Util qw/reftype blessed/;

my %options = (
	       'LOG_MAXSIZE'  => 100000,
	       'LOG_WARNINGS' => 'off',
	       'LOG_ERRORS'   => 'off',
	       'LOG_FORMAT'   => '%m/%d/%Y %H:%M:%S'
	      );

sub start_logger {
  my %args = @_;
  
  return unless $args{LOG_FILE} || $args{LOG_SPOOLER};
    
  # override default options
  @options{keys %args} = values %args;
  
  my ( $logfile, $maxsize ) = @options{qw(LOG_FILE LOG_MAXSIZE)};
  
  if ( $logfile ) {
    if ( -e $logfile ) {
      # truncate log unless LOG_MAXSIZE < 0
      if ( $maxsize == 0 || ( $maxsize > 0 && ( stat($logfile) )[7] > $maxsize ) ) {
	open( FILE, ">$logfile" );
	close FILE;
      }
    }
    
    # Redirect STDERR too to append to logfile
    unless ( open( STDERR, ">> $logfile" ) ) {
      warn __PACKAGE__,
	":start_logger(): Unable to append to log file $logfile: $!\n";
      return;
    }
  
    my $fd = IO::Handle->new_from_fd( 'STDERR', 'a' );
    $fd->autoflush(1);
    $fd->close;
  }
  
  # log warnings/errors to page log, unless in eval {}
  $SIG{__DIE__} = $SIG{__WARN__} = sub {
    # don't log if we are in an eval {}
    return unless  defined $^S and $^S == 0;
    
    unshift @_, undef;
    goto &log_message
  };
  
  &log_message( undef, "started logger");
}


sub log_message {
  my $obj = shift;
  
  return unless exists $options{LOG_FILE} || $options{LOG_SPOOLER};

  # Make caller
  my ( $pack, $filename, $line ) = caller;

  # Input location tracking
  my $loc = '';
  if ( blessed($obj) && $obj->can('start_location') ) {
    $loc = "[" . $obj->start_location . "]\n[$obj]\n";
  }

  my $page = eval {
    if ( $ENV{BEDROCK_URI} ) {
      $ENV{BEDROCK_URI};
    }
    elsif ( $ENV{BEDROCK_FILE} ) {
      $ENV{BEDROCK_FILE};
    }
    else {
      'unknown';
    }
  };

  # Allow either, or neither...
  if ( $options{LOG_FILE} ) {
    my $logfile = $options{LOG_FILE};
    # default: %m/%d/%Y %H:%M:%S
    my $time = time2str($options{LOG_FORMAT}, time);

    open( LOG, ">> $logfile" ) or return;   
    print LOG "$time \[$$\]\t[$page]\t$filename($line)\n$loc@_\n";
    close LOG
  }

  if ( $options{LOG_SPOOLER} ) {
    my %spooler_options = (channel => "bedrock/log$page");

    # note LOG_SPOOLER can override channel
    #
    # <object name="HTML_LOGSPOOLER">
    #   <scalar name="channel">mychannel</scalar>
    # </object>
    #
    if (ref($options{LOG_SPOOLER}) && reftype($options{LOG_SPOOLER}) eq 'HASH' ) {
      @spooler_options{keys %{$options{LOG_SPOOLER}}} = values %{$options{LOG_SPOOLER}};
    }
    
    eval {
      my $spooler = Bedrock::Log::Spooler->instance(%spooler_options);
      $spooler->publish(\@_, page => $page, line => $line, loc => $loc, filename => $filename);
    };

    if ( $@ ) {
      # try to print the log message to STDERR
      print STDERR "error spooling log message: $@\n";
      print STDERR sprintf("[%s] %s - %s\n", time2str("%c", time), $page, join('', @_));
    }
  }
}

1;
