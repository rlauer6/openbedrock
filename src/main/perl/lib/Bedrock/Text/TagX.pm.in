package Text::TagX;

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2024, TBC Development Group, LLC.
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

use strict;
use warnings;

use Bedrock qw(slurp_file :booleans choose);
use Bedrock::Constants qw(:booleans :chars :regexp);
use Carp;
use Cwd;
use English qw(-no_match_vars);
use File::Basename;
use HTML::Entities;
use IO::Scalar;
use List::Util qw(pairs);
use Module::Load;
use Scalar::Util qw(reftype blessed);
use Storable qw(freeze thaw);
use TagX::Error;
use TagX::Output;
use TagX::Symtab;
use TagX::TAG::WithBody::Container;

use Role::Tiny::With;
with 'Bedrock::Logger';

our $VERSION = '@PACKAGE_VERSION@'; ## no critic

our %TAGS_ENABLED = (
  q{#} => 'TagX::TAG::NoBody::Comment',
  q{?} => 'TagX::TAG::NoBody::NULL',
  else => [
    'TagX::TAG::WithBody::If::Else',
    sub {
      my ( $class, $obj, $raw ) = @_;

      die "Tag is not valid here.\n"
        if ref($obj) !~ /::If|Unless/xsm;

      return ( $obj->else_obj( $class->new ), $raw // 0 );
    }
  ],
  elsif  => sub { return 'else' },
  elseif => sub { return 'else' },
  array  => 'TagX::TAG::NoBody::Array',
  cache  => 'TagX::TAG::WithBody::Cache',
  case   => 'TagX::TAG::NoBody::Case',
  catch  => [
    'TagX::TAG::WithBody::Catch',
    sub {
      my ( $class, $obj, $raw ) = @_;

      die "Tag is not valid here.\n"
        if ref($obj) !~ /TagX::TAG::WithBody::Try|Catch/xsm;

      return ( $obj->catch( $class->new ), $raw // 0 );
    }
  ],
  exec        => sub { return 'pebble' },
  flush       => 'TagX::TAG::NoBody::Flush',
  foreach     => 'TagX::TAG::WithBody::Loop::Foreach',
  hash        => 'TagX::TAG::NoBody::Hash',
  if          => 'TagX::TAG::WithBody::If',
  iif         => 'TagX::TAG::NoBody::Iif',
  include     => 'TagX::TAG::NoBody::Include',
  noexec      => [ 'TagX::TAG::WithBody::NoExec', sub { return spawn( $_[0], $_[1], 1 ); } ],
  null        => 'TagX::TAG::NoBody::NULL',
  open        => 'TagX::TAG::NoBody::Open',
  pebble      => 'TagX::TAG::NoBody::Pebble',
  pebbledef   => [ 'TagX::TAG::WithBody::PebbleDef', sub { return spawn( $_[0], $_[1], 1 ); } ],
  plugin      => 'TagX::TAG::NoBody::Plugin',
  raise       => 'TagX::TAG::NoBody::Raise',
  recordset   => 'TagX::TAG::NoBody::RecordSet',
  sink        => 'TagX::TAG::WithBody::Sink',
  snippet     => 'TagX::TAG::WithBody::Snippet',
  sql         => 'TagX::TAG::NoBody::SQL',
  sqlcommit   => 'TagX::TAG::NoBody::SQLCommit',
  sqlconnect  => 'TagX::TAG::NoBody::SQLConnect',
  sqlrollback => 'TagX::TAG::NoBody::SQLRollback',
  sqlselect   => 'TagX::TAG::WithBody::Loop::SQLSelect',
  sqltable    => 'TagX::TAG::NoBody::SQLTable',
  trace       => 'TagX::TAG::NoBody::Trace',
  try         => 'TagX::TAG::WithBody::Try',
  unless      => 'TagX::TAG::WithBody::Unless',
  var         => 'TagX::TAG::NoBody::Var',
  while       => 'TagX::TAG::WithBody::Loop::While',
);

########################################################################
sub spawn {
########################################################################
  my ( $class, $obj, $raw ) = @_;

  return ( $obj->new_child( $class->new ), $raw // 0 );
}

# NOTE: logging during parsing is unaffected by --verbose option
# if you want to see some debugging output you need to set the log level
# of the TagX category directly in the Log::Log4perl configuration file
########################################################################
sub parse {
########################################################################
  my ($self) = @_;

  my $optstring = $EMPTY;

  my $text = $EMPTY;

  my $line = 0;

  my $file = $self->{'error'}->file;

  local $_ = undef;

  my $raw = $FALSE;

  my $obj = TagX::TAG::WithBody::Container->new($self);

  my $newscan = $TRUE;

  my $input = $self->{input};

  my $opentag_expr = $self->{OPENTAG_EXPR};

  ## no critic
  INPUT:
  while (<$input>) {

    # bedrock shell shebang
    next if $line == 0 && /^[#]!/xsm;

    $self->{error}->line( ++$line );
    MAIN: {

      #
      # Check whether we are into a new scan or collecting options
      # for a matched tag
      #
      if ( !$newscan ) {

        # Collecting options for a matched tag -- Look for tag end which is
        # an un-escaped '>'
        if (s/$OPTIONS_EXPR//xsmo) {

          # Found an un-escaped '>', which terminates the current tag and its options
          # $optstring contains the options collected so far
          $optstring .= $1;

          # chomp if tag ends in -
          if ( $3 && $3 =~ /\-/xsm ) {
            s/\n//xsm;
          }

          # Remove escapes from the option string
          $optstring =~ s/\\([^\\])/$1/xsmg;
          $optstring =~ s/\\\\/\\/xsmg;

          # And set the current object options
          $obj = $obj->options($optstring);

          $self->get_logger->trace( 'options string: ', $optstring );

          if ( $optstring =~ /--noexec/xsm ) {
            $raw = $TRUE;
          }

          # Start a new scan after this
          $newscan = $TRUE;

          # Handle possible left over stuff after the '>'
          redo MAIN;
        }
        else {

          # No terminating '>' in entire line -- start by reading input again
          $optstring .= $_;
          next INPUT;
        }
      }

      # New scan from here -- scan afresh for tags.
      # if no '<' found in line, skip line
      if ( !s/^(.*?)<//xsmoi ) {

        if (length) {
          $text .= $_;
        }

        next INPUT;
      }

      if ( length $1 ) {
        $text .= $1;
      }

      # We did find a '<' -- could be we have a possible supported tag next
      # Check if we have a supported tag
      my $tag = $_;

      if ( !$raw && s/$opentag_expr//xsmi ) {

        # Yes we do
        # The text before the '<' needs to be processed.
        if ( length $text ) {
          $obj->text($text);
          $text = $EMPTY;
        }

        my $tag_name = $1;

        my $tag_def = $TAGS_ENABLED{$tag_name}
          or die "unsupport tag ($tag_name)\n";

        my ( $class, $sub ) = choose {

          # 1. RESOLVE ALIAS
          # If it is a CODE ref, run it to get the real key (String or Array).
          if ( ref($tag_def) eq 'CODE' ) {
            my $target_key = $tag_def->();
            $tag_def = $TAGS_ENABLED{$target_key}
              or die "Alias target '$target_key' not found";

            # Flatten the alias so we don't run this code again
            $TAGS_ENABLED{$tag_name} = $tag_def;
          }

          # 2. INFLATE STRING
          # Handles standard tags OR aliases that resolved to strings (like exec -> pebble)
          if ( !ref $tag_def ) {
            $tag_def = [ $tag_def, \&spawn, 0 ];
            $TAGS_ENABLED{$tag_name} = $tag_def;
          }

          # 3. JIT LOAD
          # Check 'Loaded' flag at index 2. If 0/undef, load and set to 1.
          if ( !$tag_def->[2] ) {
            load $tag_def->[0];
            $tag_def->[2] = 1;
          }

          return @{$tag_def};
        };

        ( $obj, $raw ) = $sub->( $class, $obj, $raw );

        $obj->name($tag_name);

        $obj->start_location( $file, $line );

        # After creating object of suitable type depending on the tag, check
        # if the tag ends on the same line. Tag ending is an un-escaped '>'.
        if (s/$OPTIONS_EXPR//oixsm) {

          my $options = $1;
          # chomp if tag ends in -
          if ( $3 && $3 =~ /\-/xsm ) {
            s/\n//xsm;
          }

          $self->get_logger->trace( 'options string: ', $options );

          if ( $options =~ /--noexec/xsm ) {
            $raw = $TRUE;
          }

          if ( ref($obj) eq 'TagX::TAG::NoBody::Comment' && /$/xsm ) {
            $_ = $EMPTY;
          }

          # The tag definition ended on same line -- all options specified on
          # one line

          $options =~ s/\/$//xsm;  # allow /> for nobody tags

          # Unescape the options
          $options =~ s/\\([^\\])/$1/xsmg;
          $options =~ s/\\\\/\\/xsmg;

          # And set the current object options
          $obj = $obj->options($options);

          # And rescan the string for more tags -- Note that $newscan is unchanged
          # from its value of 1, so we don't need to set it here
          redo MAIN;
        }
        else {

          # Tag definition did not end on one line -- Further input is consumed for
          # looking for the tag ending
          $newscan = 0;

          # Start collection of the options string
          $optstring = $_;
        }

      }
      elsif (s/$CLOSETAG_EXPR//oixsm) {
        my $end = lc $1;

        if ($2) {
          s/\n//xsm;
        }

        if ($raw) {

          if ( $end =~ /\/(pebbledef|sink|noexec)/xsm ) {
            $obj->text($text);
            $obj  = $obj->end_child;
            $raw  = $FALSE;
            $text = $EMPTY;
          }
          else {
            $text .= '<' . $1 . '>';
          }
          redo MAIN;
        }
        else {
          die "Illegal tag-end encountered.\n"
            if !$obj->parent;

          if ( $end eq '/unless' ) {
            $end = '/if';
          }

          if ( $obj->closure ne $end ) {
            $self->improper_end( $obj, $end );
          }

          # A tag has ended. The text before the '<' needs to be processed.
          if ( length $text ) {
            $obj->text($text);
            $text = $EMPTY;
          }

          # Check whether we have hit a tag-body-ending tag
          # and if so, end the current object
          $obj = $obj->end_child;

          # And rescan string for more tags
          redo MAIN;
        }
      }
      else {

        # Unsupported tag hit -- output the '<' since we don't consume it
        $text .= q{<};

        # And rescan for more tags
        redo MAIN;
      }
    }
  }

  if ( $obj->parent ) {
    my ( $file, $line ) = $obj->start_location;
    die "Tag on line $line not closed properly.\n";
  }

  if ( length $text ) {
    $obj->text($text);
    $text = $EMPTY;
  }

  return $obj->finalize;
}

########################################################################
sub improper_end {
########################################################################
  my ( $self, $obj, $endexpr ) = @_;

  my $iObj = $obj;

  while ( $iObj->parent ) {
    my $closure = $iObj->closure;

    if ( $endexpr eq $closure ) {
      my ( $file, $line ) = $obj->start_location;
      die "Tag on line $line should be closed here.\n";
    }
    $iObj = $iObj->parent;
  }

  die "Illegal tag-end encountered.\n";
}

########################################################################
sub output {
########################################################################
  my $self = shift;

  eval { $self->parse };

  my $err = $EVAL_ERROR;

  if ( $self->{'__input_opened'} ) {
    $self->{'__input_opened'}->close;
  }

  if ($err) {
    chomp $err;
    $self->error->mesg($err);

    die "$err\n";
  }
  else {
    return $self->error->endLevel;
  }

  return;
}

########################################################################
sub show_error {
########################################################################
  my ( $self, $errfile ) = @_;

  my $fh;

  open $fh, '<', $errfile or ## no critic (RequireBriefOpen)
    carp "Unable to open $errfile: $OS_ERROR";

  my $error = $self->{error};

  # handle pebble errors
  if ( is_array($error) ) {
    foreach ( @{$error} ) {
      if ( ref $_->{file} ) {
        $_->{file} = $self->pebble( $_->{file} );
      }
    }
  }

  if ($fh) {
    eval {
      $self->{input} = $fh;
      $self->{error} = TagX::Error->new($errfile);
      $self->param( ERROR => $error );
      $self->output;

      close $fh;
    };

    if ($EVAL_ERROR) {
      carp "Error in error handler: $EVAL_ERROR";
    }
  }
  else {
    my $err = HTML::Entities::encode_entities( $self->{error}->mesg );

    my $html_error_message = <<"EOT";
<!DOCTYPE HTML>
<html>
  <head>
    <title>Bedrock Error</title>
  </head>

  <body>
  The following Bedrock error has been encountered:

  <pre style="color:brown;">
    $err
  </pre>

  <hr>

  <div style="color:red;font-style:italic;">

     Something's also wrong with your Bedrock configuration.  You've
     specfied an <b>ERROR_LOCATION</b> or
     <b>BEDROCK_ERROR_LOCATION</b> ($errfile) but it appears the file
     does not exist.  Please check your <b><i>tagx.xml</i></b>.

  </div>

  <pre>
    <b>$errfile</b> is empty or does not exist.
  </pre>

  </body>
</html>
EOT

    my $fh = IO::Scalar->new( \$html_error_message );

    $self->{input} = $fh;
    $self->output;

    $fh->close;
  }

  return;
}

########################################################################
sub error {
########################################################################
  my ($self) = @_;

  return $self->{error};
}

########################################################################
sub var_exists {
########################################################################
  my ( $self, $name ) = @_;

  return
    if !$name || $name eq '_';

  my $var_exists = exists $self->{VARS}->{$name};

  return $FALSE
    if !$var_exists;

  $self->get_logger->warn(
    sub {
      sprintf '[%s] conflicts with existing variable.', $name;
    }
  );

  return $TRUE;
}

########################################################################
# I don't think this method will ever be called?
# It is presumably called from param() if the key to be set has a .
# i.e. it is an array, but the function doesn't even set anything!
#
#  ?? param('foo.[0]', 1) ??
#
########################################################################
sub define_name {
########################################################################
  my ( $self, $name, $value ) = @_;

  my @components = split /[.]/oixsm, $name || $EMPTY;

  return
    if !@components;

  $self->get_logger->debug(
    sub {
      return sprintf 'components are (%s):', join $SPACE, @components;
    }
  );

  my $found = $self->{'VARS'};

  for (@components) {
    if (/^\Q[\E(\d+)\Q]\E/xsm) {

      # array element
      if ( is_array($found) ) {
        $found = $found->[$1];
        next;
      }
      else {
        $self->get_logger->error(
          sub {
            return sprintf 'Cannot define [%s] component in %s: %s is not an array', $_, $name, $found;
          }
        );

        return;
      }
    }

    if (/^(\w+)/xsm) {

      # hash element
      my $key = $1;

      if ( is_hash($found) ) {
        $found = $found->{$key};
      }
      else {

        # Support syntax for accessing hash contained in an array
        # if the array contains only one element
        if ( !is_array($found) || @{$found} != 1 ) {

          $self->get_logger->error(
            sub {
              sprintf "Cannot define [%s] component in [%s]: %s is an array\n"
                . "containing more than one element .. cannot access it as a hash\n",
                $_, $name, $found;
            }
          );

          return;
        }

        $found = $found->[0]->{$key};
      }

      next;
    }

    # illegal
    return;
  }

  $self->get_logger->debug(
    sub {
      return sprintf 'finally found [%s]', $found;
    }
  );

  return $value;
}

########################################################################
sub define_pebble {
########################################################################
  my ( $self, @args ) = @_;

  foreach my $p ( pairs @args ) {
    my ( $name, $value ) = @{$p};

    if ( exists $self->pebble_cache->{$name} ) {
      $self->get_logger->debug( sub { return sprintf 'redefining pebble: [%s]', $name } );
    }

    if ($value) {
      $self->store_pebble( $name, $value );
    }
    else {
      $self->get_logger->warn(
        sub {
          return sprintf 'undefined pebble: [%s]', $name;
        }
      );
    }
  }

  return;
}

# return the pebble text or the pebble name from cache or file
########################################################################
sub pebble {
########################################################################
  my ( $self, $pebble_name, @search_path ) = @_;

  if ( ref $pebble_name ) {
    foreach my $pebble ( keys %{ $self->pebble_cache } ) {
      return $pebble
        if $pebble_name == $self->pebble_cache->{$pebble}->{handle};
    }

    return 'anonymous';
  }

  my $pebble = $self->fetch_pebble($pebble_name);  # look in cache, first

  return $pebble
    if $pebble;

  my $file = $self->find_file( $pebble_name, '.peb', @search_path );

  die sprintf "could not find pebble: %s\n", $pebble_name
    if !$file;

  $pebble = $self->fetch_pebble( $pebble_name, $file );

  return $pebble;
}

########################################################################
sub config {
########################################################################
  my ( $self, @args ) = @_;

  my $ctx = $self->ctx // {};

  my $config = $ctx->config();

  return
    if !$config || !ref $config;

  return @args ? $config->{ $args[0] } : $config;

  return;
}

########################################################################
sub store_pebble {
########################################################################
  my ( $self, $name, $value ) = @_;

  $self->pebble_cache->{$name} //= {};

  $self->pebble_cache->{$name}->{name} = $name;

  my $var = ref $value ? 'handle' : 'value';

  $self->pebble_cache->{$name}->{$var} = $value;

  return $value;
}

########################################################################
sub pebble_cache {
########################################################################
  my ($self) = @_;

  $self->{PEBBLE_CACHE} //= {};

  return $self->{PEBBLE_CACHE};
}

########################################################################
sub fetch_pebble {
########################################################################
  my ( $self, $pebble, $pebble_file ) = @_;

  # see if pebble is in cache
  return $self->pebble_cache->{$pebble}->{value}
    if exists $self->pebble_cache->{$pebble};

  return
    if !$pebble_file || !-s $pebble_file;

  my $pebble_text = eval { slurp_file $pebble_file; };

  if ( !$pebble_text || $EVAL_ERROR ) {
    die $EVAL_ERROR;
  }

  # save in cache
  $self->store_pebble( $pebble, $pebble_text );

  return $pebble_text;
}

# find a file using a progression of locations to search
# returns the fqp or undef
########################################################################
sub find_file {
########################################################################
  my ( $self, $file, $ext, @search_paths ) = @_;

  my $logger = $self->get_logger;

  $logger->trace( Dumper( [ $file, $ext, \@search_paths ] ) );
  my $abs_paths = to_boolean( $self->config('ABSOLUTE_PATHS') );

  die "Absolute and relative paths not supported (set ABSOLUTE_PATHS=yes)\n"
    if !$abs_paths && $file =~ /^[\/.]|[.]{2}/xsm;  # Blocks '..' anywhere

  my $document_root = $ENV{DOCUMENT_ROOT} // getcwd;

  # ~ (tilde) means DOCUMENT_ROOT
  if ( $file =~ /^\~/xsm ) {
    $file =~ s/^\~/$document_root\//xsm;
    push @search_paths, $document_root;
    $logger->debug( sprintf 'using DOCUMENT_ROOT: [%s]', $document_root );
  }

  # if given an extension, then if the name does not have that
  # extension, add it
  if ( defined $ext && $file !~ /[.][^.]+$/xsm ) {
    $file .= $ext;
  }

  # we check for allowance of absolute paths above...so we're good
  return $file
    if $file =~ /^\//xsm && -e $file;

  @search_paths = map { split /:/xsm } grep {defined} @search_paths;

  $logger->debug( sprintf "searching for pebble: %s in:\n %s", $file, join "\n", @search_paths );

  @search_paths = map { $_ eq q{.} ? cwd : $_ } @search_paths;

  my ($path) = grep { -e $_ } map {"$_/$file"} @search_paths;

  return $path;
}

########################################################################
sub param {
########################################################################
  my ( $self, @args ) = @_;

  return $self->{VARS}->{ $args[0] }
    if scalar(@args) % 2;

  foreach my $p ( pairs @args ) {
    my ( $name, $value ) = @{$p};

    if ( $name =~ /[.]/oixsm ) {
      # more expensive resolve operation
      $self->define_name( $name, $value );
    }
    else {
      $self->{VARS}->{$name} = $value;
    }
  }

  return $self;
}

########################################################################
# Constructor -- Invoked as follows
#    $Class_or_Object->new( $Input_Handle [, $Output_Handle [, $Error_Handler ] ] )
#    $Class_or_Object->new( $file, $ctx );
# Return the constructed object
#
########################################################################
sub new {
########################################################################
  my ( $proto, $input, $output, $error_handler, %args ) = @_;

  my ( $class, $self );

  if ( $class = ref $proto ) {

    $self = bless { error => {} }, $class;

    if ($output) {
      $self->out_handle( TagX::Output->new($output) );
    }
    else {
      $self->out_handle( $proto->out_handle );
    }

    $self->{VARS}         = $proto->{VARS};
    $self->{error}        = $proto->{error}->newLevel($input);
    $self->{OPENTAG_EXPR} = $OPENTAG_EXPR;

    for (qw( DB options )) {
      next if !exists $proto->{$_};

      $self->{$_} = $proto->{$_};
    }
  }
  else {
    $self = bless {}, $proto;

    $self->out_handle( TagX::Output->new($output) );

    @{$self}{qw(VARS error DB OPENTAG_EXPR)} = ( TagX::Symtab->new, TagX::Error->new($input), {}, $OPENTAG_EXPR, );
  }

  if ( !ref $input ) {
    my $file_or_fd = $input;

    if ( ref $file_or_fd ) {
      $input = IO::Handle->new_from_fd($file_or_fd);
    }
    else {
      $input = IO::File->new($file_or_fd);
    }

    $self->{__input_opened} = $input;

    die sprintf "Unable to open [%s] $file_or_fd] for input: [%s]\n", $file_or_fd, $OS_ERROR
      if !$input;
  }

  $self->{input} = $input;
  $self->ctx($output);

  $self->check_disabled_tags;

  for ( keys %args ) {
    $self->{VARS}->{$_} = $args{$_};
  }

  return $self;
}

########################################################################
sub symtab {
########################################################################
  my ($self) = @_;

  return $self->{VARS};
}

########################################################################
# Hook for passing options to TagX.
# Takes an array which maps into a hash as arguments.
# valid options are
#   LOG_PREFIX          Directory and filename prefix for log file. An
#                       extension of `.log' is attached to this prefix
#   LOG_MAXSIZE         Maximum size of log file in bytes. If file is larger than
#                       this, it is truncated.
#   HEADER		Object for outputting headers.
#   PAGE                Filename of the Bedrock page
# Returns: Nothing.
########################################################################
sub options {
########################################################################
  my ( $self, %args ) = @_;

  if ( exists $args{HEADER} ) {
    $self->out_handle->header( delete $args{HEADER} );
  }

  my $options = $self->{options} // {};
  @{$options}{ keys %args } = values %args;

  $self->{options} = $options;

  $self->check_disabled_tags;

  return $self->{options};
}

########################################################################
sub check_disabled_tags {
########################################################################
  my ($self) = @_;

  my $options = $self->{options} // {};

  # disabled tags?
  my $ctx = $self->ctx;

  my $config = choose {
    return $options->{CONFIG}
      if $options->{CONFIG};

    return $ctx->config
      if blessed($ctx) && $ctx->can('config');

    return $ctx->{CONFIG}
      if is_hash($ctx);
  };

  return
    if !$config;

  my @disabled_tags = @{ $config->{DISABLED_TAGS} || [] };

  #  return
  #    if !@disabled_tags;

  my $tags = $self->{OPENTAG_EXPR};

  foreach my $t (@disabled_tags) {
    $tags =~ s/$t\|?//xsm;
  }

  $self->{OPENTAG_EXPR} = $tags;

  return;
}

########################################################################
sub getoption {
########################################################################
  my ( $self, $name ) = @_;

  return
    if !exists $self->{options};

  return $self->{options}->{$name};
}

########################################################################
sub _set_get {
########################################################################
  my ( $key, $self, @args ) = @_;

  if (@args) {
    $self->{$key} = $args[0];
  }

  return $self->{$key};
}

########################################################################
{
  ## no critic (RequireArgUnpacking)
  sub ctx        { unshift @_, 'ctx';        goto &_set_get; }
  sub out_handle { unshift @_, 'out_handle'; goto &_set_get; }
}

########################################################################
sub _get_cache_provider {
########################################################################
  my ($self) = @_;

  # Retrieve the Context object attached to the parser
  my $ctx = $self->ctx;

  # Look for the new generic 'cache' method [cite: 17]
  return $ctx->cache if $ctx && $ctx->can('cache');

  return;
}

########################################################################
sub is_cached {
########################################################################
  my ( $self, $key ) = @_;

  my $cache = $self->_get_cache_provider;

  # Delegate existence check to the provider [cite: 16]
  return $cache ? $cache->exists($key) : $FALSE;
}

########################################################################
sub read_cache {
########################################################################
  my ( $self, $key ) = @_;

  my $cache = $self->_get_cache_provider;

  return if !$cache;

  # The provider now handles the internal 'thaw' logic [cite: 16]
  return $cache->get($key);
}

########################################################################
sub write_cache {
########################################################################
  my ( $self, $key, $value, $ttl ) = @_;

  my $cache = $self->_get_cache_provider;

  if ($cache) {
    # The provider now handles the 'nfreeze' logic and TTL enforcement [cite: 16]
    $cache->set( $key, $value, ttl => $ttl );
  }
  else {
    $self->get_logger->warn('no cache provider available for write_cache');
  }

  return;
}

########################################################################
sub parse_range {
########################################################################
  my ( $self, $range ) = @_;

  my ( $start, $stop );

  if ( $range =~ /^(\d+)\D+(\d+)$/xsm ) {
    ( $start, $stop ) = ( $1, $2 );
  }

  return
    if !defined $start || !defined $stop || $start > $stop;

  return wantarray ? ( $start, $stop ) : [ $start .. $stop ];
}

########################################################################
sub close { ## no critic
########################################################################
  my ($self) = @_;

  # Close any database handles left behind
  my @keys = keys %{ $self->{DB} };

  for my $name (@keys) {
    my $dbh = $self->{DB}->{$name};

    if ( $dbh && $dbh->ping ) {
      my $rc = $dbh->disconnect;

      if ( !$rc ) {
        $self->get_logger( ref $self )->error(
          sub {
            sprintf "Error shutting down db connection [%s]\n\t[%s]\n", $name, $dbh->errstr;
          }
        );

      }
    }

    delete $self->{DB}->{$name};
  }

  # Clean up symbol table.
  $self->{VARS}->close;

  return delete $self->{VARS};
}

1;

## no critic

# TODO: document methods

__END__

=pod

=head1 NAME

Text::TagX - Bedrock's tag parser and support methods

=head1 SYNOPSIS

 use Bedrock;

 use Text::TagX;
 
 my $output = q{};

 my $fh = IO::Scalar->new(\$output);

 my $tx = Text::TagX->new($fh, $ctx);

 $tx->param( %parameters );

 my $error = eval { $tx->output; };

 die Dumper [ $tx->error ]
   if $EVAL_ERROR;

=head1 DESCRIPTION

Bedrock's tag parser - interprets and processes Bedrock tags to create
text output.

=head1 METHODS AND SUBROUTINES

Most of these routines are *only* used internally, but documented here
for completeness.

=head2 new

=head2 close

Closes any open database connections and cleans up symbol table.

=head2 check_disabled_tags

Disables Bedrock tags specified in the DISABLED_TAGS section of the
configuration file. This is useful for example if trying to use
Bedrock on a file that may contain tags that look like Bedrock tags
(but aren't). For example, if you try to use Bedrock to parse a page
that is a Bedrock XML file and it contains an <array> object, Bedrock
will interpret that as the <array> tag.

When running from the shell, use the C<--disable-tag> option. If you
want to disable the use of a tag for all pages or when running as a
web enabled application, create a DISABLED_TAGS object in the
F<tagx.xml> configuration file.

 <array name="DISABLED_KEYS">
   <scalar>array</scalar>
 </array>

=head2 config

 config([key])

Returns Bedrock's config object or the value of a key from the config
object.

=head2 ctx

Returns the L<Bedrock::Context> object.

=head2 error

=head2 find_file

=head2 getoption

=head2 options

Set or get the hash of options. Bedrock will set at least these
options when it parses a page:

=over 5

=item

=item * INCLUDE_DIR

=item * ALLOW_SNIPPET_TAG

=item * CONFIG

=back

=head2 out_handle

Set or get the output handle.

=head2 output

Bedrock's internal method to close the input stream and begin the
process of finalizing output.

=head2 param

 param(key, value, ...)
 param(key)

Set or get the value of a Bedrock variable.

=head2 parse

This is Bedrock's parser method. Its job is to parse content and
create a C<TagX::TAG::WithBody::Container> object which represents the
parsed input.

=head2 show_error

 show_error(error-file)

Outputs an error page. Called internally when an exception is raised.

=head2 symtab

Returns the symbol table of the current context. You don't really need
this! Use C<param> to export variables into the current frame.

 $tx->param($key, $value);

=head2 var_exists

 var_exists(var-name)

Returns a boolean indicating whether the specified variable exists in
Bedrock's current frame.

=head1 AUTHOR

J. Sridhar

Rob Lauer - <rlauer@treasurersbriefcase.com>

=head1 SEE ALSO

L<Text::TAG>, L<TagX::Symtab>

=cut
