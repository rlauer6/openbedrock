package Bedrock;

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2024, TBC Development Group, LLC.
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

use strict;
use warnings;

BEGIN {

  ######################################################################
  # Hack to get Bedrock, Bedrock/Text in @INC
  ######################################################################
  # - this was done because the modules were originally rooted at TagX
  #   and the files were never updated
  # - if Bedrock was installed somewhere other than installsitelib the
  #   you need to set PERL5LIB or @INC yourself
  ######################################################################
  use English qw(-no_match_vars);

  our $BEDROCK_DIST_DIR;

  if ( !$COMPILING ) {
    use List::Util qw(any);
    use File::ShareDir qw(dist_dir);

    my $dist_dir = eval { dist_dir('Bedrock'); };
    $BEDROCK_DIST_DIR = $dist_dir;

    if ($dist_dir) {
      $dist_dir =~ s/auto.*$//xsm;

      foreach my $bedrock_path (qw( Bedrock Bedrock/Text )) {
        next if any { $_ eq "$dist_dir/$bedrock_path" } @INC;
        next if any { $_ =~ /$bedrock_path\//xsm } @INC;

        next if !-d $bedrock_path;

        unshift @INC, $bedrock_path;
      }
    }
  }

}

use Bedrock::Cache;
use Bedrock::Constants qw(:booleans :chars %LOG4PERL_LOG_LEVELS);
use Carp qw(croak carp cluck);
use Config qw(config_sh);
use Cwd qw(getcwd abs_path);
use Data::Dumper;
use English qw(-no_match_vars);
use File::Copy;
use File::Find;
use File::Path qw(make_path);
use File::Temp qw(tempdir);
use JSON;
use List::Util qw(uniq min max any mesh);
use Scalar::Util qw(reftype blessed openhandle);

use parent qw(Exporter);

our $VERSION = '@PACKAGE_VERSION@';

our @EXPORT_OK = qw(
  cache
  choose
  create_temp_dir
  find_in_path
  grab_bag
  force_array
  force_hash
  is_array
  is_bedrock_array
  is_bedrock_hash
  is_blessed
  is_code
  is_hash
  is_regexp
  is_scalar
  muddle
  perl_config
  resolve_stream
  slurp_file
  slurp_json
  to_boolean
  to_loglevel
  to_regexp
  $BEDROCK_DIST_DIR
);

our %EXPORT_TAGS = (
  all  => [@EXPORT_OK],
  file => [
    qw(
      slurp_file
      find_in_path
      create_temp_dir
      resolve_stream
    ),
  ],
  booleans => [
    qw(
      is_array
      is_bedrock_array
      is_bedrock_hash
      is_blessed
      is_code
      is_hash
      is_regexp
      is_scalar
      to_boolean
    ),
  ],
);

{
  my $cache;

########################################################################
  sub cache {
########################################################################
    my (@args) = @_;

    cluck "usage: cache(options)\n" . Dumper( [ args => \@args ] )
      if ref $args[0];

    return $cache
      if $cache;

    return undef ## no critic
      if !$ENV{BEDROCK_CACHE_ENGINE};

    require Bedrock::Cache;

    # Pass options (like create => 1) to the constructor
    $cache = eval { return Bedrock::Cache->new(@args); };

    if ( !$cache || $EVAL_ERROR ) {
      warn "WARNING: could not initialize Bedrock::Cache provider:\n$EVAL_ERROR";
    }

    return $cache;
  }
}

########################################################################
sub resolve_stream {
########################################################################
  my ($thing) = @_;

  if ( !defined $thing ) {
    my $text = q{};
    $thing = \$text;
  }

  if ( !ref $thing && index( $thing, "\n" ) >= 0 ) {
    carp 'Argument looks like content (has newlines) but is not a reference. Assuming you meant \$scalar.';
    my $content = $thing;
    $thing = \$content;
    print {*STDERR} Dumper( [ reftype => reftype($thing) ] );
  }

  if ( ref $thing && reftype($thing) eq 'SCALAR' ) {
    open my $fh, '<', $thing
      or die "Could not open scalar ref: $OS_ERROR";

    return $fh;
  }

  return $thing
    if openhandle($thing);

  die "Invalid argument: must be filename, scalar ref, or open handle.\n"
    if ref $thing;

  die "File not found: $thing\n"
    if !-e $thing;

  open my $fh, '<', $thing
    or die "Cannot open $thing: $OS_ERROR\n";

  return $fh;
}

########################################################################
sub choose(&) { ## no critic
########################################################################
  my @result = shift->();

  return wantarray ? @result : $result[0];
}

########################################################################
sub grab_bag {
########################################################################
  my ( $source_bag, @keys_to_grab ) = @_;

  return
    if !@keys_to_grab;

  die "usage: grab_bag(hash-ref, list-o-things)\n"
    if !$source_bag || reftype($source_bag) ne 'ARRAY';

  my @little_bag_o_refs;

  # Using $_ to avoid shadowing $source_bag
  foreach ( @{$source_bag} ) {
    my @bag_o_items = @{$_}{@keys_to_grab};

    push @little_bag_o_refs, { mesh \@keys_to_grab, \@bag_o_items };
  }

  return @little_bag_o_refs;
}

########################################################################
sub is_array {
########################################################################
  my ($thing) = @_;

  return _is( $thing, 'ARRAY' );
}

########################################################################
sub muddle {
########################################################################
  my ($str) = @_;

  return $str
    if !$str || length($str) <= 2;

  # Match first char, middle bit, last char
  $str =~ s/^(.)(.+)(.)$/$1 . ('*' x length $2) . $3/exsm;

  return $str;
}

########################################################################
sub is_code {
########################################################################
  my ($thing) = @_;

  return _is( $thing, 'CODE' );
}

########################################################################
sub force_array {
########################################################################
  my (@args) = @_;

  if ( @args == 1 && is_array( $args[0] ) ) {
    return $args[0];
  }
  else {
    return [@args];
  }
}

########################################################################
sub force_hash {
########################################################################
  my (@args) = @_;

  if ( @args == 1 && is_hash( $args[0] ) ) {
    return $args[0];
  }
  else {
    return {@args};
  }
}

########################################################################
sub is_hash {
########################################################################
  my ($thing) = @_;

  return _is( $thing, 'HASH' );
}

########################################################################
sub is_scalar {
########################################################################
  my ($thing) = @_;

  return !ref $thing;
}

########################################################################
sub to_regexp {
########################################################################
  my ($regexp) = @_;

  return $regexp
    if is_regexp($regexp);

  if ( $regexp !~ /^qr/xsm ) {
    $regexp = "qr/$regexp/";
  }

  return eval "$regexp;"; ## no critic (ProhibitStringyEval)
}

########################################################################
sub to_loglevel {
########################################################################
  my ($level) = @_;

  $level //= 0;

  my @level_names = qw(error warn info debug trace);

  if ( $level =~ /^\d$/xsm ) {
    $level = min( $level, -1 + scalar @level_names );
    $level = $level_names[$level];
  }
  elsif ( any { lc $level eq $_ } @level_names ) {
    $level = lc $level;
  }
  else {
    $level = 'error';
  }

  return $LOG4PERL_LOG_LEVELS{$level};
}

########################################################################
sub is_regexp {
########################################################################
  my ($thing) = @_;

  return _is( $thing, 'Regexp' );
}

########################################################################
sub _is {
########################################################################
  my ( $thing, $type ) = @_;

  return ref $thing && ( reftype($thing) eq $type || ref $thing eq $type );
}

########################################################################
sub is_bedrock_object {
########################################################################
  my ( $thing, $type ) = @_;

  return ref($thing) && ref($thing) =~ /^Bedrock::$type$/xsm;
}

########################################################################
sub is_bedrock_hash {
########################################################################
  my ($thing) = @_;

  return is_bedrock_object $thing, 'hash';
}

########################################################################
sub is_bedrock_array {
########################################################################
  my ($thing) = @_;

  return is_bedrock_object $thing, 'array';
}

########################################################################
sub is_blessed {
########################################################################
  my ($thing) = @_;

  return ref $thing && blessed $thing;
}

########################################################################
# Usage      : find_in_path(options)
# Purpose    : return a list of paths to a file or file pattern
# Parameters : hash of options
#            :   pattern   => regexp to use for finding files
#            :   file      => file to find if not using a pattern
#            :   find_one  => max items to return
#            :   max_items => max items to return
#            :   path_list => array of paths to search (default: cwd)
# Returns    : in list context, list of files found
#            : in scalar context, number of files found
# Errors     : throws exception 'found' if finding just 1 file
########################################################################
sub find_in_path {
########################################################################
  my (%options) = @_;

  my ( $max_items, $file, $pattern ) = @options{qw{max_items file pattern}};

  if ( $options{find_one} ) {
    $max_items = 1;
  }

  my $path_list = $options{path_list};
  $path_list //= [getcwd];

  $path_list = is_array($path_list) ? $path_list : [$path_list];

  my @found_list;

  $pattern //= $file;

  my $cwd = getcwd;

  eval {
    find(
      { wanted => sub {
          return if /^[.]+$/xsm;

          my $found;

          if ( is_regexp($pattern) ) {
            $found = $_ =~ $pattern;
          }
          else {
            $found = $_ eq $pattern;
          }

          if ($found) {
            no warnings 'once'; ## no critic (ProhibitNoWarnings)

            push @found_list, abs_path $File::Find::name;

            die 'found'
              if $max_items && @found_list >= $max_items;
          }
        },
        no_chdir => $FALSE,
      },
      @{$path_list}
    );
  };

  my $err = $EVAL_ERROR;

  chdir $cwd;

  if ( $err && $err !~ /found/xsm ) {
    print {*STDERR} $err;

    @found_list = ();
  }

  return @found_list;
}

########################################################################
# Usage      : create_temp_dir( options )
# Purpose    : create a temp directory and possibly copy files to it
# Parameters : options => hash of arguments
#                cleanup  => remove directory when program terminates
#                dir      => array or name of directories to create
#                manifest => hash of arguments
#                  source   => source directory
#                  dest_dir => destination directory
#                  files    => array of file names
# Returns    : temporary directory created
# Errors     : throws exceptions if cannot create path or copy files
# Examples   : create_temp_dir( dir => 'foo')
#            : create_temp_dir( manifest => [ { files => [ 'foo.bar' ]} ]
########################################################################
sub create_temp_dir {
########################################################################
  my (%options) = @_;

  my $temp_dir = tempdir( CLEANUP => $options{cleanup} // $TRUE );

  my ( $manifest, $dir ) = @options{qw{manifest dir}};

  $manifest //= [];
  $dir      //= [];

  if ($dir) {
    my $dir_list = ref $dir ? $dir : [$dir];

    my @path_list
      = map {"$temp_dir/$_"} ( uniq @{$dir_list}, map { $_->{dest_dir} || () } @{$manifest} );

    if (@path_list) {
      my $paths_created = make_path(@path_list);

      die sprintf "could not create path(s): %s %s\n", $paths_created, join "$COMMA ", @path_list
        if $paths_created < @path_list;
    }
  }

  foreach my $file_list ( @{$manifest} ) {
    my ( $source, $dest_dir, $files )
      = @{$file_list}{qw{source dest_dir files}};

    $files    //= [];
    $source   //= getcwd;
    $dest_dir //= $EMPTY;  # just copy files to temp directory

    foreach my $file ( @{$files} ) {

      copy( "$source/$file", "$temp_dir/$dest_dir" );

      die "could not copy $source/$file $temp_dir/$dest_dir\n"
        if -e "$temp_dir/dest_dir/$file";
    }
  }

  return $temp_dir;
}

########################################################################
sub slurp_json {
########################################################################
  my ($file) = @_;

  return slurp_file( $file, $TRUE );
}

########################################################################
sub slurp_file {
########################################################################
  my ( $file, $json ) = @_;

  local $RS = undef;

  my $content;

  if ( openhandle $file ) {
    $content = <$file>;
  }
  else {
    open my $fh, '<', $file
      or die "could not open $file for reading: $OS_ERROR";

    $content = <$fh>;

    close $fh;
  }

  return JSON->new->decode($content)
    if $json;

  return wantarray ? split /\n/xsm, $content : $content;
}

########################################################################
sub to_boolean {
########################################################################
  my ($val) = @_;

  # undefined may trigger defaults, so return
  return
    if !defined $val;

  $val =~ s/^\s+|\s+$//xsmg;

  $val = lc $val;

  my %booleans = (
    true     => 1,
    enabled  => 1,
    disabled => 0,
    false    => 0,
    yes      => 1,
    y        => 1,
    n        => 0,
    no       => 0,
    on       => 1,
    off      => 0,
    '0'      => 0,
    '1'      => 1,
  );

  return $booleans{$val};
}

{
  my $perl_config;

########################################################################
  sub perl_config {
########################################################################
    my (@args) = @_;

    if ( !$perl_config ) {
      $perl_config = {};

      foreach my $c ( split /\n/xsm, config_sh() ) {
        my ( $var, $value ) = $c =~ /^([^=]+)=(.*)$/xsm;
        $value =~ s/^[']([^']+)[']$/$1/xsm;
        $perl_config->{$var} = $value;
      }
    }

    if ( @args == 1 ) {
      return $perl_config->{ $args[0] };
    }
    elsif (@args) {
      return @{$perl_config}{@args};
    }
    else {
      return $perl_config;
    }
  }
}

1;

__END__

## no critic (RequirePodSections)

=pod

=head1 PUBLIC

Bedrock - Set of useful utilities used throughout Bedrock

=head1 SYNOPSIS

 use Bedrock qw(:all);

 my $text = slurp_file 'foo.txt';

=head1 DESCRIPTION

This module contains a group of useful utilities used throughout
Bedrock. They are documented here so that you can use them too!

Including this module in your script will also add the necessary paths
to C<@INC> that will allow you to use other documented modules inside
of Bedrock.

=head2 Why Does Bedrock Hack @INC?

In short, the module hierarchy is deep. Modifying C<@INC> allows for
cleaner, more readable C<use> statements within the application logic:

 use TagX::TAG;

rather than...

 use Bedrock::Text::TagX::TAG;

...or even more deeply into the hierarchy...

 use TagX::TAG::WithBody::If;

rather than...

 use Bedrock::Text::TagX::TAG::WithBody::If;

=head1 EXPORT TAGS

=head2 :all

C<:booleans>, C<:file>, C<force_array>

=head2 :booleans

 is_array
 is_bedrock_array
 is_bedrock_hash
 is_blessed
 is_hash
 is_regexp
 is_scalar
 to_boolean

=head2 :file

 create_temp_file
 find_in_path
 resolve_stream
 slurp_file

=head1 EXPORTED VARIABLES

=head2 $BEDROCK_DIST_DIR

The absolute path to the installed Bedrock distribution directory (e.g.,
F</usr/local/share/perl/5.34/auto/share/dist/Bedrock-Core>).

This is determined dynamically via C<File::ShareDir> and is useful for
locating shared assets, default configuration files, or templates
shipped with the distribution.

  use Bedrock qw($BEDROCK_DIST_DIR);
  
  my $default_conf = "$BEDROCK_DIST_DIR/config/log4perl.conf";

=head1 METHODS AND SUBROUTINES

=head2 Methods on Objects

=head3 is_array

Equivalent to:

 ref $x && reftype($x) eq 'ARRAY'

=head3 is_bedrock_array

Equivalent to:

 ref $x && ref($x) eq 'Bedrock::Array'

=head3 is_bedrock_hash

Equivalent to:

 ref $x && ref($x) eq 'Bedrock::Hash'

=head3 is_blessed

Equivalent to:

 ref $x && blessed($x)

=head3 is_hash

Equivalent to:

 ref $x && reftype($x) eq 'HASH'

=head3 is_regexp

Equivalent to:

 ref $x && ref($x) eq 'Regexp'

=head3 is_scalar

Equivalent to:

 ref $x

=head2 Methods on Files

=head3 create_temp_dir

 create_temp_dir(options)

Creates a temporary directory (and optional sub-directories),
populating them with files specified in a manifest. Returns
the name of the temporary directory created.

=over 5

=item cleanup

Boolean that indicates that the directory and all files beneath it
should be removed when the program terminates.

=item manifest

Reference to an array of hash reference where each element of the hash
represent a set of files to be copied from a soruce to a sub-directory
of the temporary directory.

=over 10

=item source

Source of the files to copy.

=item dest_dir

Destination directory.  If omitted, files are copied to the root of
the temporary directory.

=item files

List of file names to copy.

=back

=item dir

=back

=head3 find_in_path

 find_in_path(options)

In list context, returns a list of absolute paths to files found in a
list of paths, filtered by a pattern or file name.  In scalar context,
returns the number of files found.

Either C<file> or C<pattern> is required. Options are described below:

=over 5

=item max_items

Maximum number of items to return.

=item find_one

Equivalent to max_items => 1.

=item path_list

An array reference to a list of paths to search. If no C<path_list> is
passed or the list is empty, the current working directory (and its
sub-directories) will be traversed.

=item file

The name of the file to search for.

=item pattern

A regular expression that will be used as a filter.

=back

Examples:

Find the first occurrence of file F<foo> in current working directory
( and sub-directories ).

 my ($file) = find_in_path(file => 'foo', max_items => 1);

Roughly equivalent to:

 $ find . -name foo

Find all files named F<foo> in a list of directories.

 my @files = find_in_path( file => 'foo', path_list => [ $ENV{PATH} ] );

Find all F<.xml> files in list of directories.

 my @xml_files = find_in_path(pattern => qr/[.]xml$/, path_list => [$ENV{CONFIG_PATH}]);

=head3 resolve_stream

 resolve_stream(filename|filehandle|text)

Returns a filehandle by examining the passed argument and determining
an action to take based on its type. Throws an exception if the file
or text reference cannot be opened for reading.

I<NOTE: This is a handy tool when you want to create a method or class
that requires a stream and you want to support multiple input types.>

=over 4

=item * filename => open the file and return a filehandle

=item * filehandle => return the passed argument

=item * text => open a handle to the text reference

I<NOTE: You should pass a reference to a scalar object (text), not a
text string. As a convenience, if you happen to forget and do not pass
a text reference AND it contains new lines, the method will store the
text in a scalar and return a handle to it. However, it will
emit a warning, as you should strictly pass scalar references for
content.>

=back

=head3 slurp_file

 slurp_file(filename|handle)

In scalar context returns entire contents of a file.  In list context
returns a list of the lines in the file. Throws an exception if the
file cannot be opened.

B<WARNING: If use this method and it returns an integer you likely
called the method in list context and assigned the result to a
scalar!>

=head3 slurp_json

 slurp_json(filename|handle)

Returns a Perl object from a JSON file.

=head2 MISCELLANEOUS

=head3 choose

 choose { ... }

Executes a block of code and returns the result. This utility is
particularly useful for creating a localized scope where a C<return>
statement will exit the block (returning a value to the caller of
C<choose>) rather than exiting the enclosing subroutine. It preserves
the calling context (scalar vs list).

  # $val gets the result of the logic block
  my $val = choose {
      return 'A' if $condition;
      return 'B';
  };

=head3 force_array

 force_array(@_)

Returns a reference to an array that contains the passed
parameter(s). If the passed value is already an array reference it is
simply returned. This method is useful for ensuring a value is a
reference or creating a new array reference from a list.

 my $array = force_array(@_);

=head3 grab_bag

 grab_bag( \@array_of_hashes, @keys )

Extracts specific keys from a list of hash references. Returns a new
list of anonymous hash references containing only the requested keys
and their values.

  my $users = [
    { id => 1, user => 'fred', password => '123' },
    { id => 2, user => 'wilma', password => '456' }
  ];

  # Returns: [ { user => 'fred' }, { user => 'wilma' } ]
  my $safe_list = grab_bag( $users, 'user' );

=head3 muddle

 muddle( $string )

Obfuscates a string by masking all characters except the first and the
last with asterisks. Useful for logging sensitive data like tokens or
passwords without revealing the full value. If the string is 2
characters or less, it is returned unchanged.

  # Returns 's****t'
  muddle('secret');

=head3 to_boolean

 to_boolean('yes')

Converts a string (case insensitive) that represents a boolean into a
0 or 1.

  true  => 1
  false => 0
  yes   => 1
  no    => 0
  on    => 1
  off   => 0
  '0'   => 0
  '1'   => 1

=head3 to_loglevel

 to_loglevel(level)

Converts a numeric verbosity level or string representing a log level
to a Log4perl log level.

Verbosity levels supported are:

 0 = error
 1 = warn
 2 = info
 3 = debug
 4 = trace

Any numeric level higher than 4 is treated as 'trace'.

String log levels (case insensitive):

 error
 warn
 info
 debug
 trace

Any string other than those above will be converted to the 'error' level.

=head3 to_regexp

Returns a compiled regular expression from a string of the forms:

 'some text'
 'qr/some text/'

Examples:

 my $re = to_regexp('^#');
 my $re = to_regexp('qr/^package\s[^;]+;$/xsm');

=head1 AUTHOR

Rob Lauer - <bigfoot@cpan.org>

=cut
