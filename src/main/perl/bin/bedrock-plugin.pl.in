#!/usr/bin/env perl

package Bedrock::Plugin::CLI;

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2026, TBC Development Group, LLC.
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

use strict;
use warnings;

use Archive::Tar qw(COMPRESS_GZIP);
use Bedrock qw(slurp_file :booleans choose toPascalCase);
use Bedrock::Constants qw(:chars :booleans);
use Carp;
use Cwd;
use Data::Dumper;
use English qw(-no_match_vars);
use File::Basename qw(basename);
use File::Temp qw(tempfile);
use List::Util qw(pairs);

our $VERSION = '@PACKAGE_VERSION@';

use parent qw(CLI::Simple);

caller or __PACKAGE__->main();

########################################################################
sub cmd_version {
########################################################################
  my ( $self, $donot_exit ) = @_;

  print <<"END_OF_VERSION";
bedrock-plugin v$VERSION
END_OF_VERSION

  return $SUCCESS;
}

########################################################################
sub cmd_create_plugin {
########################################################################
  my ($self) = @_;

  my $module  = $self->get_module;
  my $binding = $self->get_binding;
  my $plugin  = $self->get_plugin;

  croak "ERROR: no module name, usage: bedrock-plugin -m module-name\n"
    if !$module;

  my $module_name = $module;
  $module_name =~ s/\-/_/xsmg;
  my ($class_suffix) = reverse %{ toPascalCase($module_name) };

  $self->set_class_suffix($class_suffix);
  $self->set_class_name( $plugin  ? "BLM::$class_suffix"     : "BLM::Startup::$class_suffix" );
  $self->set_module_path( $plugin ? "BLM/${class_suffix}.pm" : "BLM/Startup/${class_suffix}.pm}" );

  if ( !$binding && !$plugin ) {
    $binding = lc $module_name;
  }
  elsif ($binding) {
    $binding = lc $binding;
  }

  $binding //= $EMPTY;
  $self->set_binding($binding);

  $self->set_module( ucfirst $module );

  $self->set_plugin_file( $self->create_plugin_file );

  if ( !$plugin ) {
    $self->set_config_file( $self->create_config_file );
  }

  $self->set_unit_test_file( $self->create_test_file );

  $self->create_tarball;

  return $SUCCESS;
}

########################################################################
sub create_tarball {
########################################################################
  my ($self) = @_;

  my $tar = Archive::Tar->new();

  my $module       = $self->get_module;
  my $class_suffix = $self->get_class_suffix;

  my $plugin_path = $self->get_plugin ? 'BLM' : 'BLM/Startup';

  my @files = (
    $self->get_plugin_file    => ( sprintf '%s/%s.pm',    $plugin_path, $class_suffix ),
    $self->get_config_file    => ( sprintf '%s.xml',      lc $module ),
    $self->get_unit_test_file => ( sprintf 't/00-%s.yml', lc $module ),
  );

  foreach my $p ( pairs @files ) {
    my ( $file, $name ) = @{$p};
    next if !$file;

    $tar->add_files($file);
    $tar->rename( $file, $name );
    unlink $file;
  }

  my $tarball = sprintf '%s.tgz', $class_suffix;

  if ( -e $tarball ) {
    unlink $tarball;
  }

  $tar->write( $tarball, COMPRESS_GZIP );

  return;
}

########################################################################
sub create_plugin_file {
########################################################################
  my ($self) = @_;

  my ( $fh, $tempfile ) = tempfile( 'plugin-XXXXX', UNLINK => $FALSE );

  my $module      = $self->get_module;
  my $author      = $self->get_author;
  my $email       = $self->get_email;
  my $class_name  = $self->get_class_name;
  my $suffix_name = $self->get_class_suffix;

  if ( $self->get_plugin ) {
    print {$fh} sprintf _create_plugin(), $class_name, $module, $suffix_name, $author, $email;
  }
  else {
    my $binding = $self->get_binding;
    print {$fh} sprintf _create_application_plugin(), $class_name, $class_name, $binding, $class_name, $author, $email;
  }

  close $fh;

  return $tempfile;
}

########################################################################
sub create_config_file {
########################################################################
  my ($self) = @_;

  my $xml = <<'END_OF_XML';
<object>
  <scalar name="binding">%s</scalar>
  <scalar name="module">%s</scalar>
  <object name="config">
  <!-- your config info here -->
  </object>
</object>
END_OF_XML

  my ( $fh, $tempfile ) = tempfile( 'config-XXXXX', UNLINK => $FALSE );

  print {$fh} sprintf $xml, $self->get_binding, $self->get_class_name;

  close $fh;

  return $tempfile;
}

########################################################################
sub create_test_file {
########################################################################
  my ($self) = @_;

  my $binding = $self->get_binding;
  my $module  = $self->get_module;

  my ( $fh, $tempfile ) = tempfile( 'test-XXXXX', UNLINK => $FALSE );

  my $class_name = $self->get_class_name;

  if ( !$self->get_plugin ) {
    my $unit_test = <<'END_OF_TEST';
---
name: %s
config:
  MODULES:
    - binding: %s
      module: %s
test: |
  <var $%s.version()>
result: 1.0.0
chomp: all
END_OF_TEST
    print {$fh} sprintf $unit_test, $class_name, $binding, $class_name, $binding;
  }
  else {
    my $unit_test = <<'END_OF_TEST';
name: %s
test: |
  <plugin:%s><var $_>
result: 1
chomp: all
END_OF_TEST
    print {$fh} sprintf $unit_test, $class_name, $self->get_class_suffix;
  }

  close $fh;

  return $tempfile;
}

########################################################################
sub _create_plugin {
########################################################################
  my $class = <<'END_OF_PLUGIN';
package %s;

use strict;
use warnings;

use Bedrock::Constants qw(:booleans);
use Data::Dumper;
use English qw(-no_match_vars);

use parent qw(BLM::Plugin);

our $VERSION = '1.0.0';

########################################################################
sub init_plugin {
########################################################################
  my ($self, @args) = @_;

  return $TRUE;
}

########################################################################
sub version {
########################################################################
  my ($self) = @_;

  return $VERSION;
}

1;

__END__
END_OF_PLUGIN

  my $pod = <<'END_OF_POD';
 =pod
 
 =head1 PUBLIC
 
 %s
 
 =head1 SYNOPSIS
 
  <plugin:%s args>
 
 =head1 DESCRIPTION
 
 =head1 SEE ALSO
 
 L<BLM::Plugin>
 
 =head1 AUTHOR
 
 %s - <%s>
 
 =cut
END_OF_POD

  $pod =~ s/^\s//xmsg;

  return sprintf "%s\n%s", $class, $pod;
}

########################################################################
sub _create_application_plugin {
########################################################################

  my $class = <<'END_OF_PLUGIN';
package %s;

use strict;
use warnings;

use Bedrock::Constants qw(:booleans);
use Data::Dumper;
use English qw(-no_match_vars);

use parent qw(Bedrock::Application::Plugin);

our $VERSION = '1.0.0';

########################################################################
sub init_plugin {
########################################################################
  my ($self) = @_;

  return $TRUE;
}

########################################################################
sub version {
########################################################################
  my ($self) = @_;

  return $VERSION;
}

1;

__END__
END_OF_PLUGIN

  my $pod = <<'END_OF_POD';
 =pod
  
 =head1 PUBLIC
 
 %s
 
 =head1 SYNOPSIS
 
  <var $%s.version()>
 
 =head1 DESCRIPTION
 
 This is the %s Application Plugin.
 
 =head1 METHODS AND SUBROUTINES
 
 =head1 SEE ALSO
 
 L<Bedrock::Application::Plugin>
 
 =head1 AUTHOR
 
 %s - <%s>
 
 =cut
END_OF_POD

  $pod =~ s/^\s//xmsg;

  return sprintf "%s\n%s", $class, $pod;
}

########################################################################
sub main {
########################################################################
  my %default_options = (
    author => 'Anonymouse',
    email  => 'anonymouse@example.org',
  );

  my @extra_options = qw(
    unit_test_file
    config_file
    plugin_file
    class_name
    class_suffix
    module_path
  );

  my @option_specs = qw(
    binding=s
    help
    module=s
    plugin
    author|a=s
    email|e=s
  );

  my %commands = (
    version => \&cmd_version,
    create  => \&cmd_create_plugin,
    default => \&cmd_create_plugin,
  );

  my $cli = Bedrock::Plugin::CLI->new(
    commands        => \%commands,
    option_specs    => \@option_specs,
    default_options => \%default_options,
    extra_options   => \@extra_options,
  );

  $cli->run();

  return 0;
}

1;

__END__

=pod

=head1 NAME

bedrock-plugin - create Bedrock plugin scaffolding

=head1 SYNOPSIS

bedrock-plugin --module foo 

=head1 DESCRIPTION

Provides a quick and easy way to create the scaffolding for a new
application plugin or plain plugin. The script creates a tarball
containing the plugin module, a unit test file and possibly a
configuration file (application plugins only).

=head1 Options
 
  -h, --help          this help
  -m, --module=NAME   plugin name
  -b, --binding=NAME  name of the variable to bind the plugin to. default: module name
  -p, --plugin        create a simple Plugin instead of an application plugin
  -a, --author        plugin author
  -e, --email         email of author

=head2 Examples

=over 4

=item * Create an application plugin

 bedrock-plugin.pl -m foo
 tar tfz Foo.tgz  
 BLM/Startup/Foo.pm
 foo.xml
 t/00-foo.yml

=item * Create a simple plugin

 bedrock-plugin -m foo -
 tar tfz Foo.tgz  
 BLM/Foo.pm
 t/00-foo.yml

=back 

=head2 Notes

=over 4

=item 1. Plugin names in lower or snake case (or hyphenated) are
converted to PascalCase.

=item 2. The F<.yml> unit test files are suitable for use with
C<bedrock-test.pl>.

 bedrock-plugin.pl -m foo
 tar xfvz Foo.tgz
 bedrock-test.pl -I . run t/00-foo.yml

=back

=head1 AUTHOR

Rob Lauer - <bigfoot@cpan.org>

=head1 SEE ALSO

L<Bedrock::Application::Plugin>, L<BLM::Plugin>

=cut
