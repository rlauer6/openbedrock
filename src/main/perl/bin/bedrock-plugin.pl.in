#!@PERL@

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2001, Charles Jones, LLC.
#    Copyright (C) 2024, TBC Development Group, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

use strict;
use warnings;

use Bedrock::Constants qw(:chars :booleans);
use Getopt::Long;
use Cwd;

use Role::Tiny::With;
with 'Bedrock::Logger';

our $VERSION = '0.02';
our $LIBDIR  = '@libdir@';

########################################################################
sub version {
########################################################################
  my ($donot_exit) = @_;

  print <<"END_OF_VERSION";
bedrock-plugin $VERSION
END_OF_VERSION

  exit if !$donot_exit;

  return;
}

########################################################################
sub help {
########################################################################
  print <<'END_OF_USAGE';
Usage: bedrock-plugin [OPTION...]

  -r, --replace       replace modules, files if they exist
  -b, --binding=NAME  name of the variable to bind the Plugin to
  -m, --module=NAME   name of perl module (ex: MyPlugin)
  -p, --plugin        create a simple Plugin instead of an Application Plugin

Examples:

  bedrock-plugin -r -m Foo

  bedrock-plugin -r -b foo

Creates the scaffolding for an Application or simple Plugin for
Bedrock. The files are created in your current working directory.

Creates the files:

  MODULE-NAME.pm      => perl module stub
  BINDING-NAME.xml    => application plugin configuration file
  BINDING-NAME.roc    => example test file
END_OF_USAGE

  exit;
}

########################################################################
sub main {
########################################################################
  my %options;

  my @option_specs = qw(
    binding=s
    help
    module=s
    no-test|x
    plugin
    replace
    version
  );

  my $success = GetOptions( \%options, @option_specs );

  if ( !$success || $options{help} ) {
    help();
  }

  if ( $options{version} ) {
    version();
  }

  my $logger = get_logger();

  $options{logger} = $logger;

  if ( !$options{module} && !$options{binding} ) {
    $logger->error('You must specify one of --module or --binding.');
    help();
  }

  if ( $options{binding} && $options{plugin} ) {
    $logger->warn(
      "no binding needed for Plugins. Only useful for Application Plugins...ignoring $options{binding}.");
    delete $options{binding};
  }

  if ( $options{plugin} && !$options{module} ) {
    $logger->error('You must supply a module name for Plugins.');
    exit;
  }
  else {
    if ( !$options{binding} && !$options{plugin} ) {
      $options{binding} = lc $options{module};
    }

    if ( !$options{module} ) {
      $options{module} = ucfirst $options{binding};
    }
  }

  my $plugin_type = $options{plugin} ? 'Plugin' : 'Application Plugin';

  my $plugin_path = $options{plugin} ? 'BLM' : 'BLM/Startup';

  my $test_file = $options{plugin} ? lc $options{module} : $options{binding};

  my $config_file = $options{plugin} ? $EMPTY : lc $options{module};

  my $binding = $options{binding} // $EMPTY;

  my $summary = <<"END_OF_SUMMARY";

\tOptions
\t-------
\tmodule:  [$options{module}]
\tbinding: [$binding]
\ttype:    [$plugin_type]
END_OF_SUMMARY

  $logger->info($summary);

  my $config = sprintf '%s.xml', $options{module};
  my $plugin = sprintf '%s.pm',  $options{module};
  my $test   = sprintf '%s.roc', $test_file;

  if ( !$options{replace} && ( -s $plugin || -s $config ) ) {
    $logger->error('plugin or config already exists. Specify -r to replace.');
    exit;
  }
  else {
    if ( -s $plugin ) {
      $logger->warn("$plugin will be replaced.");
    }

    if ( -s $config ) {
      $logger->warn("$config will be replaced.");
    }

    if ( !$options{'no-test'} && -s $test ) {
      $logger->warn("$test will be replaced.");
    }
  }

  create_plugin_file( $plugin, %options );

  create_config_file( $config, %options );

  create_test_file( $test, %options );

  if ( -s $plugin ) {
    $logger->info("created: $plugin");
  }

  if ( -s $config ) {
    $logger->info("created: $config");
  }

  if ( -s $test ) {
    $logger->info("created: $test");
  }

  my $copy_path = $options{plugin} ? 'BLM/' : 'BLM/Startup';

  $logger->info("Copy $options{module} to the $copy_path directory.\n");

  if ( !$options{plugin} ) {
    $logger->info("Copy $config to Bedrock's configuration directory.\n");
  }

  return 0;
}

########################################################################
sub create_plugin_file {
########################################################################
  my ( $plugin, %options ) = @_;

  open my $fh, '>', $plugin
    or die "Could not open $plugin for writing.";

  my $module = $options{module};

  if ($plugin) {
    print {$fh} sprintf _create_plugin(), $module, $module, $module;
  }
  else {
    print {$fh} sprintf _create_application_plugin(), $module, $options{binding}, $module, $module;
  }

  return close $fh;
}

########################################################################
sub create_config_file {
########################################################################
  my ( $config, %options ) = @_;

  return
    if $options{plugin};

  my $xml = <<'END_OF_XML';
<object>
  <scalar name="binding">%s</scalar>
  <scalar name="module">BLM::Startup::%s</scalar>
  <object name="config">
  </object>
</object>
END_OF_XML

  open my $fh, '>', $config
    or die "Could not open $config for writing.";

  print {$fh} sprintf $xml, $options{binding}, $options{module};

  return close $fh;
}

########################################################################
sub create_test_file {
########################################################################
  my ( $test, %options ) = @_;

  return
    if $options{'no-test'};

  open my $fh, '>', $test
    or die "Could not open $test for writing.\n";

  if ( $options{plugin} ) {
    my $module = $options{module};
    print {$fh} sprintf "<plugin:%s><var \$%s.version()>\n", $module, $module;
  }
  else {
    print {$fh} sprintf "<var \$%s.version()>\n", $options{binding};
  }

  return close $fh;
}

########################################################################
sub _create_plugin {
########################################################################
  return <<'END_OF_PLUGIN';
package BLM::%s;

use strict;
use warnings;

use parent qw(BLM::Plugin);

our $VERSION = 0.01;

########################################################################
sub init_plugin {
########################################################################
  my ($self, @args) = @_;

  return 1;
}

########################################################################
sub version {
########################################################################
  my ($self) = @_;

  return $VERSION;
}

=pod

=head1 PUBLIC

BLM::%s

=head1 SYNOPSIS

 <plugin:%s args>

=head1 DESCRIPTION

=head1 SEE ALSO

L<BLM::Plugin>

=head1 AUTHOR

=cut

1;

END_OF_PLUGIN
}

########################################################################
sub _create_application_plugin {
########################################################################

  return <<'END_OF_PLUGIN';
package BLM::Startup::%s;

use strict;
use warnings;

use parent qw(Bedrock::Application::Plugin);

our $VERSION = 1.0;

########################################################################
sub version {
########################################################################
  my ($self) = @_;

  return $VERSION;
}

=pod

=head1 PUBLIC

BLM::Startup::%s

=head1 SYNOPSIS

 <var $%s.version()>

=head1 DESCRIPTION

This is the %s Application Plugin.

=head1 METHODS AND SUBROUTINES

=head1 SEE ALSO

=head1 AUTHOR

=cut

1;
END_OF_PLUGIN
}

exit main();

1;
