#!@PERL@

#
#    This file is a part of Bedrock, a server-side web scripting tool.
#    Check out http://www.openbedrock.net
#    Copyright (C) 2001, Charles Jones, LLC.
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

use strict;
use warnings;

use vars
  qw/$LIST $CREATE $DELETE $HELP $SIZE %BEDROCK_CONFIG_CACHE $RESET $FILE/;

require IPC::Shareable;

use Getopt::Long;
use Carp;
use Data::Dumper;
use English qw{-no_match_vars};

sub usage {
  print <<'EOT';
usage: bedrock-cache.pl options

 Options:

   --create = create the cache
   --delete = delete the cache
   --file   = filename of a file to cache (experimental)
   --list   = list the contents of the cache
   --help   = this
   --reset  = clear the cache
   --size   = size of the cache in MB (default: 256)
 
EOT

  return;
}

# +--------------------------+
# | MAIN PROGRAM STARTS HERE |
# +--------------------------+

GetOptions(
  'list',   \$LIST, 'create', \$CREATE, 'delete', \$DELETE,
  'size',   \$SIZE, 'help|?', \$HELP,   'reset',  \$RESET,
  'file=s', \$FILE,
);

if ($HELP) {
  usage();

  exit 0;
}

if ( !$SIZE ) {
  $SIZE = $ENV{BEDROCK_CACHE_CONFIG_SIZE} || 256;
}

if ($CREATE) {
  $CREATE = 1;
}

# see if we have a cache defined
eval {
  {
    local $SIG{__WARN__} = sub { };

    ## no critic (ProhibitTies)
    tie %BEDROCK_CONFIG_CACHE, 'IPC::Shareable',
      {
      key     => 'BCFG',
      create  => $CREATE,
      destroy => 0,
      size    => 1024 * ($SIZE)
      };
  }
};

if ( !$EVAL_ERROR ) {

  $LIST && do {
    print Dumper( \%BEDROCK_CONFIG_CACHE );
    exit 0;
  };

  $DELETE && do {
    ( tied %BEDROCK_CONFIG_CACHE )->remove;
    exit 0;
  };

  $RESET && do {
    %BEDROCK_CONFIG_CACHE = ();
    exit 0;
  };

  $FILE && do {
    croak "file ($FILE) not found or empty\n"
      if !-e $FILE || !-s $FILE;

    $BEDROCK_CONFIG_CACHE{$FILE} = slurp_file $FILE;

    exit 0;
  };

  $CREATE && do {
    exit 0;
  };

  $SIZE && do {
    print {*STDERR} "error: size only matters with --create\n";
    exit 0;
  };

  usage();

  exit 0;
}
else {
  croak $EVAL_ERROR;
}

exit 0;

__END__

=pod

=head1 NAME

bedrock-cache.pl

=head1 SYNOPSIS

Create the cache...

 $ bedrock-cache.pl -c

List cache contents...

 $ bedrock-cache.pl -l

Delete the cache...

 $ bedrock-cache.pl -d

=head1 DESCRIPTION

The Bedrock I<cache> is currently used to cache the Bedrock
configuration object created by Bedrock when a Bedrock page is
accessed on an Apache virtual host.  Cacheing of the configuration is
optionally done when the Bedrock Apache handler begins to process a
page.

The I<cache> is implemented as shared memory using Perl's
C<IPC::Shareable> module.

Because the configuration object is created from potentially several
different XML-based configuration files that are merged together to
create the final object, startup time for the page may be adversely
affected.  One way to mitigate that startup time is to cache the
configuration object since configuration files are unlikely to change
very often for production applications.

Bedrock will automatically use the cache if it has been created (with
this utility for example) and the environment variable
C<BEDROCK_CACHE_CONFIG> has a value of "On".  This value is typically
set in the Apache virtual host configuration file using the C<SetEnv>
directive. 

 SetEnv BEDROCK_CACHE_CONFIG On

=head1 NOTES

For more gory details regarding Bedrock configuration file cacheing,
see the I<perldocs> for C<Bedrock::Handler>.

=head1 OPTIONS

=over 5

=item --delete

Deletes the shared memory object and removes the cache.

=item --file

Caches a file.  The key is the fully qualified path name of the file.
This is experimental and Bedrock does not currently use this in any
way shape or form.  It has been added to this utility for potential
future usage.

=item --list

Dumps the contents of the cache.

=item --help

Usage notes.

=item --reset

Clears the cache, but does not delete it.

=item --size

Sets the size of the cache.  Only valid when used with the C<--create> option.

=back

=head1 AUTHOR

Rob Lauer - <rlauer6@comcast.net>

=head1 SEE ALSO

C<IPC::Shareable>, C<Bedrock::Handler>

=cut
